Restless Leather Monkey

High

# A malicious Actor can claim a Higher amount than they deposited by calling Create order twice in the same timestamp

### Summary

Order IDs are generated by hashing the sender's address along with the current `block.timestamp`. However, this method poses a randomness problem because both `blockhash` and `block.timestamp` are not reliable sources of randomness. Malicious users can exploit this vulnerability by calling the function twice within the same timestamp. Since the Order ID remains the same in both cases, a user can submit the first order with a lower amount and then submit a second order with a significantly higher amount. This results in the second order overriding the details of the first one. The `Pending IDs` array only stores the Order ID without retaining any order details. Consequently, if the user cancels the order immediately, they potentially withdraw the maximum funds twice.

### Root Cause

Using Block.timestamp as a source of randomness instead of using a nonce or unique salt.

### Internal pre-conditions

1.  Attackers call Create Order twice within the same timestamp with a smaller amount first and a significant amount afterwards depositing both.


https://github.com/sherlock-audit/2024-11-oku/blob/main/oku-custom-order-types/contracts/automatedTrigger/StopLimit.sol#L300-L356


see hashing 
https://github.com/sherlock-audit/2024-11-oku/blob/main/oku-custom-order-types/contracts/automatedTrigger/AutomationMaster.sol#L89-L95


2. Attacker then cancels order to collect the Significant/ second deposit twice as if he deposited both differently.

https://github.com/sherlock-audit/2024-11-oku/blob/main/oku-custom-order-types/contracts/automatedTrigger/StopLimit.sol#L358-L377

### External pre-conditions

_No response_

### Attack Path

1.  Using block.timestamp as source of randomness

```solidity
 @audit >>.    ///@notice generate a random and unique order id
    function generateOrderId(address sender) external view override returns (uint96) {
        uint256 hashedValue = uint256(

 @audit >>.             keccak256(abi.encodePacked(sender, block.timestamp))

        );
        return uint96(hashedValue);
    }
```

2.  Creating multiple orders top overide the former amount

```solidity
 function _createOrder(
        uint256 stopLimitPrice,
        uint256 takeProfit,
        uint256 stopPrice,
        uint256 amountIn,
        IERC20 tokenIn,
        IERC20 tokenOut,
        address recipient,
        uint16 feeBips,
        uint16 takeProfitSlippage,
        uint16 stopSlippage,
        uint16 swapSlippage,
        bool swapOnFill
    ) internal {
        //verify both oracles exist, as we need both to calc the exchange rate
        require(
            address(MASTER.oracles(tokenIn)) != address(0x0) &&
                address(MASTER.oracles(tokenOut)) != address(0x0),
            "Oracle !exist"
        );
        require(
            pendingOrderIds.length < MASTER.maxPendingOrders(),
            "Max Order Count Reached"
        );
        require(
            takeProfitSlippage <= 10000 &&
                stopSlippage <= 10000 &&
                swapSlippage <= 10000 &&
                feeBips <= 10000,
            "BIPS > 10k"
        );

        MASTER.checkMinOrderSize(tokenIn, amountIn);

 @audit >>.         uint96 orderId = MASTER.generateOrderId(msg.sender);

 @audit >>.         orders[orderId] = Order({
            orderId: orderId,
            stopLimitPrice: stopLimitPrice,
            stopPrice: stopPrice,
            takeProfit: takeProfit,

 @audit >>.             amountIn: amountIn,
        
          tokenIn: tokenIn,
            tokenOut: tokenOut,
            takeProfitSlippage: takeProfitSlippage,
            feeBips: feeBips,
            stopSlippage: stopSlippage,
            swapSlippage: swapSlippage,
            recipient: recipient,
            direction: MASTER.getExchangeRate(tokenIn, tokenOut) >
                stopLimitPrice, //compare to stop price for this order's direction
            swapOnFill: swapOnFill
        });


 @audit >>.         pendingOrderIds.push(uint96(orderId));
      

  //emit
        emit OrderCreated(orderId);
    }
```

3. cancelling order will only check the order number since we still relies on the order id we can claim the second high amount 

```solidity
 function _cancelOrder(uint96 orderId) internal returns (bool) {
        Order memory order = orders[orderId];
        for (uint96 i = 0; i < pendingOrderIds.length; i++) {
            if (pendingOrderIds[i] == orderId) {
                //remove from pending array
                pendingOrderIds = ArrayMutation.removeFromArray(
                    i,
                    pendingOrderIds
                );
                order.tokenIn.safeTransfer(order.recipient, order.amountIn);

                //emit event
                emit OrderCancelled(orderId);

                //short circuit loop
                return true;
            }
        }
        return false;
    }
```


### Impact

An attacker can use this to exploit the contract and take out more fund than they deposited into the system.

### PoC

_No response_

### Mitigation

Add a nonce to this hash creation and increment it this ensures that there is intended randomness in the Orderid creation no matter the multi call from the attacker