Little Velvet Dragonfly

High

# Attackers can steal the funds by creating orders with the same order id.

### Summary

When users create orders, the order is calculated from the address of the sender and the `block.timestamp`. The problem is if users create two orders in the same block, the two orders will have the same order id and attackers can steal funds by utilizing this.

This issue exists in `Bracket.sol`, `Oracleless.sol` and `StopLimit.sol`

### Root Cause

`Bracket::_createOrder` function doesn't check if the generated orderId exists already.

https://github.com/sherlock-audit/2024-11-oku/blob/ee3f781a73d65e33fb452c9a44eb1337c5cfdbd6/oku-custom-order-types/contracts/automatedTrigger/Bracket.sol#L444

https://github.com/sherlock-audit/2024-11-oku/blob/ee3f781a73d65e33fb452c9a44eb1337c5cfdbd6/oku-custom-order-types/contracts/automatedTrigger/OracleLess.sol#L38

https://github.com/sherlock-audit/2024-11-oku/blob/ee3f781a73d65e33fb452c9a44eb1337c5cfdbd6/oku-custom-order-types/contracts/automatedTrigger/StopLimit.sol#L300

### Internal pre-conditions

N/A

### External pre-conditions

N/A

### Attack Path

1. The attacker creates two orders in the same block.
 - First order contains 100 usdc as `tokenIn`. (The minimum amount is assumed to be less than 100 here).
 - Second order contains 500 usdc as `tokenIn`. Then first order is overwritten with the second one but two same order ids will be added to the `pendingOrderIds`
2. Then the attacker cancels the order with order id twice and the attacker can withdraw 1000 usdc instead of 510 usdc.

The same thing can happen with the `performUpkeep` is called.
### Impact

The attackers can drain the funds from the contract.

### PoC
When users create orders `_createOrder` function is called in the end.
`currentValue` function is as follows:

```solidity

    File: Bracket.sol
444: function _createOrder(
445:         uint256 takeProfit,
446:         uint256 stopPrice,
447:         uint256 amountIn,
448:         uint96 existingOrderId,
449:         IERC20 tokenIn,
450:         IERC20 tokenOut,
451:         address recipient,
452:         uint16 feeBips,
453:         uint16 takeProfitSlippage,
454:         uint16 stopSlippage
455:     ) internal {
456:         //verify both oracles exist, as we need both to calc the exchange rate

475:         //generate random but unique order id if there is not an existing orderId from a stop limit order
476:         if (existingOrderId == 0) {
477:             existingOrderId = MASTER.generateOrderId(msg.sender);
478:         }
479: 
480:         //construct order
481:         orders[existingOrderId] = Order({
482:             orderId: existingOrderId,
483:             takeProfit: takeProfit,
484:             stopPrice: stopPrice,
485:             amountIn: amountIn,
486:             tokenIn: tokenIn,
487:             tokenOut: tokenOut,
488:             recipient: recipient,
489:             takeProfitSlippage: takeProfitSlippage,
490:             feeBips: feeBips,
491:             stopSlippage: stopSlippage,
492:             direction: MASTER.getExchangeRate(tokenIn, tokenOut) > takeProfit //exchangeRate in/out > takeProfit
493:         });
494: 
495:         //store pending order
496:         pendingOrderIds.push(existingOrderId);
497: 
498:         emit OrderCreated(existingOrderId);
499:     }

```
At [L477](https://github.com/sherlock-audit/2024-11-oku/blob/ee3f781a73d65e33fb452c9a44eb1337c5cfdbd6/oku-custom-order-types/contracts/automatedTrigger/Bracket.sol#L477), order id for the new order is generated.

And this is how the id is generated.

```solidity
File: AutomationMaster.sol
90: function generateOrderId(address sender) external view override returns (uint96) {
91:         uint256 hashedValue = uint256(
92:             keccak256(abi.encodePacked(sender, block.timestamp))
93:         );
94:         return uint96(hashedValue);
95:     }

```
This function only considers the sender and the `block.timestamp`, hence the two orders in the same block could have the same ids.

At [L481](https://github.com/sherlock-audit/2024-11-oku/blob/ee3f781a73d65e33fb452c9a44eb1337c5cfdbd6/oku-custom-order-types/contracts/automatedTrigger/Bracket.sol#L481) of the `_createOrder` function overwrites the order, however at [L496](https://github.com/sherlock-audit/2024-11-oku/blob/ee3f781a73d65e33fb452c9a44eb1337c5cfdbd6/oku-custom-order-types/contracts/automatedTrigger/Bracket.sol#L496), the same order is pushed to the `pendingOrderIds` again.

When users cancel the order, `_cancelOrder` function is called.

```solidity
File: Bracket.sol
501: function _cancelOrder(Order memory order) internal returns (bool) {
502:         for (uint96 i = 0; i < pendingOrderIds.length; i++) {
503:             if (pendingOrderIds[i] == order.orderId) {
504:                 //remove from pending array
505:                 pendingOrderIds = ArrayMutation.removeFromArray(
506:                     i,
507:                     pendingOrderIds
508:                 );
509: 
510:                 //refund tokenIn amountIn to recipient
511:                 order.tokenIn.safeTransfer(order.recipient, order.amountIn);
512: 
513:                 //emit event
514:                 emit OrderCancelled(order.orderId);
515: 
516:                 return true;
517:             }
518:         }
519:         return false;
520:     }
```

The function iterates the `pendingOrderIds` and tries to find out the `orderId`. Besides it doesn't remove the order from the `orders` mapping. As mentioned above, the same id can be appended to the `pendingOrderIds` twice and the attacker is able to cancel the order twice.

Also, attackers can benefit from this flaw in the `performUpkeep`. The attack path and the poc is similar to this.

### Mitigation

It is recommended to fix the function to check the existing order ids. And it would be better to remove the canceled orders from the `orders` mapping.

```diff
Bracket.sol
        function _createOrder(
            uint256 takeProfit,
            uint256 stopPrice,
            uint256 amountIn,
            uint96 existingOrderId,
            IERC20 tokenIn,
            IERC20 tokenOut,
            address recipient,
            uint16 feeBips,
            uint16 takeProfitSlippage,
            uint16 stopSlippage
        ) internal {


            //generate random but unique order id if there is not an existing orderId from a stop limit order
            if (existingOrderId == 0) {
                existingOrderId = MASTER.generateOrderId(msg.sender);
            }
+           require(orders[existingOrderId].amountIn == 0, "Existing id");            
            //construct order
            
        }

        function _cancelOrder(Order memory order) internal returns (bool) {
        for (uint96 i = 0; i < pendingOrderIds.length; i++) {
            if (pendingOrderIds[i] == order.orderId) {
                //remove from pending array
                pendingOrderIds = ArrayMutation.removeFromArray(
                    i,
                    pendingOrderIds
                );

                //refund tokenIn amountIn to recipient
+               require(order.amountIn != 0, "Zero amount");
                order.tokenIn.safeTransfer(order.recipient, order.amountIn);
+               delete orders[order.orderId];


                //emit event
                emit OrderCancelled(order.orderId);

                return true;
            }
        }
        return false;
    }

```