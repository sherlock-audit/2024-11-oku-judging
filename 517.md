Polished Graphite Wombat

High

# An attacker can create arbitrary orders for other users

## Summary
The `Bracket` or `OracleLess` contract, when creating an order, first deducts the user's token through the `procureTokens` function and then creates the order:

https://github.com/sherlock-audit/2024-11-oku/blob/main/oku-custom-order-types/contracts/automatedTrigger/OracleLess.sol#L38

```solidity
    function createOrder(
        IERC20 tokenIn,
        IERC20 tokenOut,
        uint256 amountIn,
        uint256 minAmountOut,
        address recipient,
        uint16 feeBips,
        bool permit,
        bytes calldata permitPayload
    ) external override returns (uint96 orderId) {
        //procure tokens
@>      procureTokens(tokenIn, amountIn, recipient, permit, permitPayload);

        //construct and store order
        orderId = MASTER.generateOrderId(recipient);
@>      orders[orderId] = Order({
            orderId: orderId,
            tokenIn: tokenIn,
            tokenOut: tokenOut,
            amountIn: amountIn,
            minAmountOut: minAmountOut,
            recipient: recipient,
            feeBips: feeBips
        });

        //store pending order
        pendingOrderIds.push(orderId);

        emit OrderCreated(orderId);
    }
```

The problem is that the `procureTokens` function allows deducting any user's tokens, and if the user does not execute `createOrder` immediately after approving the token to the current contract,
The attacker can execute the `createOrder` function and specify any parameters. The attacker can set a lower price and then complete the order.

```solidity
    function procureTokens(
        IERC20 token,
        uint256 amount,
        address owner,
        bool permit,
        bytes calldata permitPayload
    ) internal {
        if (permit) {
           .....
        } else {
@>           token.safeTransferFrom(owner, address(this), amount);
        }
    }
```


## Root Cause
The `procureTokens` function allows you to spend any of the tokens of an approved user, not` msg.sender`.

## Internal pre-conditions
The user approves the Bracket/OracleLess contract.

## External pre-conditions

## Attack Path

1. Alice is ready to create an order.
2. Alice first approvs the token to OracleLess/Bracket, but does not execute `createOrder` immediately.
3. The attacker knows from the query that Alice's token has been authorized to the OracleLess/Bracket contract.
4. The attacker immediately invokes the `createOrder` function, specifying the `recipient` as Alice. The attacker can set other parameters arbitrary.
5. The attacker sets a low price and immediately trades the order, and the attacker trades the order at a very low price.

## Impact
1. Create orders for other users with any parameters, The attacker can set a lower price and then complete the order.
2. Set large `feeBips`, resulting in loss of user funds.

## PoC

## Mitigation
```diff
    function procureTokens(
        IERC20 token,
        uint256 amount,
        address owner,
        bool permit,
        bytes calldata permitPayload
    ) internal {
        if (permit) {
           .....
        } else {
-            token.safeTransferFrom(owner, address(this), amount);
+            token.safeTransferFrom(msg.sender, address(this), amount);
        }
    }
```