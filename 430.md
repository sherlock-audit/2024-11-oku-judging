Bright Ultraviolet Starfish

High

# Orders will be overwritten if multiple orders are created by the same sender within the same block

### Summary

_No response_

### Root Cause

- The `generateOrderId` function lacks the nonce to differentiate between orders created by the same sender within the same block.

- As a result, orders will be overwritten if more than multiple orders are created by the same sender within the same block

- This issue can be easily remediated by having a nonce that increases whenever a new order is created.

```diff
function createOrder(
..SNIP..
+		orderIdNonce++;  
    //construct and store order
-    orderId = MASTER.generateOrderId(recipient);
+    orderId = MASTER.generateOrderId(recipient, orderIdNonce);
```

```diff
///@notice generate a random and unique order id
- function generateOrderId(address sender) external view override returns (uint96) {
+ function generateOrderId(address sender, uint256 _orderIdNonce) external view override returns (uint96) {
    uint256 hashedValue = uint256(
-        keccak256(abi.encodePacked(sender, block.timestamp))
+        keccak256(abi.encodePacked(sender, block.timestamp, _orderIdNonce))
    );
    return uint96(hashedValue);
}
```

### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

If Bob generated three (3) orders within the same block, then the order id generated will be the same since the inputs of the hashing function is simply the sender and the block's timestamp, which will be the same for all three (3) orders within the same block.

https://github.com/sherlock-audit/2024-11-oku/blob/main/oku-custom-order-types/contracts/automatedTrigger/AutomationMaster.sol#L90

```solidity
File: AutomationMaster.sol
89:     ///@notice generate a random and unique order id
90:     function generateOrderId(address sender) external view override returns (uint96) {
91:         uint256 hashedValue = uint256(
92:             keccak256(abi.encodePacked(sender, block.timestamp))
93:         );
94:         return uint96(hashedValue);
95:     }
```

Assume that each of the orders holds 1000 USD worth of `tokenIn` tokens. In this case, the first two (2) orders will be overwritten in the `orders` mapping by the third/last order at Line 53 below since the generated order ID will be the same. The order ID is used as the key for the `orders` mapping.

Since the first two (2) orders will be overwritten, the assets within them will also be lost, totaling 2000 USD worth of assets.

https://github.com/sherlock-audit/2024-11-oku/blob/main/oku-custom-order-types/contracts/automatedTrigger/OracleLess.sol#L53

```solidity
File: OracleLess.sol
38:     function createOrder(
39:         IERC20 tokenIn,
40:         IERC20 tokenOut,
41:         uint256 amountIn,
42:         uint256 minAmountOut,
43:         address recipient,
44:         uint16 feeBips,
45:         bool permit,
46:         bytes calldata permitPayload
47:     ) external override returns (uint96 orderId) {
48:         //procure tokens
49:         procureTokens(tokenIn, amountIn, recipient, permit, permitPayload);
50: 
51:         //construct and store order
52:         orderId = MASTER.generateOrderId(recipient);
53:         orders[orderId] = Order({
54:             orderId: orderId,
55:             tokenIn: tokenIn,
56:             tokenOut: tokenOut,
57:             amountIn: amountIn,
58:             minAmountOut: minAmountOut,
59:             recipient: recipient,
60:             feeBips: feeBips
61:         });
```

Above is only one of the attack vectors possible. Other attack vectors include the following, should be considered as duplicates of this issue due to shared root cause:

- David creates a new order via `OracleLess.createOrder` function, and set the `order.recipient` to himself. Bob (malicious user) can exploit the fact that the  `OracleLess.createOrder` function allows caller to define the `order.recipient` to an arbitrary address. Thus, he backruns David's transaction and attempts to overwrite his order, leading to a loss for David.

### Impact

High. Loss of assets.

### PoC

_No response_

### Mitigation

This issue can be easily remediated by having a nonce that increases whenever a new order is created.

```diff
function createOrder(
..SNIP..
+		orderIdNonce++;  
    //construct and store order
-    orderId = MASTER.generateOrderId(recipient);
+    orderId = MASTER.generateOrderId(recipient, orderIdNonce);
```

```diff
///@notice generate a random and unique order id
- function generateOrderId(address sender) external view override returns (uint96) {
+ function generateOrderId(address sender, uint256 _orderIdNonce) external view override returns (uint96) {
    uint256 hashedValue = uint256(
-        keccak256(abi.encodePacked(sender, block.timestamp))
+        keccak256(abi.encodePacked(sender, block.timestamp, _orderIdNonce))
    );
    return uint96(hashedValue);
}
```