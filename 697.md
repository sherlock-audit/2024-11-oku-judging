Mean Brown Barbel

High

# orderId is not unique which will lead to loss of funds for order creator or contract

### Summary

`createOrder()` is used to create order in OracleLess,StopLimit and Bracket contract. It creates orderId for an order and store the order details in mappings. which is used during filling of orders for getting the information of tokens,amount and other information. 
Due to issue in orderId creation current implementation overwrite multiple orders to the same order id. Which will lead to execution of unwanted trades and loss of funds 

### Root Cause

```solidity
    function createOrder(
        IERC20 tokenIn,
        IERC20 tokenOut,
        uint256 amountIn,
        uint256 minAmountOut,
        address recipient,
        uint16 feeBips,
        bool permit,
        bytes calldata permitPayload
    ) external override returns (uint96 orderId) {
        //procure tokens
        procureTokens(tokenIn, amountIn, recipient, permit, permitPayload);

        //construct and store order
        orderId = MASTER.generateOrderId(recipient);
        orders[orderId] = Order({
            orderId: orderId,
            tokenIn: tokenIn,
            tokenOut: tokenOut,
            amountIn: amountIn,
            minAmountOut: minAmountOut,
            recipient: recipient,
            feeBips: feeBips
        });

        //store pending order
        pendingOrderIds.push(orderId);

        emit OrderCreated(orderId);
    }

```
https://github.com/sherlock-audit/2024-11-oku/blob/main/oku-custom-order-types/contracts/automatedTrigger/OracleLess.sol#L38

`Master.generateOrderid(recipient)` is used for orderId which returns same orderID for recipient if the tx is sent at same block.timestamp which is very common

```solidity
    ///@notice generate a random and unique order id
    //@audit order id is not unique
    function generateOrderId(address sender) external view override returns (uint96) {
        uint256 hashedValue = uint256(keccak256(abi.encodePacked(sender, block.timestamp)));
        return uint96(hashedValue);
    }
```
https://github.com/sherlock-audit/2024-11-oku/blob/main/oku-custom-order-types/contracts/automatedTrigger/AutomationMaster.sol#L90


### Impact

The last submitted order by the user will be executed multiple time since it will be pushed multiple times in pendingOrder array but will read the same order details due to over writing. 

### PoC

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.19;

import "forge-std/Test.sol";
import "../src/automatedTrigger/OracleLess.sol";
import "../src/automatedTrigger/IAutomation.sol";
import "../src/automatedTrigger/AutomationMaster.sol";
import "../src/automatedTrigger/StopLimit.sol";
import "../src/interfaces/uniswapV3/IPermit2.sol";
import {MockERC20} from "./mocks/MockERC20.sol";
//import {IPythRelay} from "../src/oracle/IPythRelay.sol";
//import {MockPythRelay} from "./mocks/MockPythRelay.sol";



contract SherlockContestTest is Test {
    OracleLess public oracleLess;
    AutomationMaster public automationMaster;
    IPermit2 public permit2;
    StopLimit public stopLimit;
    // Test tokens
    MockERC20 public mockTokenIn;
    MockERC20 public mockTokenOut;
    MockPythRelay public mockOracleIn;
    MockPythRelay public mockOracleOut;

    // Test accounts
    address public owner;
    MaliciousTarget public maliciousTarget;

    function setUp() public {
        // Set up accounts
        owner = address(this);

        // Deploy dependencies
        automationMaster = new AutomationMaster();
        permit2 = IPermit2(deployPermit2()); // You'll need to implement this or use a mock

        // Deploy main contract
        oracleLess = new OracleLess(automationMaster, permit2);
        stopLimit = new StopLimit(automationMaster, IBracket(address(0)), permit2);

        // Deploy test tokens
        mockTokenIn = new MockERC20("Token A", "TKNA", 18);
        mockTokenOut = new MockERC20("Token B", "TKNB", 18);


        mockOracleIn = new MockPythRelay();
        mockOracleOut = new MockPythRelay();

        // Setup oracles in AutomationMaster
        IERC20[] memory tokens = new IERC20[](1);
        tokens[0] = IERC20(address(mockTokenIn));
        IPythRelay[] memory relays = new IPythRelay[](1);
        relays[0] = IPythRelay(address(mockOracleIn));
        automationMaster.registerOracle(tokens, relays);

        // Do the same for mockTokenOut
        tokens[0] = IERC20(address(mockTokenOut));
        relays[0] = IPythRelay(address(mockOracleOut));
        automationMaster.registerOracle(tokens, relays);

        automationMaster.setMaxPendingOrders(10);

        automationMaster.setMinOrderSize(1);
    }

    function deployPermit2() internal returns (address) {
        // You can either:
        // 1. Deploy a mock Permit2
        // 2. Fork mainnet and use the real Permit2 address
        // 3. Deploy the actual Permit2 contract
        // For this example, let's assume we're using a mock
        return address(0x1); // Replace with actual deployment
    }
       function test_createOrderAndOverwrite() public {
        // Setup initial tokens for Alice
        address alice = makeAddr("alice");
        uint256 initialAmount = 1000e18;
        mockTokenIn.mint(alice, initialAmount);
        vm.startPrank(alice);
        mockTokenIn.approve(address(oracleLess), type(uint256).max);

        // First order creation
        uint256 amountIn1 = 100e18;
        uint256 minAmountOut1 = 95e18;
        uint96 orderId1 = oracleLess.createOrder(
            IERC20(address(mockTokenIn)),
            IERC20(address(mockTokenOut)),
            amountIn1,
            minAmountOut1,
            alice,
            0, // feeBips
            false, // permit
            "" // permitPayload
        );

        // Assert state after first order
        assertEq(mockTokenIn.balanceOf(alice), initialAmount - amountIn1, "Alice's balance should be reduced");
        assertEq(mockTokenIn.balanceOf(address(oracleLess)), amountIn1, "OracleLess should have received tokens");
        assertEq(oracleLess.pendingOrderIds(0), orderId1, "First order should be in pending array");

        // Create second order with same recipient (will generate same orderId)
        uint256 amountIn2 = 200e18;
        uint256 minAmountOut2 = 190e18;
        uint96 orderId2 = oracleLess.createOrder(
            IERC20(address(mockTokenIn)), IERC20(address(mockTokenOut)), amountIn2, minAmountOut2, alice, 0, false, ""
        );

        // Create third order
        uint256 amountIn3 = 300e18;
        uint256 minAmountOut3 = 285e18;
        uint96 orderId3 = oracleLess.createOrder(
            IERC20(address(mockTokenIn)), IERC20(address(mockTokenOut)), amountIn3, minAmountOut3, alice, 0, false, ""
        );

        // Assert all orderIds are the same (since same recipient)
        assertEq(orderId1, orderId2, "OrderIds should be same for same recipient");
        assertEq(orderId2, orderId3, "OrderIds should be same for same recipient");

        // Get the final order details
        (
            uint96 _orderId,
            IERC20 tokenIn,
            IERC20 tokenOut,
            uint256 amountIn,
            uint256 minAmountOut,
            address recipient,
            uint16 feeBips
        ) = oracleLess.orders(orderId3);

        // Assert only the latest order details are stored
        assertEq(amountIn, amountIn3, "Amount should be from latest order");
        assertEq(minAmountOut, minAmountOut3, "MinAmountOut should be from latest order");

        // Assert total balance changes
        assertEq(
            mockTokenIn.balanceOf(alice),
            initialAmount - (amountIn1 + amountIn2 + amountIn3),
            "Alice's final balance should reflect all transfers"
        );
        vm.stopPrank();

        // earlier two orders can't be executed now since during execution, it fetches the order details from orders mapping
    }


}
```

run `test_createOrderAndOverwrite ` which shows that Alice submitted 3 different orders but the last order will be executed 3 times. 
A loss of funds eg
1. Alice submits a limit order for swapping 10WETH into USDC
2. Alice submits a limit order for swapping 50WETH into USDC
3. ALice submits a limit order for swapping 10USDC to WETH

for the above case 60WETH + 10USDC will be transferred from ALice address but she will end of getting 3 times swap of 10 USDC

## Mitigation

generate unique order Id. Adding nonce per user could be a good solution



