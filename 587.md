Mythical Tortilla Moose

High

# Missing reentrancy protection in the OracleLess contract can be used to drain the contract

### Summary

OracleLess contract doesn't implement any reentrancy protection, as well as the functions don't implement the reentrancy modifier which is utilized in the other contracts. This in a combination with a non-CEI pattern can be used to drain the contract with `fillOrder` and by utilizing a malicious swap target. 

### Root Cause

https://github.com/sherlock-audit/2024-11-oku/blob/ee3f781a73d65e33fb452c9a44eb1337c5cfdbd6/oku-custom-order-types/contracts/automatedTrigger/OracleLess.sol#L103-L148

Since `fillOrder` doesn't utilize any reentrancy protection, the pattern used isn't CEI compliant, combined with the fact that an arbitrary contract can be used to as a target, this can be utilized to drain the contract of its funds.

```solidity

 function fillOrder(uint96 pendingOrderIdx, uint96 orderId, address target, bytes calldata txData)
        external
        override
    {
        //fetch order
        Order memory order = orders[orderId];

        require(order.orderId == pendingOrderIds[pendingOrderIdx], "Order Fill Mismatch");

        //perform swap
        (uint256 amountOut, uint256 tokenInRefund) = execute(target, txData, order);

        //handle accounting
        //remove from array
        pendingOrderIds = ArrayMutation.removeFromArray(pendingOrderIdx, pendingOrderIds);

        //handle fee
        (uint256 feeAmount, uint256 adjustedAmount) = applyFee(amountOut, order.feeBips);
        if (feeAmount != 0) {
            order.tokenOut.safeTransfer(address(MASTER), feeAmount);
        }

        //send tokenOut to recipient
        order.tokenOut.safeTransfer(order.recipient, adjustedAmount);

        //refund any unspent tokenIn
        //this should generally be 0 when using exact input for swaps, which is recommended
        if (tokenInRefund != 0) {
            order.tokenIn.safeTransfer(order.recipient, tokenInRefund);
        }
    }

```

The problem arises because the `execute` part is before the order is removed from the array. This can be taken advantage of by a malicious actor which can re-enter the contract to execute a single order n-amount of times until the contract is drained.

### Internal pre-conditions

1. OracleLess doesn't utilize reentrancy protection.
2. The pending order is taken out of the `pendingOrderIds` array after it's executed;
3. An arbitrary contract can be used to perform the swap which can reenter the function and execute a single order n-amount of times, until the contract is drained.

### External pre-conditions

1. Malicious contract reenters the `fillOrder` function n-amount of times until the contract is drained.

### Attack Path

- Assume the OracleLess contract currently holds 100_000e6 in USDT which are the funds from the pending orders which are awaiting execution;
- Malicious user creates a pending order with a `tokenIn` value of 5_000e6 USDT; 
- The malicious actor has set the `minAmountOut` as 1 wei for example; 
- They call the `fillOrder` with their pending order in which their address is the `receiver`, as well as a malicious contract as the execution target, which will be used to reenter the `fillOrder` function in order to drain the contract.

`fillOrder` is called and it makes sure that the `orderId` and the `pendingOrderIds` match, i.e. order is still pending:

```solidity

function fillOrder(uint96 pendingOrderIdx, uint96 orderId, address target, bytes calldata txData)
        external
        override
    {
        //fetch order
        Order memory order = orders[orderId];

        require(order.orderId == pendingOrderIds[pendingOrderIdx], "Order Fill Mismatch");

```

After the check passes the swap will be executed using the arbitrary contract, as well as the arbitrary transaction data:

```solidity

        //perform swap
        (uint256 amountOut, uint256 tokenInRefund) = execute(target, txData, order);

```

The `execute` logic: 

```solidity

  function execute(address target, bytes calldata txData, Order memory order)
        internal
        returns (uint256 amountOut, uint256 tokenInRefund)
    {
        //update accounting
        uint256 initialTokenIn = order.tokenIn.balanceOf(address(this));
        uint256 initialTokenOut = order.tokenOut.balanceOf(address(this));

        //approve
        order.tokenIn.safeApprove(target, order.amountIn);

        //perform the call
        (bool success, bytes memory reason) = target.call(txData);

        if (!success) {
            revert TransactionFailed(reason);
        }

        uint256 finalTokenIn = order.tokenIn.balanceOf(address(this));
        require(finalTokenIn >= initialTokenIn - order.amountIn, "over spend");
        uint256 finalTokenOut = order.tokenOut.balanceOf(address(this));

        require(finalTokenOut - initialTokenOut > order.minAmountOut, "Too Little Received");

        amountOut = finalTokenOut - initialTokenOut;
        tokenInRefund = order.amountIn - (initialTokenIn - finalTokenIn);
    }

```
The malicious contract will take the 5_000 USDT from the contract, and then re-enter the contract by calling `fillOrder` again during the call: 
```solidity

  //perform the call
        (bool success, bytes memory reason) = target.call(txData);

```


Since the pending order was never taken out of the array prior to the execution, said checks would pass again: 


```solidity

 Order memory order = orders[orderId];

        require(order.orderId == pendingOrderIds[pendingOrderIdx], "Order Fill Mismatch");

        //perform swap
        (uint256 amountOut, uint256 tokenInRefund) = execute(target, txData, order);

```

This will be repeated 20-times (i.e. the order will be executed 20 times), until the 100_000e6 is drained. 

Then during the "last call" the following checks will record the new balance, which will be 5_000 before the last amount is taken out for the `tokenIn` or USDT and let's assume 0 for the tokenOut as well: 
```solidity

 uint256 initialTokenIn = order.tokenIn.balanceOf(address(this));
 uint256 initialTokenOut = order.tokenOut.balanceOf(address(this));

```

The malicious contract will return 2 wei which greater than the `minAmountOut` ,i.e. slippage, which is 1 wei.

After which we have these: 

```solidity

 uint256 finalTokenIn = order.tokenIn.balanceOf(address(this));
        require(finalTokenIn >= initialTokenIn - order.amountIn, "over spend");
        uint256 finalTokenOut = order.tokenOut.balanceOf(address(this));

        require(finalTokenOut - initialTokenOut > order.minAmountOut, "Too Little Received");


```

All checks will pass as the `initialTokenIn` recorded was 5_000, and that's how much we've taken, while the `finalTokenIn` which is the difference `order.amountIn`;

`finalTokenOut` is 2 wei, which is greater than the `minAmountOut`, and the last check will pass as well. 
The rest of the function will be executed;

### Impact

All funds from the OracleLess contract are stolen by executing a single order numerous times until the contract is drained.

### PoC

_No response_

### Mitigation

Include reentrancy protection on all functions in OracleLess as well.