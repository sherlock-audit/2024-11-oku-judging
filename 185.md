Warm Pear Hyena

High

# Bracket pending orders from users can be deleted by an attacker

## Summary
A malicious user can effectively unqueue/delete another users `Bracket` pending order.

## Vulnerability Details
In the `Bracket` contract - users call [createOrder()](https://github.com/sherlock-audit/2024-11-oku/blob/ee3f781a73d65e33fb452c9a44eb1337c5cfdbd6/oku-custom-order-types/contracts/automatedTrigger/Bracket.sol#L184-L213) to create their orders, during this process their orders are created, added to the `orders` mapping, and the `orderId` is pushed to the append only `pendingOrderIds`.

Orders can be filled by calling [fillOrder()](https://github.com/sherlock-audit/2024-11-oku/blob/ee3f781a73d65e33fb452c9a44eb1337c5cfdbd6/oku-custom-order-types/contracts/automatedTrigger/Bracket.sol#L147-L181) - but this must be called from the `StopLimit` contract. 

Alternatively, [Bracket::performUpKeep()](https://github.com/sherlock-audit/2024-11-oku/blob/ee3f781a73d65e33fb452c9a44eb1337c5cfdbd6/oku-custom-order-types/contracts/automatedTrigger/Bracket.sol#L85-L145) can be called to execute an order - this also deletes a pending order after execution:

```solidity
    function performUpkeep(
        bytes calldata performData
    ) external override nonReentrant {
        MasterUpkeepData memory data = abi.decode(
            performData,
            (MasterUpkeepData)
        );
        Order memory order = orders[pendingOrderIds[data.pendingOrderIdx]];

        require(
            order.orderId == pendingOrderIds[data.pendingOrderIdx],
            "Order Fill Mismatch"
        );

        //deduce if we are filling stop or take profit
        (bool inRange, bool takeProfit, ) = checkInRange(order);
        require(inRange, "order ! in range");

        ...

        (uint256 swapAmountOut, uint256 tokenInRefund) = execute(                           // @audit: reentrancy on cancel
            data.target,
            data.txData,
            order.amountIn,
            order.tokenIn,
            order.tokenOut,
            bips
        );

        //handle accounting
        //remove from pending array
        pendingOrderIds = ArrayMutation.removeFromArray(
            data.pendingOrderIdx,
            pendingOrderIds
        );

        ...
    }
```

`execute()`:

```solidity
    function execute(
        address target,
        bytes memory txData,
        uint256 amountIn,
        IERC20 tokenIn,
        IERC20 tokenOut,
        uint16 bips
    ) internal returns (uint256 swapAmountOut, uint256 tokenInRefund) {
        //update accounting
        uint256 initialTokenIn = tokenIn.balanceOf(address(this));
        uint256 initialTokenOut = tokenOut.balanceOf(address(this));

        //approve
        tokenIn.safeApprove(target, amountIn);

        //perform the call
        (bool success, bytes memory result) = target.call(txData);

        if (success) {
            uint256 finalTokenIn = tokenIn.balanceOf(address(this));
            require(finalTokenIn >= initialTokenIn - amountIn, "over spend");
            uint256 finalTokenOut = tokenOut.balanceOf(address(this));

            //if success, we expect tokenIn balance to decrease by amountIn
            //and tokenOut balance to increase by at least minAmountReceived
            require(
                finalTokenOut - initialTokenOut >
                    MASTER.getMinAmountReceived(
                        amountIn,
                        tokenIn,
                        tokenOut,
                        bips
                    ),
                "Too Little Received"
            );

            swapAmountOut = finalTokenOut - initialTokenOut;
            tokenInRefund = amountIn - (initialTokenIn - finalTokenIn);
        } else {
            //force revert
            revert TransactionFailed(result);
        }
    }
```

Upkeep can only be performed if an order is in range, this can still be leveraged by an attacker as an attacker can create an order that will always be in range, for example - an order setting `stopPrice` and `takeProfit` to the current price of the token.

### Attack Path
1. Malicious user sees the victim's pending create order transaction in the mempool.
2. Attacker frontruns their transaction, ensuring their own create order transaction (with both slippage types set to max) from their contract executes just before the victim's own transaction. The attacker's transaction is at index `n` in the `pendingOrderIds` array, while the victim's is at `n + 1`.
3. Now the attacker calls `performUpkeep()` on his order created in the previous step, but sets the `target` to their contract.
4. From the attacker's contract, `cancelOrder()` is called and executes successfully, at this point the attacker's pending order at index `n` is removed, with the victim's transaction now being at `n` instead of `n+1` due to mutation. The attacker's original token amount is also refunded back.
5. During this call, the attacker's contract would send just `1 wei` of the output token so the rest of the checks pass.
6. The remainder of `performUpkeep` now executes with the `pendingOrderIdx` which originally pointed to the attacker's transaction, now pointing to the victim's due to the reentrancy + cancellation:

> Note: setting slippage to the max is important to get 0 at all times, this is allowed and not checked by the code.

```solidity
    function performUpkeep(
        bytes calldata performData
    ) external override nonReentrant {
        ...

        //handle accounting
        //remove from pending array
        pendingOrderIds = ArrayMutation.removeFromArray(
            data.pendingOrderIdx,
            pendingOrderIds
        );

        //handle fee
        (uint256 feeAmount, uint256 adjustedAmount) = applyFee(
            swapAmountOut,
            order.feeBips
        );

        if (feeAmount != 0) {
            order.tokenOut.safeTransfer(address(MASTER), feeAmount);
        }

        //send tokenOut to recipient
        order.tokenOut.safeTransfer(order.recipient, adjustedAmount);

        //refund any unspent tokenIn
        //this should generally be 0 when using exact input for swaps, which is recommended
        if (tokenInRefund != 0) {
            order.tokenIn.safeTransfer(order.recipient, tokenInRefund);
        }

        //emit
        emit OrderProcessed(order.orderId);
    }
```

The rest of the code runs, and the unsuspecting user's pending order is now removed. No reverts occur.

## Impact
A user's order can be removed by an attacker, effectively censoring that user at little to no cost (`1 wei` of any currency).

## Recommendation
Add a `nonReentrant` modifier to [cancelOrder](https://github.com/sherlock-audit/2024-11-oku/blob/ee3f781a73d65e33fb452c9a44eb1337c5cfdbd6/oku-custom-order-types/contracts/automatedTrigger/Bracket.sol#L309-L313).