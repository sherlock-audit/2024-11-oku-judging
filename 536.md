Polished Graphite Wombat

High

# An attacker can perform a reentrancy attack in OracleLess::fillOrder() to steal tokens from the contract.

## Summary
Since none of the functions in the OracleLess contract are protected with nonReentrant, it is vulnerable to reentrancy attacks, allowing an attacker to steal tokens from the contract.


## Root Cause
The OracleLess contract does not apply nonReentrant protection to its functions and relies on the difference in balanceOf(address(this)) to determine the number of tokens received.

https://github.com/sherlock-audit/2024-11-oku/blob/ee3f781a73d65e33fb452c9a44eb1337c5cfdbd6/oku-custom-order-types/contracts/automatedTrigger/OracleLess.sol#L103
```javascript
function fillOrder(
        uint96 pendingOrderIdx,
        uint96 orderId,
        address target,
        bytes calldata txData
@>    ) external override {
        //fetch order
        Order memory order = orders[orderId];

        require(
            order.orderId == pendingOrderIds[pendingOrderIdx],
            "Order Fill Mismatch"
        );

        //perform swap
@>        (uint256 amountOut, uint256 tokenInRefund) = execute(
            target,
            txData,
            order
        );
    ......
        
```
It can be observed that fillOrder is not protected by the nonReentrant modifier.
```javascript
   function execute(
        address target,
        bytes calldata txData,
        Order memory order
    ) internal returns (uint256 amountOut, uint256 tokenInRefund) {
        //update accounting
        uint256 initialTokenIn = order.tokenIn.balanceOf(address(this));
        uint256 initialTokenOut = order.tokenOut.balanceOf(address(this));

        //approve
        order.tokenIn.safeApprove(target, order.amountIn);

        //perform the call
        (bool success, bytes memory reason) = target.call(txData);

        if (!success) {
            revert TransactionFailed(reason);
        }

        uint256 finalTokenIn = order.tokenIn.balanceOf(address(this));
        require(finalTokenIn >= initialTokenIn - order.amountIn, "over spend");
        uint256 finalTokenOut = order.tokenOut.balanceOf(address(this));

        require(
            finalTokenOut - initialTokenOut > order.minAmountOut,
            "Too Little Received"
        );

    @>    amountOut = finalTokenOut - initialTokenOut;
        tokenInRefund = order.amountIn - (initialTokenIn - finalTokenIn);
    }
```
In the execute function, the quantity of tokenOut is determined using the difference in tokenOut.balanceOf(address(this)). This creates an opportunity for a reentrancy attack.

An attacker can exploit this by invoking createOrder() within target.call(), creating a new order that artificially increases the tokenOut balance of address(this) by minAmountOut.

This allows the attacker to complete the fillOrder() operation and then retrieve the previously added minAmountOut by canceling the new order through cancelOrder().


## Internal pre-conditions


## External pre-conditions
There are orders in the OracleLess contract.

## Attack Path
For example, there is an order with tokenIn as 1 WETH and tokenOut as minAmountOut = 3000 USDT.
	1.	The attacker executes fillOrder(), setting the target parameter to the attack contract.
	2.	Inside the attack contract, 1 WETH is transferred from the OracleLess contract.
	3.	Inside the attack contract, createOrder() is called with tokenIn = 3000 USDT and tokenOut = minAmountOut = type(uint256).max WETH.
	•	Since createOrder() transfers 3000 USDT from the target(attack contract)  to the OracleLess contract, the checks within fillOrder() pass successfully.
	4.	After fillOrder() completes, the attacker calls cancelOrder() to retrieve the 3000 USDT deposited earlier.

Result:

The attacker profits 1 WETH.

Note:

This exploit can be completed within a single transaction.
## Impact
After stealing tokens from the OracleLess contract, the victim is unable to retrieve their tokens from the OracleLess contract (an “insufficient balance” error occurs when executing cancelOrder()).

## PoC

## Mitigation
Add nonReentrant protection.