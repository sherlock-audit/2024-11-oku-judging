Real Ruby Turtle

High

# Malicious users can steal from the protocol and freeze other users' funds

### Summary

In Bracket.sol and Oracleless.sol users can call performUpkeep() and fillOrder() with any data and cancelOrder() lacks reentrancy guard, opening up ways to attack.

### Root Cause

I think the root cause of this would be the lack of `nonReentrant` modifier on cancelOrder() in Bracket.sol and Oracleless.sol as the attack would not be possible if it was in place.
```solidity
    function performUpkeep(
        bytes calldata performData
    ) external override nonReentrant {
        MasterUpkeepData memory data = abi.decode(
            performData,
            (MasterUpkeepData)
        );
        Order memory order = orders[pendingOrderIds[data.pendingOrderIdx]];
         ...

        (uint256 swapAmountOut, uint256 tokenInRefund) = execute(
            data.target,
            data.txData,
            order.amountIn,
            order.tokenIn,
            order.tokenOut,
            bips
        );

        //handle accounting
        //remove from pending array
        pendingOrderIds = ArrayMutation.removeFromArray(
            data.pendingOrderIdx,
            pendingOrderIds
        );
        ...
        //send tokenOut to recipient
        order.tokenOut.safeTransfer(order.recipient, adjustedAmount);

        //refund any unspent tokenIn
        //this should generally be 0 when using exact input for swaps, which is recommended
        if (tokenInRefund != 0) {
            order.tokenIn.safeTransfer(order.recipient, tokenInRefund);
        }
    }
```
https://github.com/sherlock-audit/2024-11-oku/blob/main/oku-custom-order-types/contracts/automatedTrigger/Bracket.sol#L85-L145
```solidity
    function execute(
        address target,
        bytes memory txData,
        uint256 amountIn,
        IERC20 tokenIn,
        IERC20 tokenOut,
        uint16 bips
    ) internal returns (uint256 swapAmountOut, uint256 tokenInRefund) {
        //update accounting
        uint256 initialTokenIn = tokenIn.balanceOf(address(this));
        uint256 initialTokenOut = tokenOut.balanceOf(address(this));

        //approve
        tokenIn.safeApprove(target, amountIn);

        //perform the call
>       (bool success, bytes memory result) = target.call(txData);

        if (success) {
            ...
        } else {
            //force revert
            revert TransactionFailed(result);
        }
    }
```
https://github.com/sherlock-audit/2024-11-oku/blob/main/oku-custom-order-types/contracts/automatedTrigger/Bracket.sol#L526-L568
```solidity
    function cancelOrder(uint96 orderId) external {
        Order memory order = orders[orderId];
        require(msg.sender == order.recipient, "Only Order Owner");
        require(_cancelOrder(order), "Order not active");
    }
```
https://github.com/sherlock-audit/2024-11-oku/blob/main/oku-custom-order-types/contracts/automatedTrigger/Bracket.sol#L309-L313
```solidity
    function _cancelOrder(Order memory order) internal returns (bool) {
        for (uint96 i = 0; i < pendingOrderIds.length; i++) {
            if (pendingOrderIds[i] == order.orderId) {
                //remove from pending array
                pendingOrderIds = ArrayMutation.removeFromArray(
                    i,
                    pendingOrderIds
                );

                //refund tokenIn amountIn to recipient
                order.tokenIn.safeTransfer(order.recipient, order.amountIn);

                //emit event
                emit OrderCancelled(order.orderId);

                return true;
            }
        }
        return false;
    }
```
https://github.com/sherlock-audit/2024-11-oku/blob/main/oku-custom-order-types/contracts/automatedTrigger/Bracket.sol#L501-L520


### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

1. Malicious user creates an order specifying themselves as recipient
2. Malicious user calls performUpkeep() in Bracket.sol or fillOrder() in Oracleless.sol specifying themselves as target
3. The contract calls the malicious user
4. Malicious user reenters to the contract's cancelOrder() with their order ID, receiving the initial funds and deleting their order
5. Malicious contract then sends the tokenOut amount and pulls tokenIn amount from the protocol
6. The protocol deletes another order of a different user because the same pendingOrderIdx now points to a different Id in the array
7. The protocol sends the intended tokenOut amount.

The malicious user basically sends tokenIn once but receives tokenOut and twice the tokenIn amount, essentially stealing from the protocol/users while also deleting another order that might not be theirs which will freeze other users' tokens.

### Impact

Malicious users could steal all tokens from the protocol while also deleting other users' orders in a way that freezes their assets.

### PoC

_No response_

### Mitigation

I think this attack can be prevented if a `nonReentrant` modifier is added to cancelOrder().