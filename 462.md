Elegant Jade Chameleon

High

# "Fresh price" returned by `PythOracle` will cause a significant denial of service in the Oku automation system

High

### Summary

The use of the wrong comparison operator when checking if the price returned by oracles is fresh in `PythOracle.sol` `PythOracle::currentValue` will cause this function to interpret fresh prices as stale, and vice versa. This would prevent swaps from going through when the price is actually fresh, as all functions dependent on it would fail. 
These dependent functions are `AutomationMaster::checkMinOrderSize`, `AutomationMaster::getExchangeRate` and by extension, every contract function dependent on these automation functions.

### Root Cause

In [PythOracle: Line 29](https://github.com/sherlock-audit/2024-11-oku/blob/main/oku-custom-order-types/contracts/oracle/External/PythOracle.sol#L29) a `<` is used to check that the price returned is still fresh, this should not be the case as a `>` should be used instead.

### Internal pre-conditions

nil

### External pre-conditions

nil

### Attack Path

No attack path. Issue with the system.

### Impact

The users' orders cannot be processed.

### PoC
```solidity
//SPDX-License-Identifier:MIT
pragma solidity ^0.8.19;

import {Test, console2} from "forge-std/Test.sol";
import {MockPyth} from "./Mocks/MockPyth.sol";
import {PythOracle, IPyth, IPythRelay} from "../../contracts/oracle/External/PythOracle.sol";
import {IERC20} from "../../contracts/interfaces/openzeppelin/IERC20.sol";
import {TestERC20} from "./TestERC20.sol";

contract TestForge is Test {
    MockPyth pyth;
    PythOracle pythOracleIn;
    bytes32 TOKEN_IN_PRICE_FEED_ID = bytes32(uint256(0x1));

    IERC20 tokenIn;

    uint256 noOlderThan = 60;
    address owner = makeAddr("owner");

    function setUp() public {
        pyth = new MockPyth(60, 1);
        address underlyingIn = address(tokenIn);
        pythOracleIn = new PythOracle(IPyth(address(pyth)), TOKEN_IN_PRICE_FEED_ID, noOlderThan, underlyingIn);

        //so that the publish time is 90
        vm.warp(90);
        //initial price of tokenIn set to 3000
        setTokenPrice(3000, TOKEN_IN_PRICE_FEED_ID);
    }
    
    //oracle price helper functions
    function createTokenUpdate(int64 price, bytes32 id) private view returns (bytes[] memory) {
        bytes[] memory updateData = new bytes[](1);
        updateData[0] = pyth.createPriceFeedUpdateData(
            id,
            price * 1e8, // price
            10 * 1e8, // confidence
            -8, // exponent
            price * 1e8, // emaPrice
            10 * 1e8, // emaConfidence
            uint64(block.timestamp) // publishTime
        );

        return updateData;
    }

    function setTokenPrice(int64 price, bytes32 id) private {
        bytes[] memory updateData = createTokenUpdate(price, id);
        uint256 value = pyth.getUpdateFee(updateData);
        vm.deal(address(this), value);
        pyth.updatePriceFeeds{value: value}(updateData);
    }

    modifier movingTime() {
        //move block.timestamp forward to 100. just over price publishTime
        vm.warp(100);
        _;
    }

    function testOracleDoesReturnStalePrice2() public movingTime {
        IPyth.Price memory priceStruct = pyth.getPrice(TOKEN_IN_PRICE_FEED_ID);

        //price gets interpreted as stale when it should be fresh
        vm.expectRevert("Stale Price");
        uint256 freshPrice = pythOracleIn.currentValue();
        console2.log("No older than: ", noOlderThan);
        console2.log("Fresh price. Block timestamp: ", block.timestamp);
        console2.log("Fresh price. Price publish time: ", priceStruct.publishTime);

        //gets interpreted as fresh when it should be stale
        vm.warp(160); //moves block.timestamp to 160
        uint256 stalePrice = pythOracleIn.currentValue(); //Oku pythOracle function returns stale price
        console2.log("No older than: ", noOlderThan);
        console2.log("Stale price. Block timestamp: ", block.timestamp);
        console2.log("Stale price. Price publish time: ", priceStruct.publishTime);
        console2.log("Oku oracle returns stale price: ", stalePrice);
    }
}
```

### Mitigation

```diff
 function currentValue() external view override returns (uint256) {
        IPyth.Price memory price = pythOracle.getPriceUnsafe(tokenId);
        require(
-           price.publishTime < block.timestamp - noOlderThan,
+          price.publishTime > block.timestamp - noOlderThan,
            "Stale Price"
        );
        return uint256(uint64(price.price));
    }
```