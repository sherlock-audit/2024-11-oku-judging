Young Vanilla Pelican

Medium

# OracleLess.sol :: fillOrder() users may receive less than the specified minAmountOut because the check is performed before applying the fees.

### Summary

`fillOrder()` is used to execute an order. The issue is that the `minAmountOut` check is performed before applying the fees, which allows users to receive less than the minimum amount they specified in the order.

### Root Cause

[fillOrder()](https://github.com/sherlock-audit/2024-11-oku/blob/main/oku-custom-order-types/contracts/automatedTrigger/OracleLess.sol#L103-L148) is implemented as follows.
```solidity
function fillOrder(
        uint96 pendingOrderIdx,
        uint96 orderId,
        address target,
        bytes calldata txData
    ) external override {
        //fetch order
        Order memory order = orders[orderId];

        require(
            order.orderId == pendingOrderIds[pendingOrderIdx],
            "Order Fill Mismatch"
        );

        //perform swap
@>      (uint256 amountOut, uint256 tokenInRefund) = execute(
            target,
            txData,
            order
        );

        //handle accounting
        //remove from array
        pendingOrderIds = ArrayMutation.removeFromArray(
            pendingOrderIdx,
            pendingOrderIds
        );

        //handle fee
@>      (uint256 feeAmount, uint256 adjustedAmount) = applyFee(
            amountOut,
            order.feeBips
        );
        if (feeAmount != 0) {
            order.tokenOut.safeTransfer(address(MASTER), feeAmount);
        }

        //send tokenOut to recipient
        order.tokenOut.safeTransfer(order.recipient, adjustedAmount);

        //refund any unspent tokenIn
        //this should generally be 0 when using exact input for swaps, which is recommended
        if (tokenInRefund != 0) {
            order.tokenIn.safeTransfer(order.recipient, tokenInRefund);
        }
    }
```
As you can see, `applyFee()` is executed after [execute()](https://github.com/sherlock-audit/2024-11-oku/blob/main/oku-custom-order-types/contracts/automatedTrigger/OracleLess.sol#L250-L253), where the `minAmountOut` is checked. This can result in users receiving less than the amount they specified in `order.minAmountOut`.

### Internal pre-conditions

`feeBips != 0`

### External pre-conditions

None.

### Attack Path

None.

### Impact

Users may receive less than the specified `minAmountOut`.

### PoC

To better understand the issue, let's consider an example. Alice creates an order with `amountIn = 1000`, `minAmountOut = 900`, and `feeBips = 1000` (10%). For simplicity, assume `tokenInRefund = 0`.

1. Alice calls `fillOrder()` to execute the previously created order.
2. `execute()` performs the swap, resulting in `amountOut = 910`, and the `minAmountOut` check is validated and passed.
3. Fees are applied to `amountOut`â€”in this case, 10%. Thus, `amountOut = 910 - (910 * 10 / 100) = 819`.
4. Alice receives 819 tokens, which is less than the `minAmountOut` she set when creating the order.

### Mitigation

To resolve the issue, instead of checking `minAmountOut` in `execute()`, first apply the fees and then check the `minAmountOut`.
```diff
function fillOrder(
        uint96 pendingOrderIdx,
        uint96 orderId,
        address target,
        bytes calldata txData
    ) external override {
        //fetch order
        Order memory order = orders[orderId];

        require(
            order.orderId == pendingOrderIds[pendingOrderIdx],
            "Order Fill Mismatch"
        );

        //perform swap
        (uint256 amountOut, uint256 tokenInRefund) = execute(
            target,
            txData,
            order
        );

        //handle accounting
        //remove from array
        pendingOrderIds = ArrayMutation.removeFromArray(
            pendingOrderIdx,
            pendingOrderIds
        );

        //handle fee
        (uint256 feeAmount, uint256 adjustedAmount) = applyFee(
            amountOut,
            order.feeBips
        );
       
+       require(
+          adjustedAmount > order.minAmountOut,
+           "Too Little Received"
+       );

        if (feeAmount != 0) {
            order.tokenOut.safeTransfer(address(MASTER), feeAmount);
        }

        //send tokenOut to recipient
        order.tokenOut.safeTransfer(order.recipient, adjustedAmount);

        //refund any unspent tokenIn
        //this should generally be 0 when using exact input for swaps, which is recommended
        if (tokenInRefund != 0) {
            order.tokenIn.safeTransfer(order.recipient, tokenInRefund);
        }
    }
```