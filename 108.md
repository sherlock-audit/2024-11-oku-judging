Melted Sky Tiger

High

# Malicious users can drain funds

### Summary

The order id from `generateOrderId` is not unique. This will cause that malicious users can drain funds.

### Root Cause

When we create one order in Bracket, we will create one unique order id from [AutomationMaster.sol:90](https://github.com/sherlock-audit/2024-11-oku/blob/main/oku-custom-order-types/contracts/automatedTrigger/AutomationMaster.sol#L90). When we check the `generateOrderId` function, we will find out that the returned order id may not be unique. Users can get the same order id via different orders in the same block.

Malicious users can make use of this issue to drain all funds in the contract.
For example:
1. Alice creates one order with minimum order value in timestamp A. The order id is `uint96(uint256(keccak256(abi.encodePacked(Alice, block.timestamp))))`. `orders[orderId]` will be updated with minimum order value(e.g. 50USD) and this orderId will be pushed into `pendingOrderIds`.
2. Alice repeats step1 several times in timestamp A.
3. Alice creates one order with large order value(e.g. 5000 USD) in timestamp A. We will have the same order with step 1. So the `orders[orderId]`'s `amountIn` will be updated with 5000 USD.
4. Alice starts to cancel her pending orders N times. In each order cancel, Alice can withdraw 5000 USD from the contract.

```solidity
    function generateOrderId(address sender) external view override returns (uint96) {
        uint256 hashedValue = uint256(
            keccak256(abi.encodePacked(sender, block.timestamp))
        );
        return uint96(hashedValue);
    }
```
```solidity
    function _createOrder(
        uint256 takeProfit,
        uint256 stopPrice,
        uint256 amountIn,
        uint96 existingOrderId,
        IERC20 tokenIn,
        IERC20 tokenOut,
        address recipient,
        uint16 feeBips,
        uint16 takeProfitSlippage,
        uint16 stopSlippage
    ) internal {
        ...
        if (existingOrderId == 0) {
            existingOrderId = MASTER.generateOrderId(msg.sender);
        }

        //construct order
        orders[existingOrderId] = Order({
            orderId: existingOrderId,
            takeProfit: takeProfit,
            stopPrice: stopPrice,
            amountIn: amountIn,
            tokenIn: tokenIn,
            tokenOut: tokenOut,
            recipient: recipient,
            takeProfitSlippage: takeProfitSlippage,
            feeBips: feeBips,
            stopSlippage: stopSlippage,
            direction: MASTER.getExchangeRate(tokenIn, tokenOut) > takeProfit //exchangeRate in/out > takeProfit
        });
        pendingOrderIds.push(existingOrderId);
    }
```
```solidity
    function _cancelOrder(Order memory order) internal returns (bool) {
        for (uint96 i = 0; i < pendingOrderIds.length; i++) {
            if (pendingOrderIds[i] == order.orderId) {
                pendingOrderIds = ArrayMutation.removeFromArray(
                    i,
                    pendingOrderIds
                );
                order.tokenIn.safeTransfer(order.recipient, order.amountIn);
                emit OrderCancelled(order.orderId);
                return true;
            }
        }
        return false;
    }

```

### Internal pre-conditions

N/A

### External pre-conditions

N/A

### Attack Path

1. Alice creates one order with minimum order value in timestamp A. The order id is `uint96(uint256(keccak256(abi.encodePacked(Alice, block.timestamp))))`. `orders[orderId]` will be updated with minimum order value(e.g. 50USD) and this orderId will be pushed into `pendingOrderIds`.
2. Alice repeats step1 several times in timestamp A.
3. Alice creates one order with large order value(e.g. 5000 USD) in timestamp A. We will have the same order with step 1. So the `orders[orderId]`'s `amountIn` will be updated with 5000 USD.
4. Alice starts to cancel her pending orders N times. In each order cancel, Alice can withdraw 5000 USD from the contract.


### Impact

The malicious users can drain all funds from contracts.

### PoC

N/A

### Mitigation

We need to revisit the `generateOrderId` function to generate one real unique order id.
For example:
```solidity
+    uint256 order_id;
     function generateOrderId(address sender) external view override returns (uint96) {
         uint256 hashedValue = uint256(
-            keccak256(abi.encodePacked(sender, block.timestamp))
+            keccak256(abi.encodePacked(sender, block.timestamp, order_id))
         );
+        order_id = order_id + 1;
         return uint96(hashedValue);
     }
```