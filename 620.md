Obedient Midnight Kookaburra

High

# Filled and cancelled orders are not removed from the `orders` array which can be exploited by the order recipient to receive `tokenIn` via `modifyOrder()`

### Summary


In the `Bracket`, `OracleLess` & `StopLimit` contracts: filled and cancelled orders are not removed from the `orders` array which can be exploited by the order recipient to receive `tokenIn` via `modifyOrder()`.

### Root Cause

This is caused by not deleting the order from the `orders` array when the pending order is cancelled or fulfilled.

### Internal pre-conditions

- In `Bracket` contract: when orders are [created](https://github.com/sherlock-audit/2024-11-oku/blob/ee3f781a73d65e33fb452c9a44eb1337c5cfdbd6/oku-custom-order-types/contracts/automatedTrigger/Bracket.sol#L496), they are added to the `orders` array, and the orderId is pushed to the `pendingOrderIds` array:

```javascript
    function _createOrder(
        uint256 takeProfit,
        uint256 stopPrice,
        uint256 amountIn,
        uint96 existingOrderId,
        IERC20 tokenIn,
        IERC20 tokenOut,
        address recipient,
        uint16 feeBips,
        uint16 takeProfitSlippage,
        uint16 stopSlippage
    ) internal {
        //...
        //construct order
        orders[existingOrderId] = Order({
            //...
        });

        //store pending order
        pendingOrderIds.push(existingOrderId);
        //...
    }
```

once an order is [fulfilled](https://github.com/sherlock-audit/2024-11-oku/blob/ee3f781a73d65e33fb452c9a44eb1337c5cfdbd6/oku-custom-order-types/contracts/automatedTrigger/Bracket.sol#L119C1-L122C11) or [cancelled](https://github.com/sherlock-audit/2024-11-oku/blob/ee3f781a73d65e33fb452c9a44eb1337c5cfdbd6/oku-custom-order-types/contracts/automatedTrigger/Bracket.sol#L505C16-L508C19); its id will be removed from `pendingOrderIds` array:

```javascript
function _cancelOrder(Order memory order) internal returns (bool) {
        for (uint96 i = 0; i < pendingOrderIds.length; i++) {
            if (pendingOrderIds[i] == order.orderId) {
                //remove from pending array
                pendingOrderIds = ArrayMutation.removeFromArray(
                    i,
                    pendingOrderIds
                );

                //...
            }
        }
       //...
    }
```

```javascript
function performUpkeep(
        bytes calldata performData
    ) external override nonReentrant {
        //...
        pendingOrderIds = ArrayMutation.removeFromArray(
            data.pendingOrderIdx,
            pendingOrderIds
        );
        //...
    }
```

- As can be noticed; the order is only removed from the `pendingOrderIds` and not removed from the `orders` array, and this can be exploited any order recipient that has his order filled or cancelled to retrieve an amount of his `tokenIn` (maximum of `tokenInAmount - minOrderSize`) as the [`moifyOrder()`](https://github.com/sherlock-audit/2024-11-oku/blob/ee3f781a73d65e33fb452c9a44eb1337c5cfdbd6/oku-custom-order-types/contracts/automatedTrigger/Bracket.sol#L267C17-L267C76)function extracts the order from the `orders` instead of checking its existance in the `pendingOrderIds`:

```javascript
    function modifyOrder(
        uint96 orderId,
        uint256 _takeProfit,
        uint256 _stopPrice,
        uint256 amountInDelta,
        IERC20 _tokenOut,
        address _recipient,
        uint16 _takeProfitSlippage,
        uint16 _stopSlippage,
        bool permit,
        bool increasePosition,
        bytes calldata permitPayload
    ) external override nonReentrant {
        //get order
        Order memory order = orders[orderId];

        //only order owner
        require(msg.sender == order.recipient, "only order owner");

        //deduce any amountIn changes
        uint256 newAmountIn = order.amountIn;
        if (amountInDelta != 0) {
            if (increasePosition) {
                newAmountIn += amountInDelta;
                //take funds via permit2
                if (permit) {
                    handlePermit(
                        order.recipient,
                        permitPayload,
                        uint160(amountInDelta),
                        address(order.tokenIn)
                    );
                } else {
                    //legacy transfer, assume prior approval
                    order.tokenIn.safeTransferFrom(
                        order.recipient,
                        address(this),
                        amountInDelta
                    );
                }
            } else {
                //ensure delta is valid
                require(amountInDelta < order.amountIn, "invalid delta");

                //set new amountIn for accounting
                newAmountIn -= amountInDelta;

                //check min order size for new amount
                MASTER.checkMinOrderSize(order.tokenIn, newAmountIn);

                //refund position partially
                order.tokenIn.safeTransfer(order.recipient, amountInDelta);
            }
        }
        //...
    }
```

- The same issue is present in the `OracleLess` & `StopLimit` contracts, where the fulfilled and cancelled orders are removed from the `pendingOrdersIds` aonly nd not removed from `orders` array.

### External pre-conditions

_No response_

### Attack Path

- a user creates an ordr.
- the user cancels his order, so his `tokenIn` smount will be refunded back to him.
- the user calls `modifyOrder()` with `increasePosition = false` and an amount < `order.amountIn - minOrderSize`.
- the user will receive a tokenIn amount, draining the contract's funds.
- the same scenario can be repeated by creating an order then cancelling it then modify the order to steal an extra amount of tokenIn.

### Impact

Contract's funds will be depleted by repeating the process mentioned in the attack path section, which will result in other orders not being able to be filled, cancelled, or modified (decreasing its amount).

### PoC

_No response_

### Mitigation

Update `modifyOrder()` function tocheck if the orderId exists in the `pendingOrdersIds` to prevent stealing from the contract's funds:

```diff
    function modifyOrder(
        uint96 orderId,
        uint256 _takeProfit,
        uint256 _stopPrice,
        uint256 amountInDelta,
        IERC20 _tokenOut,
        address _recipient,
        uint16 _takeProfitSlippage,
        uint16 _stopSlippage,
        bool permit,
        bool increasePosition,
        bytes calldata permitPayload,
+       uint96 pendingOrderIdx,
    ) external override nonReentrant {
        //get order
        Order memory order = orders[orderId];
+       require(order.orderId == pendingOrderIds[pendingOrderIdx], "order doesn't exist");

        //...
    }
```
