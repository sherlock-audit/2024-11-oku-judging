Muscular Raspberry Porcupine

High

# The protocol does not account for the exponent of the `pyth` oracle when retrieving the token's price

### Summary

The `getPriceUnsafe` function of the `pyth` oracle returns the price in the format of `price * 10^expo` described in [here](https://api-reference.pyth.network/price-feeds/evm/getPriceNoOlderThan#:~:text=Sample%20price%20object%3A). However, the protocol does not take this into consideration when using the price.

### Root Cause

In the [`PythOracle.currentValue`](https://github.com/sherlock-audit/2024-11-oku/blob/main/oku-custom-order-types/contracts/oracle/External/PythOracle.sol#L26-L33) function it returns the price without considering the exponent.

```solidity
    function currentValue() external view override returns (uint256) {
        IPyth.Price memory price = pythOracle.getPriceUnsafe(tokenId);
        require(
            price.publishTime < block.timestamp - noOlderThan,
            "Stale Price"
        );
        return uint256(uint64(price.price));
    }
```

The `price.price` is in the format of `price * 10^expo`. If the price is 123456789, the usd price is the number 123456789 * 10^(-8) = 1.23456789.

In the `_getExchangeRate` function, it uses `currentValue` to calculate the exchange the rate.

```solidity
    function _getExchangeRate(
        IERC20 tokenIn,
        IERC20 tokenOut
    ) internal view returns (uint256 exchangeRate) {
        // Retrieve USD prices from oracles, scaled to 1e8
        uint256 priceIn = oracles[tokenIn].currentValue();
        uint256 priceOut = oracles[tokenOut].currentValue();

        // Return the exchange rate in 1e8 terms
        return (priceIn * 1e8) / priceOut;
    }
```

When creating orders, it checks exchange rate is greater than `takeProfit` or `stopLimitPrice`.

```solidity
        orders[existingOrderId] = Order({
            orderId: existingOrderId,
            takeProfit: takeProfit,
            stopPrice: stopPrice,
            amountIn: amountIn,
            tokenIn: tokenIn,
            tokenOut: tokenOut,
            recipient: recipient,
            takeProfitSlippage: takeProfitSlippage,
            feeBips: feeBips,
            stopSlippage: stopSlippage,
            direction: MASTER.getExchangeRate(tokenIn, tokenOut) > takeProfit //exchangeRate in/out > takeProfit
        });

        orders[orderId] = Order({
            orderId: orderId,
            stopLimitPrice: stopLimitPrice,
            stopPrice: stopPrice,
            takeProfit: takeProfit,
            amountIn: amountIn,
            tokenIn: tokenIn,
            tokenOut: tokenOut,
            takeProfitSlippage: takeProfitSlippage,
            feeBips: feeBips,
            stopSlippage: stopSlippage,
            swapSlippage: swapSlippage,
            recipient: recipient,
            direction: MASTER.getExchangeRate(tokenIn, tokenOut) >
                stopLimitPrice, //compare to stop price for this order's direction
            swapOnFill: swapOnFill
        });
```

If the tokenIn and tokenOut's exponents are different, order is created with incorrect `direction` parameter.

Additionally, to fill orders, it calls `checkInRange` which uses `exchangeRate`.

```solidity
    function checkInRange(
        Order memory order
    )
        internal
        view
        returns (bool inRange, bool takeProfit, uint256 exchangeRate)
    {
        exchangeRate = MASTER.getExchangeRate(order.tokenIn, order.tokenOut);
        if (order.direction) {
            //check for take profit price
            if (exchangeRate <= order.takeProfit) {
                return (true, true, exchangeRate);
            }
            //check for stop price
            if (exchangeRate >= order.stopPrice) {
                return (true, false, exchangeRate);
            }
        } else {
            //check for take profit price
            if (exchangeRate >= order.takeProfit) {
                return (true, true, exchangeRate);
            }
            //check for stop price
            if (exchangeRate <= order.stopPrice) {
                return (true, false, exchangeRate);
            }
        }
    }
```

As a result, this performs incorrect check to fill orders and breaks the limit order mechanism.

### Internal pre-conditions

None

### External pre-conditions

1. None

### Attack Path

None

### Impact

Filling orders is performed with incorrect check.

### PoC

None

### Mitigation

It is recommended to consider the `pyth`'s exponent to calculate the token's price.
