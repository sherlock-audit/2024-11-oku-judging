Young Citron Goblin

High

# `oracleLess` contract can be permanently DoS'd

### Summary
The `OracleLess` contract is expected to be used as an orderbook for tokens which do not have an oracle. As there is no oracle for the tokens, no minimum order size is enforced.

```solidity
    function createOrder(
        IERC20 tokenIn,
        IERC20 tokenOut,
        uint256 amountIn,
        uint256 minAmountOut,
        address recipient,
        uint16 feeBips,
        bool permit,
        bytes calldata permitPayload
    ) external override returns (uint96 orderId) {
        //procure tokens
        procureTokens(tokenIn, amountIn, recipient, permit, permitPayload);

        //construct and store order
        orderId = MASTER.generateOrderId(recipient);
        orders[orderId] = Order({
            orderId: orderId,
            tokenIn: tokenIn,
            tokenOut: tokenOut,
            amountIn: amountIn,
            minAmountOut: minAmountOut,
            recipient: recipient,
            feeBips: feeBips
        });

        //store pending order
        pendingOrderIds.push(orderId);

        emit OrderCreated(orderId);
    }
```

The protocol also lacks a limit of pending orders. This is crucial, as any time a order is removed, the protocol loops through all pending orders to create a new array

```solidity
        pendingOrderIds = ArrayMutation.removeFromArray(
            pendingOrderIdx,
            pendingOrderIds
        );
```

```solidity
library ArrayMutation {
    ///@param idx the element to remove from @param inputArray
    function removeFromArray(
        uint96 idx,
        uint96[] memory inputArray
    ) internal pure returns (uint96[] memory newList) {
        // Check that inputArray is not empty and idx is valid
        require(inputArray.length > 0, "inputArray length == 0");
        require(idx < inputArray.length, "index out of bounds");

        // Create a new array of the appropriate size
        newList = new uint96[](inputArray.length - 1);

        // Copy elements before the index
        for (uint96 i = 0; i < idx; i++) {
            newList[i] = inputArray[i];
        }

        // Copy elements after the index
        for (uint96 i = idx + 1; i < inputArray.length; i++) {
            newList[i - 1] = inputArray[i];
        }

        return newList;
    }
}
```

This allows any user to add thousands of dust offers for a fake token. By doing so, any time an offer is attempted to be removed, it would try to loop through the entire list, which would result in OOG. `cancelOrder` would also not work. All funds within the contract would be stuck.

### Root Cause
Looping through unbounded array

### Attack Path
1. Attacker creates thousands of dust offers
2. Next time an offer is attempted to be removed, it would run OOG.
3. All funds within the contract will be permanently stuck

### Impact
Loss of funds

### Affected Code
https://github.com/sherlock-audit/2024-11-oku/blob/main/oku-custom-order-types/contracts/automatedTrigger/OracleLess.sol#L64

### Mitigation
Fix is non-trivial.