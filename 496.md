Loud Lace Porpoise

High

# Attackers can drain the `OracleLess` contract by generating duplicate `orderId`s.

### Summary

The `orderId` is generated using the order owner's address and the block timestamp. Therefore, if a user creates multiple orders within the same block, identical `orderId`s will be produced. When a duplicate `orderId` is created, it will be added to `pendingOrderIds`, but the existing `orders[orderId]` will be overridden instead of creating a new one. Attackers can exploit this vulnerability to drain the contract.

### Root Cause

As shown in [line 92](https://github.com/sherlock-audit/2024-11-oku/blob/main/oku-custom-order-types/contracts/automatedTrigger/AutomationMaster.sol#L92), the `generateOrderId()` function relies solely on `sender` and `block.timestamp`.

```solidity
    function generateOrderId(address sender) external view override returns (uint96) {
        uint256 hashedValue = uint256(
92          keccak256(abi.encodePacked(sender, block.timestamp))
        );
        return uint96(hashedValue);
    }
```

### Internal pre-conditions

### External pre-conditions

### Attack Path

Let's consider the following scenario:

1. Alice, the attacker, creates an order:

    - `amountIn`: 1
    - `recipient`: Alice
    - `orderId`: `n` (generated using parameters `(Alice, current block timestamp)`)
    - `n` is added to `pendingOrderIds` at the `m`th position.
2. In the same block, Alice creates another order with the following parameters:

    - `amountIn`: 100
    - `recipient`: Alice
    - `orderId`: `n` (generated again with parameters `(Alice, current block timestamp)`)
    - `n` is added to `pendingOrderIds` at the `(m + 1)`th position.
    - `orders[n]` is overridden (at line 52), updating its `amountIn` to 100.

    ```solidity
        function createOrder(
            IERC20 tokenIn,
            IERC20 tokenOut,
            uint256 amountIn,
            uint256 minAmountOut,
            address recipient,
            uint16 feeBips,
            bool permit,
            bytes calldata permitPayload
        ) external override returns (uint96 orderId) {
            //procure tokens
            procureTokens(tokenIn, amountIn, recipient, permit, permitPayload);

            //construct and store order
    51      orderId = MASTER.generateOrderId(recipient);
    52      orders[orderId] = Order({
                orderId: orderId,
                tokenIn: tokenIn,
                tokenOut: tokenOut,
                amountIn: amountIn,
                minAmountOut: minAmountOut,
                recipient: recipient,
                feeBips: feeBips
            });

            //store pending order
            pendingOrderIds.push(orderId);

            emit OrderCreated(orderId);
        }
    ```
3. Alice cancels her order:

    - `n` is removed from `pendingOrderIds` (the `m`th entry is deleted).
    - `n` is reinserted at the `m`th position (since the `(m + 1)`th entry was also `n`).
    - 100 is refunded to Alice (the order was already overridden with an `amountIn` of 100).
4. Alice cancels her order again:

    - `n` is removed from `pendingOrderIds`.
    - 100 is refunded to Alice once more.

As a result, Alice unfairly gains 99.

### Impact

Attackers can drain the `oracleLess` contract by generating duplicate `orderId`s.

### PoC

### Mitigation

Incorporate a unique `nonce` to generate the `orderId`.

```diff
    function generateOrderId(address sender) external view override returns (uint96) {
        uint256 hashedValue = uint256(
-           keccak256(abi.encodePacked(sender, block.timestamp))
+           keccak256(abi.encodePacked(sender, block.timestamp, nonce ++))
        );
        return uint96(hashedValue);
    }
```