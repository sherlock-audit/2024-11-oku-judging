Loud Lace Porpoise

High

# The `_cancelOrder()` function removes the `orderId` solely from the `pendingOrderIds` array, but does not remove it from the `orders` mapping.

### Summary

The `_cancelOrder()` function does not remove the order from the `orders` mapping. As a result, the order owner can exploit this retained mapping data to call the `modifyOrder()` function and unfairly retrieve assets.

### Root Cause

The [_cancelOrder()](https://github.com/sherlock-audit/2024-11-oku/blob/main/oku-custom-order-types/contracts/automatedTrigger/OracleLess.sol#L150-L169) function does not remove `orders[orderId]`.

```solidity
    function _cancelOrder(Order memory order) internal returns (bool) {
        for (uint96 i = 0; i < pendingOrderIds.length; i++) {
            if (pendingOrderIds[i] == order.orderId) {
                //remove from pending array
                pendingOrderIds = ArrayMutation.removeFromArray(
                    i,
                    pendingOrderIds
                );

                //refund tokenIn amountIn to recipient
                order.tokenIn.safeTransfer(order.recipient, order.amountIn);

                //emit event
                emit OrderCancelled(order.orderId);

                return true;
            }
        }
        return false;
    }
```

### Internal pre-conditions

### External pre-conditions

### Attack Path

Let's consider the following scenario:

1. Alice creates an order:

    - `amountIn`: $100
    - `recipient`: Alice
    - `orderId`: n
2. Alice cancels her order:

    - Her `orderId` (`n`) is removed from `pendingOrderIds`, but `orders[n]` remains unchanged.
    - `$100` is refunded to Alice.
3. Alice modifies her order, decreasing the amount to `$0`:

    - `$100` is refunded to Alice again.
        ```solidity
            function _modifyOrder(
                uint96 orderId,
                IERC20 _tokenOut,
                uint256 amountInDelta,
                uint256 _minAmountOut,
                address _recipient,
                bool increasePosition,
                bool permit,
                bytes calldata permitPayload
            ) internal {
                //fetch order
                Order memory order = orders[orderId];

                require(msg.sender == order.recipient, "only order owner");

                //deduce any amountIn changes
                uint256 newAmountIn = order.amountIn;
                if (amountInDelta != 0) {
                    if (increasePosition) {
                        //take more tokens from order recipient
                        newAmountIn += amountInDelta;
                        procureTokens(
                            order.tokenIn,
                            amountInDelta,
                            order.recipient,
                            permit,
                            permitPayload
                        );
                    } else {
                        //refund some tokens
                        //ensure delta is valid
                        require(amountInDelta < order.amountIn, "invalid delta");

                        //set new amountIn for accounting
                        newAmountIn -= amountInDelta;

                        //refund position partially
        @>             order.tokenIn.safeTransfer(order.recipient, amountInDelta);
                    }
                }

                //construct new order
                Order memory newOrder = Order({
                    orderId: orderId,
                    tokenIn: order.tokenIn,
                    tokenOut: _tokenOut,
                    amountIn: newAmountIn,
                    minAmountOut: _minAmountOut,
                    feeBips: order.feeBips,
                    recipient: _recipient
                });

                //store new order
                orders[orderId] = newOrder;
            }
        ```

As a result, Alice can unfairly gain a profit of `$100`.

### Impact

Attackers can drain the `OracleLess` contract by executing the sequence of `create + cancel + modify`. This type of attack can also be carried out in other contracts.

### PoC

### Mitigation

Also, remove `orderId` from the `orders` mapping.

```diff
    function _cancelOrder(Order memory order) internal returns (bool) {
        for (uint96 i = 0; i < pendingOrderIds.length; i++) {
            if (pendingOrderIds[i] == order.orderId) {
                //remove from pending array
                pendingOrderIds = ArrayMutation.removeFromArray(
                    i,
                    pendingOrderIds
                );

                //refund tokenIn amountIn to recipient
                order.tokenIn.safeTransfer(order.recipient, order.amountIn);

+               delete orders[order.orderId];

                //emit event
                emit OrderCancelled(order.orderId);

                return true;
            }
        }
        return false;
    }
```