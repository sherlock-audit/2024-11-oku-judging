Upbeat Lead Kestrel

High

# Order will loss approx 99% of token out

### Summary

The incorrect integration with PythOracle can cause the token price deviates up to 100 times, causing loss of orders token out

### Root Cause

- According to [Pyth Price Feed's API reference](https://api-reference.pyth.network/price-feeds/evm/getPriceUnsafe) for the function `getPriceUnsafe()`
> The price above is in the format of price * 10^expo. So, the price in above mentioned sample represents the number 123456789 * 10(-8) = 1.23456789 in this case.

- But in the [function `PythOracle::currentValue()`](https://github.com/sherlock-audit/2024-11-oku/blob/main/oku-custom-order-types/contracts/oracle/External/PythOracle.sol#L26-L33), the logic does not take `expo` into account. Although many Pyth feeds return `expo = 8`, which is safe for the function `currentValue()`. But indeed, there are feeds that return `expo` different from `8`
For example, the feed for `PEPE` with `feedID = 0xd69731a2e74ac1ce884fc3890f7ee324b6deb66147055249568869ed700882e4`, returns `expo = 10`
So in this case, the function [`getExchangeRate()`](https://github.com/sherlock-audit/2024-11-oku/blob/main/oku-custom-order-types/contracts/automatedTrigger/AutomationMaster.sol#L70-L87) will deviate by 100 times because the function is assuming that prices are in terms of `1e8`
```solidity
    function _getExchangeRate(
        IERC20 tokenIn,
        IERC20 tokenOut
    ) internal view returns (uint256 exchangeRate) {
        // Retrieve USD prices from oracles, scaled to 1e8
        uint256 priceIn = oracles[tokenIn].currentValue();
        uint256 priceOut = oracles[tokenOut].currentValue();

        // Return the exchange rate in 1e8 terms
        return (priceIn * 1e8) / priceOut;
    }
```

### Internal pre-conditions

_No response_

### External pre-conditions

1. Pyth on-price is updated in order to be not stale

### Attack Path

1. Assume that `PEPE` price is now 0.000026$.
An user places an order to sell 26 `USDC` to `PEPE`, with take profit price 0.00002$. 
Just ignore the slippages, the user should receive at least 1.300.000 `PEPE`
2. When `PEPE` price at 0.00002$, the order is filled but the user only receives 13.000 `PEPE`, which is just 1% of the expected amount

### Impact

- Order owners will suffer an approx loss of 99% value

### PoC

```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.13;

import {Test, console} from "forge-std/Test.sol";
import {Bracket, IERC20, IBracket} from "contracts/automatedTrigger/Bracket.sol";
import {AutomationMaster, IPythRelay, IAutomation} from "contracts/automatedTrigger/AutomationMaster.sol";
import "contracts/interfaces/uniswapV3/IPermit2.sol";
import {MockERC20} from "forge-std/mocks/MockERC20.sol";

contract MockToken is MockERC20 {
    function mint(address to, uint256 amount) external {
        _mint(to, amount);
    }
}

contract MyTest is Test {
    Bracket bracket;
    AutomationMaster master;
    address permit2;

    IERC20 usdc;
    IERC20 PEPE;

    address usdcOracle;
    address pepeOracle;

    address pythOracle;

    IERC20[] tokens;
    IPythRelay[] oracles;

    uint96 currentOrderId;

    address alice;

    function setUp() public {
        permit2 = makeAddr("permit2");
        usdcOracle = makeAddr("usdcOracle");
        pepeOracle = makeAddr("pepeOracle");
        pythOracle = makeAddr("PythOracle");

        alice = makeAddr("alice");

        master = new AutomationMaster();
        bracket = new Bracket(master, IPermit2(permit2));

        MockToken _usdc = new MockToken();
        _usdc.initialize("usdc", "usdc", 8);
        _usdc.mint(address(this), 1000000e8);
        _usdc.mint(alice, 1000000e8);
        _usdc.approve(address(bracket), 1000000e8);
        vm.prank(alice);
        _usdc.approve(address(bracket), 1000000e8);

        usdc = IERC20(address(_usdc));

        MockToken _PEPE = new MockToken();
        _PEPE.initialize("PEPE", "PEPE", 18);
        _PEPE.mint(address(this), 1e6 ether);
        _PEPE.mint(alice, 1e6 ether);
        _PEPE.approve(address(bracket), 1e6 ether);
        vm.prank(alice);
        _PEPE.approve(address(bracket), 1e6 ether);

        PEPE = IERC20(address(_PEPE));

        vm.label(address(usdc), "USDC");
        vm.label(address(PEPE), "PEPE");

        tokens.push(usdc);
        tokens.push(PEPE);

        oracles.push(IPythRelay(usdcOracle));
        oracles.push(IPythRelay(pepeOracle));

        master.registerOracle(tokens, oracles);
        master.setMaxPendingOrders(100);

        // mock price for usdc
        vm.mockCall(usdcOracle, abi.encodeWithSignature("currentValue()"), abi.encode(1e8));

        deployCodeTo("PythOracle", abi.encode(pythOracle, bytes32("PEPE_tokenId"), 0, address(0)), pepeOracle);
    }

    function _updatePythOraclePrice(int64 price, int32 expo, uint256 publishTime) internal {
        vm.mockCall(
            pythOracle, abi.encodeWithSignature("getPriceUnsafe(bytes32)"), abi.encode(price, 0, expo, publishTime)
        );
    }

    function test_PythOracle_Expo() public {
        // set arbitrary blocktime
        vm.warp(10000);

        uint96 orderId = master.generateOrderId(address(this));
        currentOrderId = orderId;

        // PEPE 0.0000264$
        // expo is -10
        _updatePythOraclePrice(264000, -10, block.timestamp - 30);

        bracket.createOrder({
            swapPayload: new bytes(0),
            takeProfit: 200000, // take profit at PEPE 0.00002$ -> user should receive 1,3M PEPE
            stopPrice: 0,
            amountIn: 26e8, // 1 mil PEPE ~ 26$
            tokenIn: usdc,
            tokenOut: PEPE,
            recipient: address(this),
            feeBips: 0,
            takeProfitSlippage: 0,
            stopSlippage: 0,
            permit: false,
            permitPayload: new bytes(0)
        });

        IAutomation.MasterUpkeepData memory upkeepData = IAutomation.MasterUpkeepData({
            orderType: IAutomation.OrderType.BRACKET,
            target: address(this),
            txData: abi.encodeWithSignature("validSwap()"),
            pendingOrderIdx: 0,
            orderId: orderId,
            tokenIn: usdc,
            tokenOut: PEPE,
            slippage: 0,
            amountIn: 26e8,
            exchangeRate: 0
        });

        // PEPE 0.00002$
        // expo is -10
        _updatePythOraclePrice(200000, -10, block.timestamp - 10);

        uint256 PEPE_balance = PEPE.balanceOf(address(this));

        // 3/ order is filled
        bracket.performUpkeep(abi.encode(upkeepData));

        assertGe(PEPE.balanceOf(address(this)), PEPE_balance + 1_300_000e18, "should increase 1,3M PEPE");
    }

    function validSwap() external payable {
        usdc.transferFrom(address(bracket), address(this), 26e8);

        // just send 13000 PEPE, instead of 1,3M PEPE
        PEPE.transfer(address(bracket), 13000 ether + 1);
    }

}

```

Run the test `test_PythOracle_Expo` and it fails
```bash
Failing tests:
Encountered 1 failing test in test/Counter.t.sol:MyTest
[FAIL: should increase 1,3M PEPE: 1000000000000000000000000 < 2300000000000000000000000] test_PythOracle_Expo() (gas: 361939)
```

### Mitigation

Adjust token price according to the `expo` from PythOracle's return values