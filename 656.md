Mythical Tortilla Moose

High

# Hash collision of the orderId can lead to fund loss in multiple ways

### Summary

Due to the way of how the orderId is generated, there are multiple instances which can lead to hash-collisions and fund loss.
Since the `orderId` is generate as a hashed value (encodePacked) of the msg.sender combined with the block.timestamp, then converted to uint256 and further to uint96, there are multiple instances in which the same Id can be created for two different orders, effectively replacing the second order with the first, which will cause for all data in the first order to be "lost".

- This can happen if the same user creates more than one order in the same block;
- There's an order with the same id in the StopLoss contract, as well as the Bracket contract, so when the StopLoss contract order reaches the desired price, a new order with the same id will be created in the Bracket, but since there's an already existing pending order with the same id, the new order will "replace" the old one, which will "erase" the original order in the Bracket contract. 
- During truncation from uint256 to uint96 if the high-order bits will be truncated if the produced hash doesn't fit in uint96, this can lead to unexpected behavior and again possibly producing the same hash for two different orders.   

### Root Cause

https://github.com/sherlock-audit/2024-11-oku/blob/ee3f781a73d65e33fb452c9a44eb1337c5cfdbd6/oku-custom-order-types/contracts/automatedTrigger/AutomationMaster.sol#L90-L95

Due to the way how orderIds are generated, there are multiple instances which will lead to "hash collisions", i.e. the same orderId being generated for two different orders, which will lead to the later order "replacing" the first one and, which will cause the first order to be erased, and the tokenAmount from the tokenIn frozen/lost.

```solidity

    function generateOrderId(address sender) external view override returns (uint96) {
        uint256 hashedValue = uint256(
            keccak256(abi.encodePacked(sender, block.timestamp))
        );
        return uint96(hashedValue);
    }

```

The orderId is generate as a hashed value (later converted to uint256 and then uint96) from the msg.sender and the block.timestamp; 

The problem is if the same user opens two orders in the same block, the second order will replace the first one.

### Internal pre-conditions

1. OrderId generation only takes in the block.timestamp and msg.sender as values to be hashed which can lead to hash collisions;

### External pre-conditions

1. A user opens 2 orders in the same block;
2. A user opens 1 order in the Bracket and one in the StopLimit contract in the same block;

### Attack Path

1. User A creates an order:

```solidity

function createOrder(
        bytes calldata swapPayload,
        uint256 takeProfit,
        uint256 stopPrice,
        uint256 amountIn,
        IERC20 tokenIn,
        IERC20 tokenOut,
        address recipient,
        uint16 feeBips,
        uint16 takeProfitSlippage,
        uint16 stopSlippage,
        bool permit,
        bytes calldata permitPayload
    ) external override nonReentrant {
        _initializeOrder(
            swapPayload,
            takeProfit,
            stopPrice,
            amountIn,
            0, //no existing order id
            tokenIn,
            tokenOut,
            recipient,
            feeBips,
            takeProfitSlippage,
            stopSlippage,
            permit,
            permitPayload
        );
    }

```

After which the `_initializeOrder` and then `_createOrder` function is invoked, since there's no existing orderId, the argument "0" was passed which will invoke the creation of a new orderId: 

```solidity

 if (existingOrderId == 0) {
            existingOrderId = MASTER.generateOrderId(msg.sender);
        }

```

`generateOrderId` only takes in the msg.sender and block.timestmap into consideration(as arguments) when creating the hashed value which becomes the orderId: 

```solidity

    function generateOrderId(address sender) external view override returns (uint96) {
        uint256 hashedValue = uint256(keccak256(abi.encodePacked(sender, block.timestamp)));  
        return uint96(hashedValue); 
    }

```

2. User A creates a second order which is also executed in the same block.
3. This leads to the same id being produced for the second order, since the msg.sender and the block.timestamp are the same values; 
4. The second order will "replace" the first one: 

```solidity

orders[existingOrderId] = Order({
            orderId: existingOrderId,
            takeProfit: takeProfit,
            stopPrice: stopPrice,
            amountIn: amountIn,
            tokenIn: tokenIn,
            tokenOut: tokenOut,
            recipient: recipient,
            takeProfitSlippage: takeProfitSlippage,
            feeBips: feeBips,
            stopSlippage: stopSlippage,
            direction: MASTER.getExchangeRate(tokenIn, tokenOut) > takeProfit //exchangeRate in/out > takeProfit
        });

        //store pending order
        pendingOrderIds.push(existingOrderId);

```
This is because the order will be saved in the `orders` mapping using the same id, so the data from the second order will replace the ones from the first. 

The first order will practically be lost and the amount from the tokenIn deposited can never be recovered.
This scenario can also happen when a stopLimit order reaches its target price and `fillStopLimitOrder` is called on the Bracket. But if there's an existing order in the Bracket with the same id, the stopLimit order will replace the order in the Bracket. 

### Impact

Two different orders with the same id can be generated which will lead to the second order "replacing" the first, and the first order being permanently erased and the funds from it - lost. 

### PoC

Minimal PoC that can be used in Remix to test the hash collision:

```solidity

//SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

contract POC {

    function hashValues() public pure returns (uint96 a, uint96 b){

        uint256 a1;
        uint256 b1;
        uint32 c = 1733241400; // Example block.timestamp

        a1 = uint256(keccak256(abi.encodePacked(0x83636E3bBC8D27296824be893496dd3f5E0a870d, c)));
        b1 = uint256(keccak256(abi.encodePacked(0x83636E3bBC8D27296824be893496dd3f5E0a870d, c)));

        a = uint96(a1);
        b = uint96 (b1);

    }

}

```

### Mitigation

Order generation hash should include the contract in-question as well (e.g. either bracket or stopLimit) and a nonce; It's also recommended for the id to be the full uint256 number;