Young Vanilla Pelican

Medium

# AutomationMaster.sol :: generateOrderId() can produce the same orderId for different orders.

### Summary

`generateOrderId()` is used to create unique `orderId` for orders, allowing them to be identified. However, the issue lies in its implementation: it relies solely on the sender's address and the `block.timestamp`. 

If a user submits two transactions within the same `block.timestamp`, it can result in two distinct orders being assigned the same `orderId`.

### Root Cause

[generateOrderId()](https://github.com/sherlock-audit/2024-11-oku/blob/main/oku-custom-order-types/contracts/automatedTrigger/AutomationMaster.sol#L90-L95) is implemented as follows.
```solidity
function generateOrderId(address sender) external view override returns (uint96) {
        uint256 hashedValue = uint256(
            keccak256(abi.encodePacked(sender, block.timestamp))
        );
        return uint96(hashedValue);
    }
```
The `generateOrderId` function relies solely on the sender's address and the `block.timestamp` to create the `orderId`. This approach leads to a problem: if a user employs a smart contract to generate multiple orders within the same block, all those orders will share the same `block.timestamp`. Consequently, if the sender is the same, the generated `orderId` will be identical for different orders.

To understand the impact, examine the [_createOrder()](https://github.com/sherlock-audit/2024-11-oku/blob/main/oku-custom-order-types/contracts/automatedTrigger/StopLimit.sol#L300-L356) in `StopLimit.sol`. 

This issue will affect both `Bracket.sol` and `OracleLess.sol` in the same way, but for this example, we will focus on `StopLimit.sol`.
```solidity
function _createOrder(
        uint256 stopLimitPrice,
        uint256 takeProfit,
        uint256 stopPrice,
        uint256 amountIn,
        IERC20 tokenIn,
        IERC20 tokenOut,
        address recipient,
        uint16 feeBips,
        uint16 takeProfitSlippage,
        uint16 stopSlippage,
        uint16 swapSlippage,
        bool swapOnFill
    ) internal {
        //verify both oracles exist, as we need both to calc the exchange rate
        require(
            address(MASTER.oracles(tokenIn)) != address(0x0) &&
                address(MASTER.oracles(tokenOut)) != address(0x0),
            "Oracle !exist"
        );
        require(
            pendingOrderIds.length < MASTER.maxPendingOrders(),
            "Max Order Count Reached"
        );
        require(
            takeProfitSlippage <= 10000 &&
                stopSlippage <= 10000 &&
                swapSlippage <= 10000 &&
                feeBips <= 10000,
            "BIPS > 10k"
        );

        MASTER.checkMinOrderSize(tokenIn, amountIn);

@>      uint96 orderId = MASTER.generateOrderId(msg.sender);

@>      orders[orderId] = Order({
            orderId: orderId,
            stopLimitPrice: stopLimitPrice,
            stopPrice: stopPrice,
            takeProfit: takeProfit,
            amountIn: amountIn,
            tokenIn: tokenIn,
            tokenOut: tokenOut,
            takeProfitSlippage: takeProfitSlippage,
            feeBips: feeBips,
            stopSlippage: stopSlippage,
            swapSlippage: swapSlippage,
            recipient: recipient,
            direction: MASTER.getExchangeRate(tokenIn, tokenOut) >
                stopLimitPrice, //compare to stop price for this order's direction
            swapOnFill: swapOnFill
        });
        pendingOrderIds.push(uint96(orderId));
        //emit
        emit OrderCreated(orderId);
    }
```
If two different orders are assigned the same `orderId`, it will cause the second order to overwrite the first one. As a result, only one order will exist for execution instead of two. When the order is eventually executed, it will use the parameters of the second order, as the first order was overwritten and no longer exists.

Additionally, the `pendingOrderIds` array will contain duplicate entries for the same `orderId` since both orders are pushed into the array, leading to further inconsistencies and potential issues.


### Internal pre-conditions

None.

### External pre-conditions

A user creates multiple orders by calling `createOrder()` through a smart contract.

### Attack Path

None.

### Impact

The user believes they have created multiple distinct orders, but in reality, only the last order is created and will be executed.

### PoC

To better understand the issue, copy the following POC into `failure_ORACLE_LESS.ts`.
```js
it("Same orderId for different orders", async () => {
        let id1 = await s.Master.generateOrderId(await s.Steve.getAddress())
        let id2 = await s.Master.generateOrderId(await s.Steve.getAddress())

        expect(id1).to.be.eq(id2)
    })
```
As demonstrated, the same `orderId` is generated for different orders because they share the same `block.timestamp`.

### Mitigation

To resolve this issue, one solution is to include additional parameters in the `orderId` generation. For example, a user-specific nonce can be added, which increments with each new order creation. 

This approach ensures that unique `orderId` are generated, even if multiple orders are created within the same `block.timestamp`.