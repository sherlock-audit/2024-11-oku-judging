Loud Lace Porpoise

High

# The `Bracket.performUpkeep()` function removes the `orderId` solely from the `pendingOrderIds` array, but does not remove it from the `orders` mapping.

### Summary

The `Bracket.performUpkeep()` function does not remove the order from the `orders` mapping. As a result, the order owner can exploit this retained mapping data to call the `modifyOrder()` function and unfairly retrieve assets.

### Root Cause

The [Bracket.performUpkeep()](https://github.com/sherlock-audit/2024-11-oku/blob/main/oku-custom-order-types/contracts/automatedTrigger/Bracket.sol#L85-L145) function does not remove `orders[orderId]`.

<details>
    <summary> performUpkeep() </summary>

    function performUpkeep(
        bytes calldata performData
    ) external override nonReentrant {
        MasterUpkeepData memory data = abi.decode(
            performData,
            (MasterUpkeepData)
        );
        Order memory order = orders[pendingOrderIds[data.pendingOrderIdx]];

        require(
            order.orderId == pendingOrderIds[data.pendingOrderIdx],
            "Order Fill Mismatch"
        );

        //deduce if we are filling stop or take profit
        (bool inRange, bool takeProfit, ) = checkInRange(order);
        require(inRange, "order ! in range");

        //deduce bips
        uint16 bips;
        takeProfit ? bips = order.takeProfitSlippage : bips = order
            .stopSlippage;

        (uint256 swapAmountOut, uint256 tokenInRefund) = execute(
            data.target,
            data.txData,
            order.amountIn,
            order.tokenIn,
            order.tokenOut,
            bips
        );

        //handle accounting
        //remove from pending array
        pendingOrderIds = ArrayMutation.removeFromArray(
            data.pendingOrderIdx,
            pendingOrderIds
        );

        //handle fee
        (uint256 feeAmount, uint256 adjustedAmount) = applyFee(
            swapAmountOut,
            order.feeBips
        );

        if (feeAmount != 0) {
            order.tokenOut.safeTransfer(address(MASTER), feeAmount);
        }

        //send tokenOut to recipient
        order.tokenOut.safeTransfer(order.recipient, adjustedAmount);

        //refund any unspent tokenIn
        //this should generally be 0 when using exact input for swaps, which is recommended
        if (tokenInRefund != 0) {
            order.tokenIn.safeTransfer(order.recipient, tokenInRefund);
        }

        //emit
        emit OrderProcessed(order.orderId);
    }
</details>

### Internal pre-conditions

### External pre-conditions

### Attack Path

Let's consider the following scenario:

1. Alice creates an order:

    - `amountIn`: $100
    - `recipient`: Alice
    - `orderId`: n
2. Alice executes her order:

    - Her `orderId` (`n`) is removed from `pendingOrderIds`, but `orders[n]` remains unchanged.
    - `tokenOut` valued at $100 is transferred to Alice.
3. Alice modifies her order, decreasing the amount to `$20` (assuming the minimum order size):

    - `$80` is refunded to Alice again.
        <details>
            <summary> modify() </summary>

            function modifyOrder(
                uint96 orderId,
                uint256 _takeProfit,
                uint256 _stopPrice,
                uint256 amountInDelta,
                IERC20 _tokenOut,
                address _recipient,
                uint16 _takeProfitSlippage,
                uint16 _stopSlippage,
                bool permit,
                bool increasePosition,
                bytes calldata permitPayload
            ) external override nonReentrant {
                //get order
                Order memory order = orders[orderId];

                //only order owner
                require(msg.sender == order.recipient, "only order owner");

                //deduce any amountIn changes
                uint256 newAmountIn = order.amountIn;
                if (amountInDelta != 0) {
                    if (increasePosition) {
                        newAmountIn += amountInDelta;
                        //take funds via permit2
                        if (permit) {
                            handlePermit(
                                order.recipient,
                                permitPayload,
                                uint160(amountInDelta),
                                address(order.tokenIn)
                            );
                        } else {
                            //legacy transfer, assume prior approval
                            order.tokenIn.safeTransferFrom(
                                order.recipient,
                                address(this),
                                amountInDelta
                            );
                        }
                    } else {
                        //ensure delta is valid
                        require(amountInDelta < order.amountIn, "invalid delta");

                        //set new amountIn for accounting
                        newAmountIn -= amountInDelta;

                        //check min order size for new amount
                        MASTER.checkMinOrderSize(order.tokenIn, newAmountIn);

                        //refund position partially
            @>          order.tokenIn.safeTransfer(order.recipient, amountInDelta);
                    }
                }

                //check for oracles
                if (_tokenOut != order.tokenOut) {
                    require(
                        address(MASTER.oracles(_tokenOut)) != address(0x0),
                        "Oracle !exist"
                    );
                }

                //construct new order
                Order memory newOrder = Order({
                    orderId: orderId,
                    takeProfit: _takeProfit,
                    stopPrice: _stopPrice,
                    amountIn: newAmountIn,
                    tokenIn: order.tokenIn,
                    tokenOut: _tokenOut,
                    feeBips: order.feeBips,
                    takeProfitSlippage: _takeProfitSlippage,
                    stopSlippage: _stopSlippage,
                    recipient: _recipient,
                    direction: MASTER.getExchangeRate(order.tokenIn, _tokenOut) >
                        _takeProfit
                });

                //store new order
                orders[orderId] = newOrder;
            }
        </details>

As a result, Alice can unfairly gain a profit of `$80`.

### Impact

Attackers can drain the `Bracket` contract by executing the sequence of `create + performUpKeep + modify`.

### PoC

### Mitigation

Also, remove `orderId` from the `orders` mapping.

```diff
    function performUpkeep(
        ...

+       delete orders[order.orderId];
    }
```