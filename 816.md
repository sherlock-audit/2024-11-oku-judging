Mythical Tortilla Moose

High

# Malicious users can drain Bracket.sol's assets through a combination of OracleLess and Bracket dummy/malicious orders

### Summary

A malicious user can drain the Bracket.sol contract utilizing malicious orders as well as cross-contract reentrancy and multiple other protocol features explained below.

### Root Cause

https://github.com/sherlock-audit/2024-11-oku/blob/ee3f781a73d65e33fb452c9a44eb1337c5cfdbd6/oku-custom-order-types/contracts/automatedTrigger/Bracket.sol#L526-L568
https://github.com/sherlock-audit/2024-11-oku/blob/ee3f781a73d65e33fb452c9a44eb1337c5cfdbd6/oku-custom-order-types/contracts/automatedTrigger/OracleLess.sol#L38-L67

This is plausible due to multiple root causes and the whole scenario can take place in the following order:

A malicious user (let's call him Bob) opens an order in OracleLess, tokenIn amount is 1 wei (regardless of the actual tokens utilized), slippage is basically set to 0 (10_000), so 1 wei sent to the contract would satisfy the execution slippage criteria/checks.

Same malicious user opens a 100k (100_000e6) USDT order in Bracket.sol which is immediately executable (tokenOut is irrelevant to the scenario, and the 100k amount is an example). 

The user executes their own (100k) order in Bracket.sol, the target contract is OracleLess, while the txData contains instructions to call `fillOrder` on the OracleLess, the data with which `fillOrder` will be called contains the 1 wei dummy order which was initially created: 

```solidity

 function fillOrder(uint96 pendingOrderIdx, uint96 orderId, address target, bytes calldata txData)
        external
        override
    {
        //fetch order
        Order memory order = orders[orderId];

        require(order.orderId == pendingOrderIds[pendingOrderIdx], "Order Fill Mismatch");

        //perform swap
        (uint256 amountOut, uint256 tokenInRefund) = execute(target, txData, order);

```

This time, the `fillOrder` on the OracleLess has a custom target contract owned by Bob which does two things, sends 1 wei to Bracket.sol, as well as sends 1 wei to OracleLess.sol. 
Since the dummy OracleLess order has no slippage (0), the following checks would all pass: 

```solidity

uint256 finalTokenIn = order.tokenIn.balanceOf(address(this));
        require(finalTokenIn >= initialTokenIn - order.amountIn, "over spend");
        uint256 finalTokenOut = order.tokenOut.balanceOf(address(this));

        require(finalTokenOut - initialTokenOut > order.minAmountOut, "Too Little Received");

amountOut = finalTokenOut - initialTokenOut;
        tokenInRefund = order.amountIn - (initialTokenIn - finalTokenIn);

```
- The 1 wei `tokenIn` amoint from the OracleLess order will be returned to the user as it was never utilized.
- The check for the amountOut will also pass as the malicious contract sent 1 wei to OracleLess, satisfying the requirement. 

The order in Bracket.sol from which the original call to the OracleLess was initiated will also successfully be executed because the malicious contract sent 1 wei to Bracket.sol, so the following conditions will also be true: 

```solidity

 //approve
        tokenIn.safeApprove(target, amountIn);

        //perform the call
        (bool success, bytes memory result) = target.call(txData);

 if (success) {
            uint256 finalTokenIn = tokenIn.balanceOf(address(this));
            require(finalTokenIn >= initialTokenIn - amountIn, "over spend");
            uint256 finalTokenOut = tokenOut.balanceOf(address(this));

            //if success, we expect tokenIn balance to decrease by amountIn
            //and tokenOut balance to increase by at least minAmountReceived
            require(
                finalTokenOut - initialTokenOut > MASTER.getMinAmountReceived(amountIn, tokenIn, tokenOut, bips),
                "Too Little Received"
            );

            swapAmountOut = finalTokenOut - initialTokenOut;
            tokenInRefund = amountIn - (initialTokenIn - finalTokenIn);

```

Since Bob also set a slippage of 0, the 1 wei will cover this and the tokenOut condition will be considered satisfied.
Since the `tokenIn` amount (100_000e6 USDT) was never taken from the Bracket.sol, it will be returned to Bob in its entirety, but at the same time we have an existing approval to OracleLess.sol for an amount of 100K USDT which wasn't spent/decreased.

```solidity

  //approve
        tokenIn.safeApprove(target, amountIn);

```

Next few steps are crucial in order to perform this attack successfully:

- Bob opens a dummy order in Bracket (`tokenIn` amount is the minimum required size) - let's call it order no. 3;
- Bob opens an order on behalf of Bracket.sol in OracleLess, let's call it order no. 4. 

It's plausible to create orders on behalf of other recipients in both OracleLess and StopLimit, so Bob creates an order for 100k USDT in OracleLess and sets the recipient as the Bracket.sol contract: 

```solidity

 function createOrder(
        IERC20 tokenIn,
        IERC20 tokenOut,
        uint256 amountIn,
        uint256 minAmountOut,
        address recipient,
        uint16 feeBips,
        bool permit,
        bytes calldata permitPayload
    ) external override returns (uint96 orderId) {
        //procure tokens
        procureTokens(tokenIn, amountIn, recipient, permit, permitPayload);

        //construct and store order
        orderId = MASTER.generateOrderId(recipient);
        orders[orderId] = Order({
            orderId: orderId,
            tokenIn: tokenIn,
            tokenOut: tokenOut,
            amountIn: amountIn,
            minAmountOut: minAmountOut,
            recipient: recipient,
            feeBips: feeBips
        });

        //store pending order
        pendingOrderIds.push(orderId);

        emit OrderCreated(orderId);
    }

```

Since there was an unused approval of 100k from Bracket.sol to OracleLess, OracleLess will be able to procure the tokens from Bracket.sol (the 100k USDT). 

Now the dummy order created in Bracket.sol (order no.3 with non-existing slippage and immediately executable) is called to be executed, the target contract is again OracleLess, while the txData is a call to `modifyOrder`.
With the `modifyOrder` call, Bob performs two changes to the 100k order (no.4) opened on Behalf of Bracket.sol. Bob decreases the order tokenIn amount by 1 wei, and changes the recipient to themselves. 

```solidity

  function _modifyOrder(
        uint96 orderId,
        IERC20 _tokenOut,
        uint256 amountInDelta,
        uint256 _minAmountOut,
        address _recipient,
        bool increasePosition,
        bool permit,
        bytes calldata permitPayload
    ) internal {
        //fetch order
        Order memory order = orders[orderId];

        require(msg.sender == order.recipient, "only order owner");

        //deduce any amountIn changes
        uint256 newAmountIn = order.amountIn;
        if (amountInDelta != 0) {
            if (increasePosition) {
                //take more tokens from order recipient
                newAmountIn += amountInDelta;
                procureTokens(order.tokenIn, amountInDelta, order.recipient, permit, permitPayload);
            } else {
                //refund some tokens
                //ensure delta is valid
                require(amountInDelta < order.amountIn, "invalid delta");

                //set new amountIn for accounting
                newAmountIn -= amountInDelta;

                //refund position partially
                order.tokenIn.safeTransfer(order.recipient, amountInDelta);
            }
        }

        //construct new order
        Order memory newOrder = Order({
            orderId: orderId,
            tokenIn: order.tokenIn,
            tokenOut: _tokenOut,
            amountIn: newAmountIn,
            minAmountOut: _minAmountOut,
            feeBips: order.feeBips,
            recipient: _recipient
        });

        //store new order
        orders[orderId] = newOrder;
    }

``` 

- By decreasing the order with 1 wei, they will send the 1 wei to the original recipient (Bracket.sol), and with this the condition for the execution of the order no. 3 (the dummy Bracket.sol order) will be satisified, as 1 wei will be more than the 0 tokenOut amount set as slippage. 
- By changing the recipient to Bob, Bob will be able to cancel the order and refund 100K USDT to themselves, effectively stealing /  draining the Bracket.sol balance.

### Internal pre-conditions

1. Bracket.sol never decreases the allowance if the tokenIn amount wasn't utilized in its entirety.
2. Users are able to execute orders with arbitrary target contracts and arbitrary data (including those target contracts being other contracts in the same ecosystem).
3. Slippage checks can also be set by users and can be taken advantage of in this situation.
4. Users are able to open positions on behalf of other addresses (recipients) in OracleLess and StopLimit.

### External pre-conditions

1. This is exploitable by anyone, and since transactions can also be atomic, they can utilize Flash loans as well to drain the Bracket.sol contract.

### Attack Path

1. Bob opens a dummy order in the OracleLess contract (tokenIn amount can be 1 wei for example).
2. Bob opens another immediately executable order of a tokenIn amount 100_000e6 USDT in Bracket.sol;
3. Bob immediately executes their own Bracket.sol order with carefully crafted txData and a target contract which is OracleLess.
4. The malicious txData instructs the OracleLess contract to call `fillOrder` in order to execute the dummy order created in the OracleLess (target contract is a contract owed by Bob, txData is instructions).
5.  The malicious contract will send 1 wei to the Bracket.sol and 1 wei to OracleLess. Slippage is set to 0, so 1 wei from the tokenOut will be enough to cover the slippage check parameters.
6. The initial 100K which was the `tokenIn` amount in the Bracket.sol order is returned to the user as it was never taken. 
7. Since there was no transfer performed, there's an outstanding allowance of 100K from the Bracket.sol to the OracleLess contract.
8. Bob opens another "dummy" order in the Bracket.sol contract.
9. Bob creates another order in OracleLess on behalf of Bracket.sol, i.e. Bracket.sol contract address is set as recipient (remember that users can open orders on behalf of other recipients in OracleLess), tokenIn amount is 100_000e6 USDT (the unused approval from the previous order);
10. Bob executes the "dummy" order opened in Bracket.sol, with malicious txData and the target contract set as OracleLess.
11. With the above transaction Bob calls `modifyOrder` on the OracleLess, and performs two actions, the first one being decreasing the position for 1 wei (which will be sent to Bracket.sol) so that slippage checks can go through, and the second it changes the recipient to Bob.
12. Bob calls cancelOrder on the OracleLess and gets 100_000e6 USDT sent to them. (Since the original amount used was also refunded as it was never taken in), this leaves bob with a profit of 100_000e6 USDT (minus a few wei). 

### Impact

Malicious users are able to drain the Bracket.sol contract utilizing multiple protocol features.

### PoC

_No response_

### Mitigation

- Decrease the allowance if tokenIn amount wasn't used up in its entirety;
- Don't allow for orders to be created on behalf of other recipients;
- Don't allow target contracts and txData to be arbitrary, whitelist a swap protocol and hardcode the swap parameters that should be called on said target contract;