Lively Sand Woodpecker

High

# Bracket.sol::modifyOrder() Attacker can withdraw from a filled order ( "Double Spending" )

### Summary

The `modifyOrder` function permits users to adjust various parameters of an order, including `amountInDelta`. A false `increasePosition` represents a reduction in the order's `amountIn`. Without a check to verify the order's status (filled or unfilled), a user can call modifyOrder on a filled order. This effectively triggers a refund of the `tokenIn` (slightly less), even though the user has already received the equivalent tokenOut upon order fulfillment. This constitutes a double-spending attack.
 
 **Note**  `amountDelta` has to be above `checkMinOrderSize()` threshold.
 
 ```solidity 

 function modifyOrder(
        uint96 orderId,
        uint256 _takeProfit,
        uint256 _stopPrice,
        uint256 amountInDelta,
        IERC20 _tokenOut,
        address _recipient,
        uint16 _takeProfitSlippage,
        uint16 _stopSlippage,
        bool permit,
        bool increasePosition,
        bytes calldata permitPayload
    ) external override nonReentrant {
        //get order
        Order memory order = orders[orderId];

        //only order owner
        require(msg.sender == order.recipient, "only order owner");

        //deduce any amountIn changes
        uint256 newAmountIn = order.amountIn;
        if (amountInDelta != 0) {
            if (increasePosition) {
                newAmountIn += amountInDelta;
                //take funds via permit2
                if (permit) {
                    handlePermit(
                        order.recipient,
                        permitPayload,
                        uint160(amountInDelta),
                        address(order.tokenIn)
                    );
                } else {
                    //legacy transfer, assume prior approval
                    order.tokenIn.safeTransferFrom(
                        order.recipient,
                        address(this),
                        amountInDelta
                    );
                }
            } else {
                //ensure delta is valid
                require(amountInDelta < order.amountIn, "invalid delta");

                //set new amountIn for accounting
                newAmountIn -= amountInDelta;

                //check min order size for new amount
                MASTER.checkMinOrderSize(order.tokenIn, newAmountIn);

                //refund position partially
                order.tokenIn.safeTransfer(order.recipient, amountInDelta);
            }
        }

        //check for oracles
        if (_tokenOut != order.tokenOut) {
            require(
                address(MASTER.oracles(_tokenOut)) != address(0x0),
                "Oracle !exist"
            );
        }

        //construct new order
        Order memory newOrder = Order({
            orderId: orderId,
            takeProfit: _takeProfit,
            stopPrice: _stopPrice,
            amountIn: newAmountIn,
            tokenIn: order.tokenIn,
            tokenOut: _tokenOut,
            feeBips: order.feeBips,
            takeProfitSlippage: _takeProfitSlippage, // @audit does not check takeProfitSlippage <= 1000
            stopSlippage: _stopSlippage,
            recipient: _recipient,
            direction: MASTER.getExchangeRate(order.tokenIn, _tokenOut) >
                _takeProfit
        });

        //store new order
        orders[orderId] = newOrder;
    } 

 ```

### Root Cause

- Improper check to see if the order is still valid or has already been filled.
https://github.com/sherlock-audit/2024-11-oku/blob/main/oku-custom-order-types/contracts/automatedTrigger/Bracket.sol#L256-L269

### Internal pre-conditions

- Attacker has to place  an order on the OracleLess contract.
- Attacker sets the increasePosition parameter in the modifyOrder function is set to false.

### External pre-conditions

- None.

### Attack Path

1. A attacker places a legitimate order on the OracleLess contract.
2. The order is successfully filled, and the user receives the expected tokenOut.
3. The user then calls the modifyOrder function with increasePosition set to false and amountInDelta just high enough to to pass `checkMinOrderSize`
4. Attacker performs steps 1-> 3 over and over again.

### Impact

- The `OracleLess` contract suffers a loss of funds nearly equal to the `tokenIn` amount of any filled order where a malicious actor calls `modifyOrder` with `increasePosition` set to `false`. The attacker gains the `tokenIn` back, effectively double-spending.  The total loss is directly proportional to the number of filled orders exploited in this manner.

### PoC

_No response_

### Mitigation

A check to see if the order is in the `pendingOrderIds` array should be implemented.