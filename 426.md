Bright Ultraviolet Starfish

High

# Exploit `cancelOrder()` with re-entrancy to steal funds

### Summary

_No response_

### Root Cause

- Lack of re-entrancy guards in critical functions such as `cancelOrder`, `modifyOrder` and `createOrder` functions

### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

Assume that the `pendingOrderIds` array contains the following two (2) orders:

1. Index 0 = Order {receipent = Bob, tokenIn = USDC, amountIn = 3000 USDC, tokenOut = WETH}
2. Index 1 = Order {receipent = Alice, tokenIn = USDC, amountIn = 3000 USDC, tokenOut = WETH}

The total USDC on the `OracleLess` contract is 6000 USDC. At this point, assume the price of WETH is 3000 USDC per WETH. Bob's wallet now has 0 USDC.

The Bob (malicious user) executes the `fillOrder` function. Within the `fillOrder` function, the `execute` function at Line 118 will pass the control back to Bob before the `pendingOrderIds` array is updated to remove Bob's order at Line 126 below. The `execute` function allows the caller to specify an [arbitrary target](https://github.com/sherlock-audit/2024-11-oku/blob/main/oku-custom-order-types/contracts/automatedTrigger/OracleLess.sol#L240). Thus, Bob can point the `target` to a malicious DEX controlled by Bob.

Note that at this point, Bob's order still exists within the `pendingOrderIds` array.

https://github.com/sherlock-audit/2024-11-oku/blob/main/oku-custom-order-types/contracts/automatedTrigger/OracleLess.sol#L103

```solidity
File: OracleLess.sol
103:     function fillOrder(
104:         uint96 pendingOrderIdx,
105:         uint96 orderId,
106:         address target,
107:         bytes calldata txData
108:     ) external override {
109:         //fetch order
110:         Order memory order = orders[orderId];
111: 
112:         require(
113:             order.orderId == pendingOrderIds[pendingOrderIdx],
114:             "Order Fill Mismatch"
115:         );
116: 
117:         //perform swap
118:         (uint256 amountOut, uint256 tokenInRefund) = execute(
119:             target,
120:             txData,
121:             order
122:         );
123: 
124:         //handle accounting
125:         //remove from array
126:         pendingOrderIds = ArrayMutation.removeFromArray(
127:             pendingOrderIdx,
128:             pendingOrderIds
129:         );
```

When the Bob gains control, the Bob proceeds to re-enter the `OracleLess` contract and execute the `cancelOrder` function. This is possible because the `cancelOrder` function is not protected by the re-entrancy guard (`nonReentrant` modifier).

https://github.com/sherlock-audit/2024-11-oku/blob/main/oku-custom-order-types/contracts/automatedTrigger/OracleLess.sol#L74

```solidity
File: OracleLess.sol
74:     function cancelOrder(uint96 orderId) external override {
75:         Order memory order = orders[orderId];
76:         require(msg.sender == order.recipient, "Only Order Owner");
77:         require(_cancelOrder(order), "Order not active");
78:     }
```

When the `cancelOrder` function is executed, it will loop through all the orders within the `pendingOrderIds` array, and Bob's order will be found at index 0 of the `pendingOrderIds` array. At Line 154 below, it will proceed to delete Bob's order from the `pendingOrderIds` array. After the deletion, the `pendingOrderIds` array only contains Alice's order, and Alice's order is positioned at index 0 of the `pendingOrderIds` array.

In Line 160 below, the `cancelOrder` function will then transfer 3000 USDC to Bob. The total USDC on the `OracleLess` contract is 3000 USDC. Bob's wallet now has 3000 USDC.

Note: `modifyOrder` function can also be utilized here instead of `cancelOrder` function. The effect will be the same.

https://github.com/sherlock-audit/2024-11-oku/blob/main/oku-custom-order-types/contracts/automatedTrigger/OracleLess.sol#L160

```solidity
File: OracleLess.sol
150:     function _cancelOrder(Order memory order) internal returns (bool) {
151:         for (uint96 i = 0; i < pendingOrderIds.length; i++) {
152:             if (pendingOrderIds[i] == order.orderId) {
153:                 //remove from pending array
154:                 pendingOrderIds = ArrayMutation.removeFromArray(
155:                     i,
156:                     pendingOrderIds
157:                 );
158: 
159:                 //refund tokenIn amountIn to recipient
160:                 order.tokenIn.safeTransfer(order.recipient, order.amountIn);
..SNIP..
165:                 return true;
166:             }
167:         }
168:         return false;
169:     }
```

While the control is still on Bob's side, Bob's malicioius DEX will pull an additional 3000 USDC from `Oracleless` contract due to [the allowance granted](https://github.com/sherlock-audit/2024-11-oku/blob/main/oku-custom-order-types/contracts/automatedTrigger/OracleLess.sol#L237) at Line 237 below, swap it for 1 WETH on external DEXs, and transfer 1 WETH to the `OracleLess` contract. 

Currently, the total USDC on the `OracleLess` contract is 0 USDC, and the `require` check at Line 247 below will fail because the `finalTokenIn` (0 USDC) will be less than `initialTokenIn - order.amountIn` (6000 - 3000 = 3000). 

To ensure that this `require` check does not revert, Bob will create a temporary order and deposit 3000 USDC he obtained from canceling the order earlier as `tokenIn`. Fortunately, [`OracleLess.createOrder`](https://github.com/sherlock-audit/2024-11-oku/blob/main/oku-custom-order-types/contracts/automatedTrigger/OracleLess.sol#L38) function is also not protected by re-entrancy guard. This action will cause the `finalTokenIn` to be 3000 USDC, ensuring the `require` check does not revert. Bob's wallet has 0 USDC now.

https://github.com/sherlock-audit/2024-11-oku/blob/main/oku-custom-order-types/contracts/automatedTrigger/OracleLess.sol#L227

```solidity
File: OracleLess.sol
227:     function execute(
228:         address target,
229:         bytes calldata txData,
230:         Order memory order
231:     ) internal returns (uint256 amountOut, uint256 tokenInRefund) {
232:         //update accounting
233:         uint256 initialTokenIn = order.tokenIn.balanceOf(address(this));
234:         uint256 initialTokenOut = order.tokenOut.balanceOf(address(this));
235: 
236:         //approve
237:         order.tokenIn.safeApprove(target, order.amountIn);
238: 
239:         //perform the call
240:         (bool success, bytes memory reason) = target.call(txData);
241: 
242:         if (!success) {
243:             revert TransactionFailed(reason);
244:         }
245: 
246:         uint256 finalTokenIn = order.tokenIn.balanceOf(address(this));
247:         require(finalTokenIn >= initialTokenIn - order.amountIn, "over spend");
248:         uint256 finalTokenOut = order.tokenOut.balanceOf(address(this));
249: 
250:         require(
251:             finalTokenOut - initialTokenOut > order.minAmountOut,
252:             "Too Little Received"
253:         );
..SNIP..
```

When the `execute` transaction is completed, the `ArrayMutation.removeFromArray` function at Line 126 will be executed with `pendingOrderIdx = 0`. Since Alice's order is at position 0, her order will be deleted. Note that Alice's order being deleted here is also another valid issue because Alice's order has been deleted, and she won't be able to recover his funds within her order, which is considered a loss of assets.

https://github.com/sherlock-audit/2024-11-oku/blob/main/oku-custom-order-types/contracts/automatedTrigger/OracleLess.sol#L126

```solidity
File: OracleLess.sol
103:     function fillOrder(
104:         uint96 pendingOrderIdx,
105:         uint96 orderId,
106:         address target,
107:         bytes calldata txData
108:     ) external override {
109:         //fetch order
110:         Order memory order = orders[orderId];
111: 
112:         require(
113:             order.orderId == pendingOrderIds[pendingOrderIdx],
114:             "Order Fill Mismatch"
115:         );
116: 
117:         //perform swap
118:         (uint256 amountOut, uint256 tokenInRefund) = execute(
119:             target,
120:             txData,
121:             order
122:         );
123: 
124:         //handle accounting
125:         //remove from array
126:         pendingOrderIds = ArrayMutation.removeFromArray(
127:             pendingOrderIdx,
128:             pendingOrderIds
129:         );
130: 
131:         //handle fee
132:         (uint256 feeAmount, uint256 adjustedAmount) = applyFee(
133:             amountOut,
134:             order.feeBips
135:         );
136:         if (feeAmount != 0) {
137:             order.tokenOut.safeTransfer(address(MASTER), feeAmount);
138:         }
139: 
140:         //send tokenOut to recipient
141:         order.tokenOut.safeTransfer(order.recipient, adjustedAmount);
142: 
143:         //refund any unspent tokenIn
144:         //this should generally be 0 when using exact input for swaps, which is recommended
145:         if (tokenInRefund != 0) {
146:             order.tokenIn.safeTransfer(order.recipient, tokenInRefund);
147:         }
148:     }
```

At Line 141 above, 1 WETH will be sent to Bob. After the `fillOrder` transaction is executed, Bob proceeds to cancel his temporary order to get back his 3000 USDC that he locked earlier. At this point, Bob's wallet has 3000 USDC and 1 WETH (Total value of 6000 USD).

At the start, Bob had only 3000 USD worth of assets. At the end of the attack, Bob had 6000 USD worth of assets. He had stolen 3000 USD from other users.

In summary, there are two (2) main issues described in this report:

1. Bob steals all the USDC on the `OracleLess` contract
2. Alice's order has been deleted due to the bugs, causing her to lose all the funds within her order.

### Impact

High. Loss of assets.

### PoC

_No response_

### Mitigation

Add re-entrancy guards (`nonReentrant` modifier) to the following functions:

- In `OracleLess` contract, `cancelOrder`, `modifyOrder` and `createOrder` functions
- In `Bracket` contract, `cancelOrder` function
- In `StopLimit` contract, `cancelOrder` function