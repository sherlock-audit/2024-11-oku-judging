Blurry Slate Llama

High

# Attacker can drain contract via reentrancy in OracleLess.fillOrder

## Summary

`OracleLess.fillOrder` does not follow the CEI pattern, which makes it possible to reenter before the pending order ID is removed from `pendingOrderIds`, allowing the attacker to repeat swaps and fund transfers until the contract balance is fully depleted.


## Vulnerability Detail

Consider the scenario:

1. An attacker creates an order at OracleLess contract with the position favourable for immidiate swap.
2. An attacker executes `fillOrder` setting `target` to the own attack contract.
3. The internal `execute` function calls the attack contract previously granting it token spending allowance:

```solidity
    //approve
    order.tokenIn.safeApprove(target, order.amountIn);

    //perform the call
    (bool success, bytes memory reason) = target.call(txData);
```

4. The attack contract receives the call, transfers tokens and performs the swap, finally sending swapped out token back to the OracleLess contract.
5. However, before returning the code flow control, the attack contract calls `execute` again effectively reentering it and forcing it to initiate the swap again. This is possible because the order ID has not yet been removed from `pendingOrderIds`:

```solidity
    function fillOrder(
        uint96 pendingOrderIdx,
        uint96 orderId,
        address target,
        bytes calldata txData
    ) external override {
        //fetch order
        Order memory order = orders[orderId];

        require(
            order.orderId == pendingOrderIds[pendingOrderIdx],
            "Order Fill Mismatch"
        );

        //perform swap
>>>     (uint256 amountOut, uint256 tokenInRefund) = execute( // @audit reentrancy point
            target,
            txData,
            order
        );

        //handle accounting
        //remove from array
        pendingOrderIds = ArrayMutation.removeFromArray(
            pendingOrderIdx,
            pendingOrderIds
        );

        // ... rest of the code removed ...
    }
```

As a result, an attacker swaps out tokens again until the contract balance is completely depleted.

## Impact

Funds stoled, the OracleLess contract becomes insolvent.

## Code Snippet

https://github.com/sherlock-audit/2024-11-oku/blob/main/oku-custom-order-types/contracts/automatedTrigger/OracleLess.sol#L110-L129

https://github.com/sherlock-audit/2024-11-oku/blob/main/oku-custom-order-types/contracts/automatedTrigger/OracleLess.sol#L237-L240


## Recommendation

Follow the CEI pattern, and move order ID removal before the external call:

```solidity
    function fillOrder(
        uint96 pendingOrderIdx,
        uint96 orderId,
        address target,
        bytes calldata txData
    ) external override {
        //fetch order
        Order memory order = orders[orderId];

        require(
            order.orderId == pendingOrderIds[pendingOrderIdx],
            "Order Fill Mismatch"
        );

+++     //handle accounting
+++     //remove from array
+++     pendingOrderIds = ArrayMutation.removeFromArray(
+++         pendingOrderIdx,
+++         pendingOrderIds
+++     );

        //perform swap
        (uint256 amountOut, uint256 tokenInRefund) = execute(
            target,
            txData,
            order
        );

---     //handle accounting
---     //remove from array
---     pendingOrderIds = ArrayMutation.removeFromArray(
---         pendingOrderIdx,
---         pendingOrderIds
---     );

        // ... rest of the code removed ...
    }
```