Warm Pear Hyena

Medium

# Oracleless orders can be created on behalf of other users outside of permit signatures

## Sumary
`Oracleless` orders can be created on behalf of legitimate users who have interacted with the protocol and granted unused approval e.g infinite approval.

## Vulnerability Details
Users create `Oracleless` orders by calling [OracleLess::createOrder()](https://github.com/sherlock-audit/2024-11-oku/blob/ee3f781a73d65e33fb452c9a44eb1337c5cfdbd6/oku-custom-order-types/contracts/automatedTrigger/OracleLess.sol#L38-L67). 

While orders can be created on behalf of others using permit signed signatures, these orders can also be created on behalf of others if they have previously granted the contract any extra approval. This is due to the code used for fetching the tokens in [OracleLess::procureTokens()](https://github.com/sherlock-audit/2024-11-oku/blob/ee3f781a73d65e33fb452c9a44eb1337c5cfdbd6/oku-custom-order-types/contracts/automatedTrigger/OracleLess.sol#L259-L282):

```solidity
    function procureTokens(
        IERC20 token,
        uint256 amount,
        address owner,
        bool permit,
        bytes calldata permitPayload
    ) internal {
        if (permit) {
            IAutomation.Permit2Payload memory payload = abi.decode(
                permitPayload,
                (IAutomation.Permit2Payload)
            );

            permit2.permit(owner, payload.permitSingle, payload.signature);
            permit2.transferFrom(
                owner,
                address(this),
                uint160(amount),
                address(token)
            );
        } else {
            token.safeTransferFrom(owner, address(this), amount);    // @audit
        }
    }
```

Within the `else` block, the passed `owner` is also used to transfer tokens, in the previous calling function `createOrder` this value is the `recipient` of the order:

```solidity
    function createOrder(
        IERC20 tokenIn,
        IERC20 tokenOut,
        uint256 amountIn,
        uint256 minAmountOut,
        address recipient,
        uint16 feeBips,
        bool permit,
        bytes calldata permitPayload
    ) external override returns (uint96 orderId) {
        //procure tokens
        procureTokens(tokenIn, amountIn, recipient, permit, permitPayload);

        ...
    }
```

If this recipient has granted unused or infinite approval to the contract, any other user or attacker can create an order on behalf of the recipient even without a permit signature.

## Impact
Anyone can can create orders for other users which withdraws from the unsuspecting user's token balances.

## Recommendation
If not using permit, withdraw tokens from the `msg.sender` and not `recipient`:

```solidity
    function procureTokens(
        IERC20 token,
        uint256 amount,
        address owner,
        bool permit,
        bytes calldata permitPayload
    ) internal {
        if (permit) {
            ...
        } else {
            token.safeTransferFrom(msg.sender, address(this), amount);    // @audit-fix
        }
    }
```