Melted Sky Tiger

High

# Order performers can drain funds from contracts

### Summary

We miss revoking the approval in `execute()`, this will cause that order performers will drain funds from the contracts.

### Root Cause

In [Bracket::execute()](https://github.com/sherlock-audit/2024-11-oku/blob/main/oku-custom-order-types/contracts/automatedTrigger/Bracket.sol#L526), when one order is in one range, performers can perform this order.

In this execute(), the performer's expected behavior is to take the `amountIn` `tokenIn` token and do some swap and transfer the expected `tokenOut` back to the contract. If there is some left `tokenIn`, we will refund to the order owner.

The problem is that we miss the approval revoke in execute() after we finish the swap. Malicious users can make use of this valid approval to transfer tokens out of the contracts after we fill the order.

For example:
1. Alice creates one order. e.g. tokenIn: WETH(amountIn: 1e18), tokenOut: USDC, current price: 3000, stopPrice:3200, takeProfit:3200. We wish this order can be performed immediately.
2. Alice performs her own order. In this `execution()`, the contract will approve 1e18 WETH to the Alice's contract `target`.
3. In Alice's contract `target`, we don't use this WETH's approval, and transfer the expected USDC to the contracts, e.g. 3000 USDC.
4. The 3000 USDC will be transferred to the order's owner Alice, and Alice will get back 1e18 WETH because the 1e18 WETH is not used and is refunded back to her.
5. After Alice performs this order, Alice can make use of her contract `target` to transfer 1e18 WETH from the Bracket. Because the WETH approval is still valid.

```solidity
    function execute(
        address target,
        bytes memory txData,
        uint256 amountIn,
        IERC20 tokenIn,
        IERC20 tokenOut,
        uint16 bips
    ) internal returns (uint256 swapAmountOut, uint256 tokenInRefund) {
        uint256 initialTokenIn = tokenIn.balanceOf(address(this));
        uint256 initialTokenOut = tokenOut.balanceOf(address(this));
@>        tokenIn.safeApprove(target, amountIn);
@>        (bool success, bytes memory result) = target.call(txData);

        if (success) {
            uint256 finalTokenIn = tokenIn.balanceOf(address(this));
            require(finalTokenIn >= initialTokenIn - amountIn, "over spend");
            uint256 finalTokenOut = tokenOut.balanceOf(address(this));
            require(
                finalTokenOut - initialTokenOut >
                    MASTER.getMinAmountReceived(
                        amountIn,
                        tokenIn,
                        tokenOut,
                        bips
                    ),
                "Too Little Received"
            );

            swapAmountOut = finalTokenOut - initialTokenOut;
@>            tokenInRefund = amountIn - (initialTokenIn - finalTokenIn);
        } else {
           ...
        }
    }

```
```solidity
    function performUpkeep(
        bytes calldata performData
    ) external override nonReentrant {
        ...
        uint16 bips;
        takeProfit ? bips = order.takeProfitSlippage : bips = order
            .stopSlippage;
        (uint256 swapAmountOut, uint256 tokenInRefund) = execute(
            data.target,
            data.txData,
            order.amountIn,
            order.tokenIn,
            order.tokenOut,
            bips
        );

        pendingOrderIds = ArrayMutation.removeFromArray(
            data.pendingOrderIdx,
            pendingOrderIds
        );
        ...

@>        order.tokenOut.safeTransfer(order.recipient, adjustedAmount);
        if (tokenInRefund != 0) {
@>            order.tokenIn.safeTransfer(order.recipient, tokenInRefund);
        }
    }
```

### Internal pre-conditions

N/A

### External pre-conditions

N/A

### Attack Path

1. Alice creates one order. e.g. tokenIn: WETH(amountIn: 1e18), tokenOut: USDC, current price: 3000, stopPrice:3200, takeProfit:3200. We wish this order can be performed immediately.
2. Alice performs her own order. In this `execution()`, the contract will approve 1e18 WETH to the Alice's contract `target`.
3. In Alice's contract `target`, we don't use this WETH's approval, and transfer the expected USDC to the contracts, e.g. 3000 USDC.
4. The 3000 USDC will be transferred to the order's owner Alice, and Alice will get back 1e18 WETH because the 1e18 WETH is not used and is refunded back to her.
5. After Alice performs this order, Alice can make use of her contract `target` to transfer 1e18 WETH from the Bracket. Because the WETH approval is still valid.

### Impact

Malicious users can drain all funds in the contract.

### PoC

N/A

### Mitigation

In execute(), we should revoke the approval after we finishes the swap.