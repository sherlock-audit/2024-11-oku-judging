Soaring Lime Gecko

High

# An attacker can create dead orders to DoS system.

### Summary

An attacker can create orders that will never be executed and can never be canceled. These "dead" orders accumulate in the system, filling up the `maxPendingOrders` limit, which results in a DoS attack on the system.

### Root Cause

The system has a maximum pending order limit. 
By setting the `takeProfit` to its maximum value and the `stopPrice` to 0, or by passing a slippage parameter greater than 100% when modifying an order, these orders can never be executed. 
To prevent the administrator from canceling these orders, an attacker can modify the order information and set the receipt address to `address(0)`. 
https://github.com/sherlock-audit/2024-11-oku/blob/ee3f781a73d65e33fb452c9a44eb1337c5cfdbd6/oku-custom-order-types/contracts/automatedTrigger/Bracket.sol#L216
```solidity
    function modifyOrder(
        uint96 orderId,
        uint256 _takeProfit,
        uint256 _stopPrice,
        uint256 amountInDelta,
        IERC20 _tokenOut,
        address _recipient,
        uint16 _takeProfitSlippage,
        uint16 _stopSlippage,
        bool permit,
        bool increasePosition,
        bytes calldata permitPayload
    ) external override nonReentrant {
        //get order
        Order memory order = orders[orderId];

        //only order owner
        require(msg.sender == order.recipient, "only order owner");

        //deduce any amountIn changes
        uint256 newAmountIn = order.amountIn;
        if (amountInDelta != 0) {
            if (increasePosition) {
                newAmountIn += amountInDelta;
                //take funds via permit2
                if (permit) {
                    handlePermit(
                        order.recipient,
                        permitPayload,
                        uint160(amountInDelta),
                        address(order.tokenIn)
                    );
                } else {
                    //legacy transfer, assume prior approval
                    order.tokenIn.safeTransferFrom(
                        order.recipient,
                        address(this),
                        amountInDelta
                    );
                }
            } else {
                //ensure delta is valid
                require(amountInDelta < order.amountIn, "invalid delta");

                //set new amountIn for accounting
                newAmountIn -= amountInDelta;

                //check min order size for new amount
                MASTER.checkMinOrderSize(order.tokenIn, newAmountIn);

                //refund position partially
                order.tokenIn.safeTransfer(order.recipient, amountInDelta);
            }
        }

        //check for oracles
        if (_tokenOut != order.tokenOut) {
            require(
                address(MASTER.oracles(_tokenOut)) != address(0x0),
                "Oracle !exist"
            );
        }

        //construct new order
        Order memory newOrder = Order({
            orderId: orderId,
            takeProfit: _takeProfit,
            stopPrice: _stopPrice,
            amountIn: newAmountIn,
            tokenIn: order.tokenIn,
            tokenOut: _tokenOut,
            feeBips: order.feeBips,
            takeProfitSlippage: _takeProfitSlippage,
            stopSlippage: _stopSlippage,
            recipient: _recipient,
            direction: MASTER.getExchangeRate(order.tokenIn, _tokenOut) >
                _takeProfit
        });

        //store new order
        orders[orderId] = newOrder;
    }
```
When the administrator attempts to cancel these orders, the transaction will revert because ERC20 tokens do not support transfers to the zero address. As a result, these orders will never be executed or canceled. The attacker can create such orders to perform a DoS attack on the system.

### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

1. The attacker creates a small order with a takeProfit and stopPrice that are unreachable.
2. The attacker then calls `modifyOrder` to modify the order details, setting the slippage parameter to a value greater than 100%, and sets the receipt address to `address(0)`. This causes the order to permanently remain in the system, unable to be executed or canceled.
3. The attacker repeats this process until the system reaches the maximum pending order limit, causing a DoS.

### Impact

The system will be subject to a DoS.
Especially in the `OracleLess` contract does not impose a minimum order size limit, the attack cost for the malicious actor is minimized. This allows them to quickly increase the number of "dead orders," eventually causing the system to run out of gas.

### PoC

_No response_

### Mitigation

Check the slippage parameter range in the `modifyOrder` function, and verify that the receipt is not `address(0)` or an address in the token system's blacklist.