Beautiful Tangerine Panther

High

# Re-Entrancy Vulnerability in `execute` Function: Exploit Allows Order Manipulation

## Summary

The [`execute`](https://github.com/sherlock-audit/2024-11-oku/blob/ee3f781a73d65e33fb452c9a44eb1337c5cfdbd6/oku-custom-order-types/contracts/automatedTrigger/OracleLess.sol#L227) function, called within the [`fillOrder`](https://github.com/sherlock-audit/2024-11-oku/blob/ee3f781a73d65e33fb452c9a44eb1337c5cfdbd6/oku-custom-order-types/contracts/automatedTrigger/OracleLess.sol#L103) function, is vulnerable to re-entrancy attacks due to an external call ([`target.call(txData)`](https://github.com/sherlock-audit/2024-11-oku/blob/ee3f781a73d65e33fb452c9a44eb1337c5cfdbd6/oku-custom-order-types/contracts/automatedTrigger/OracleLess.sol#L240)) made before critical state variables are updated. This issue allows a malicious actor to manipulate pending orders by canceling the existing order and re-creating a new order with the same token amount mid-execution, thereby bypassing balance checks. This exploit can lead to  unintended execution outcomes, draining tokens from the contract.

The absence of a re-entrancy guard makes this vulnerability exploitable.
---

## Vulnerability Details

The `fillOrder` function makes a call to `execute` without first removing the order ID from the `pendingOrderIds` array. 

```solidity

    function fillOrder(
        uint96 pendingOrderIdx,
        uint96 orderId,
        address target,
        bytes calldata txData
    ) external override {
        //fetch order
        Order memory order = orders[orderId];

        require(
            order.orderId == pendingOrderIds[pendingOrderIdx],
            "Order Fill Mismatch"
        );

        //perform swap
     @>    (uint256 amountOut, uint256 tokenInRefund) = execute(
            target,
            txData,
            order
        );

        //handle accounting
        //remove from array
     @>    pendingOrderIds = ArrayMutation.removeFromArray(
            pendingOrderIdx,
            pendingOrderIds
        );

        //handle fee
        (uint256 feeAmount, uint256 adjustedAmount) = applyFee(
            amountOut,
            order.feeBips
        );
        if (feeAmount != 0) {
            order.tokenOut.safeTransfer(address(MASTER), feeAmount);
        }

        //send tokenOut to recipient
        order.tokenOut.safeTransfer(order.recipient, adjustedAmount);

        //refund any unspent tokenIn
        //this should generally be 0 when using exact input for swaps, which is recommended
        if (tokenInRefund != 0) {
            order.tokenIn.safeTransfer(order.recipient, tokenInRefund);
        }
    }
```



The external call ([`target.call(txData)`](https://github.com/sherlock-audit/2024-11-oku/blob/ee3f781a73d65e33fb452c9a44eb1337c5cfdbd6/oku-custom-order-types/contracts/automatedTrigger/OracleLess.sol#L240)) within `execute` can trigger re-entrant behavior, allowing an attacker to exploit by :


1. **Cancel the Order**:
   - During the fallback, call `_cancelOrder`, refunding the `amountIn` tokens to the attacker.

2. **Recreate the Order**:
   - Create a new order with the refunded `amountIn` tokens. This manipulates the state to pass the balance checks in `execute`.

3. **Drain Tokens or Exploit Logic**:
   - Using this method, attackers can bypass restrictions or manipulate the state, potentially draining tokens.

---

## Internal Pre-conditions
N/A

## External Pre-conditions
1. The attacker must have an active order in the system that is eligible for execution.
2. The attacker must control a  contract with a malicious fallback function.

---

## Proof of Concept

1. **Initial State**:
   - Assume an order is created with:
     - `orderId = 8`
     - `amountIn = 10 TOKEN_A`
     - `minAmountOut = 15 TOKEN_B`

2. **fillOrder Function**:
   -  `fillOrder` is Called, which calls `execute`.

3. **During `target.call(txData)`**:
   - The attacker's fallback function calls `_cancelOrder`, removing `orderId` and refunding `amountIn` (10 TOKEN_A) to the attacker.
   - Immediately after, the attacker creates a new order with the same `amountIn`.

4. **Post External Call**:
   - The `execute` function continues, with `finalTokenIn` and `initialTokenIn` balance checks passing due to the recreated order.

---

## Impact
This vulnerability allows an attacker to manipulate order states mid-execution, bypass balance checks, and potentially drain tokens from the contract. The attack can exploit the contract logic to claim tokens multiple times or execute unintended operations.

Additionally after `execute` function is passed `fillOrder` delete the `pendingOrderIdx` from `pendingOrderIds` which removes the orderId of some other users causing a `Denial of Service(DOS)` for them.
---

## Recommendation
To mitigate this issue:

1. **Use `ReentrancyGuard`**:
   - Add a `nonReentrant` modifier to the `execute`, `_cancelOrder`, and other critical functions.
   - Example:
     ```solidity
     function execute(
         address target,
         bytes calldata txData,
         Order memory order
     ) internal nonReentrant returns (uint256 amountOut, uint256 tokenInRefund) {
         // Existing implementation remains unchanged
     }
     ```

By implementing a re-entrancy guard, the contract will be protected against this vulnerability, ensuring safe execution of orders.