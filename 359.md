Loud Lace Porpoise

High

# The `execute()` function should reset the approved amount for the `target` to 0 at the end.

### Summary

The `execute()` function does not reset the approved amount for the `target` to 0, which gives attackers an opportunity to drain the contract.

### Root Cause

The [execute()](https://github.com/sherlock-audit/2024-11-oku/blob/main/oku-custom-order-types/contracts/automatedTrigger/OracleLess.sol#L237) function approves `amountIn` for the `target`. However, after the transaction is completed, it does not reset the approved amount to 0. If there is a remaining approved amount, attackers can reuse it to drain the contract.

```solidity
    function execute(
        address target,
        bytes calldata txData,
        Order memory order
    ) internal returns (uint256 amountOut, uint256 tokenInRefund) {
        //update accounting
        uint256 initialTokenIn = order.tokenIn.balanceOf(address(this));
        uint256 initialTokenOut = order.tokenOut.balanceOf(address(this));

        //approve
@>      order.tokenIn.safeApprove(target, order.amountIn);

        //perform the call
        (bool success, bytes memory reason) = target.call(txData);

        if (!success) {
            revert TransactionFailed(reason);
        }

        uint256 finalTokenIn = order.tokenIn.balanceOf(address(this));
        require(finalTokenIn >= initialTokenIn - order.amountIn, "over spend");
        uint256 finalTokenOut = order.tokenOut.balanceOf(address(this));

        require(
            finalTokenOut - initialTokenOut > order.minAmountOut,
            "Too Little Received"
        );

        amountOut = finalTokenOut - initialTokenOut;
        tokenInRefund = order.amountIn - (initialTokenIn - finalTokenIn);
    }
```

### Internal pre-conditions

### External pre-conditions

### Attack Path

Let's consider the following scenario:

1. Alice, the attacker, creates an order in the `OracleLess` contract:

    - `amountIn`: 100
    - `amountOut`: 1
2. Alice executes her order, setting a malicious `target` she created:

    - 100 `tokenIn` is approved for the target.
    - The `target` swaps only 1 `tokenIn` for 1 `tokenOut`.
    - The approved amount is decremented to 99 but not reset to 0.
    - The unused 99 `tokenIn` is refunded to Alice.
3. Alice then calls the `target` to transfer the approved 99 `tokenIn` to her address.

As a result, Alice can unfairly acquire 99 `tokenIn`.

### Impact

Attackers can drain the `OracleLess` contract.

### PoC

### Mitigation

Reset the approved amount to 0 at the end.

```diff
    function execute(
        address target,
        bytes calldata txData,
        Order memory order
    ) internal returns (uint256 amountOut, uint256 tokenInRefund) {
        //update accounting
        uint256 initialTokenIn = order.tokenIn.balanceOf(address(this));
        uint256 initialTokenOut = order.tokenOut.balanceOf(address(this));

        //approve
        order.tokenIn.safeApprove(target, order.amountIn);

        //perform the call
        (bool success, bytes memory reason) = target.call(txData);

        if (!success) {
            revert TransactionFailed(reason);
        }

        uint256 finalTokenIn = order.tokenIn.balanceOf(address(this));
        require(finalTokenIn >= initialTokenIn - order.amountIn, "over spend");
        uint256 finalTokenOut = order.tokenOut.balanceOf(address(this));

        require(
            finalTokenOut - initialTokenOut > order.minAmountOut,
            "Too Little Received"
        );

        amountOut = finalTokenOut - initialTokenOut;
        tokenInRefund = order.amountIn - (initialTokenIn - finalTokenIn);

+       order.tokenIn.safeApprove(target, 0);
    }
```